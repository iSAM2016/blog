---
title: 汇编学习-第十五章
date: 2021-09-30 14:29:57
tags:
categories:
-汇编
---


# 外中断

CPU在计算机系统中，除了能够执行指令，进行运算以外，还应该能够对外部设备进行控制，接收它们的输入，向它们进行输出（I/O能力）

PC系统的接口卡和主板上，装有各种接口芯片。这些外设接口芯片的内部有若干寄存器，CPU将这些寄存器当作端口来访问

外设的输入不直接送入内存和CPU，而是送入相关的接口芯片的端口中；

CPU向外设的输出也不是直接送入外设，而是先送入端口中，再由相关的芯片送到外设。

CPU还可以向外设输出控制命令，而这些控制命令也是先送到相关芯片的端口中，然后再由相关的芯片根据命令对外设实施控制。

即：CPU通过端口和外部设备进行联系

当CPU外部有需要处理的事情发生的时候，比如说，外设的输入到达，相关芯片将向CPU发出相应的中断信息。CPU在执行完当前指令后，可以检测到发送过来的中断信息，引发中断过程，处理外设的输入。

PC系统中，外中断源有两类

## 可屏蔽中断

可屏蔽中断是CPU可以不响应的外中断。CPU是否响应可屏蔽中断，要看标志寄存器的IF位的设置。

当CPU检测到可屏蔽中断信息时，如果IF=1，则CPU在执行完当前指令后响应中断，引发中断过程；如果IF=0，则不响应可屏蔽中断。

可屏蔽中断信息来自于CPU外部，中断类型码是通过数据总线送入CPU的；而内中断的中断类型码是在CPU内部产生的。

中断过程中将IF置0的原因就是，在进入中断处理程序后，禁止其他的可屏蔽中断。如果在中断处理程序中需要处理可屏蔽中断，可以用指令将IF置1。

8086CPU提供的设置IF的指令：sti，设置IF=1；cli，设置IF=0。

## 不可屏蔽中断

不可屏蔽中断是CPU必须响应的外中断。当CPU检测到不可屏蔽中断信息时，则在执行完当前指令后，立即响应，引发中断过程。

对于8086CPU，不可屏蔽中断的中断类型码固定为2，所以中断过程中，不需要取中断类型码。则不可屏蔽中断的中断过程为：
* 标志寄存器入栈，IF=0，TF=0；
* CS、IP入栈；
* （IP）=（8），（CS）=（0AH）。

几乎所有由外设引发的外中断，都是可屏蔽中断。当外设有需要处理的事件（比如说键盘输入）发生时，相关芯片向CPU发出可屏蔽中断信息。不可屏蔽中断是在系统中有必须处理的紧急情况发生时用来通知CPU的中断信息。

# PC机键盘的处理过程
键盘中有一个芯片对键盘上的每一个键的开关状态进行扫描。按下一个键时，开关接通，该芯片就产生一个扫描码，扫描码说明了按下的键在键盘上的位置。扫描码被送入主板上的相关接口芯片的寄存器中，该寄存器的端口地址为60h。松开按下的键时，也产生一个扫描码，扫描码说明了松开的键在键盘上的位置。松开按键时产生的扫描码也被送入60h端口中。

一般将按下一个键时产生的扫描码称为通码，松开一个键产生的扫描码称为断码。

扫描码长度为一个字节，通码的第7位为0，断码的第7位为1
即：断码 = 通码 + 80h。比如，g键的通码为22h，断码为a2h

## 引发9号中断

键盘的输入到达60h端口时，相关的芯片就会向CPU发出中断类型码为9的可屏蔽中断信息。CPU检测到该中断信息后，如果IF=1，则响应中断，引发中断过程，转去执行int 9中断例程。

## 执行9号中断

BIOS提供了int 9中断例程，用来进行基本的键盘输入处理，主要的工作如下：

* 读出60h端口中的扫描码；
* 如果是字符键的扫描码，将该扫描码和它所对应的字符码（即ASCII码）送入内存中的BIOS键盘缓冲区； 如果是控制键（比如Ctrl）和切换键（比如CapsLock）的扫描码，则将其转变为状态字节写入内存中存储状态字节的单元；
* 对键盘系统进行相关的控制，比如说，向相关芯片发出应答信息。

BIOS键盘缓冲区可以存储15个键盘输入，一个键盘输入用一个字单元存放，高位字节存放扫描码，低位字节存放字符码。

# 编写int 9中断例程
编程： 在屏幕中间依次显示’a‘-'z'，并且可以看清，在显示的过程中，按下Esc按键后，改变显示的颜色
```
;p15-1.asm
assume cs:code
stack segment
    db 128 dup(0)
stack ends 
code segment
    start:
        mov ax,stack
        mov ss,ax
        mov sp,128

        mov ax, 0b800h
        mov es, ax          
        mov ah, 'a'
    s:
        mov es:[160*12+40*2],ah
        call delpy
        inc ah
        cmp ah,'z'
        jna s

        mov ax,4c00h
        int 21h
    delpy:
        push ax
        push dx
        mov dx,3h ;循环1000000h
        mov ax,0
    s1: 
        sub ax,1 ; (ax)=(ax)-1
        sbb dx,0

        cmp ax,0
        jne s1
        cmp dx,0
        jne s1

        pop dx
        pop ax
        
        ret

code ends
end start    
```

最终的程序
```
;p15-2.asm
assume cs:code
stack segment
    db 128 dup(0)
stack ends 
data segment
    dw 0,0
data ends

code segment
    start:
        mov ax,stack
        mov ss,ax
        mov sp,128
        
        mov ax,data
        mov ds,ax

        mov ax,0
        mov es,ax

        push es:[9*4]
        pop ds:[0]
        push es:[9*4+2]
        pop ds:[2]         ;将原来的int9中断程序的入口地址保存在ds:0 和ds:2 单元中

        mov word ptr es:[9*4],offset int9
        mov es:[9*4+2],cs ;在中断向量表中设置新的的int9中断例程的入口地址

        mov ax, 0b800h
        mov es, ax          
        mov ah, 'a'
    s:
        mov es:[160*12+40*2],ah
        call delpy
        inc ah
        cmp ah,'z'
        jna s

        mov ax,0
        mov es,ax

        push ds:[0]
        pop es:[9*4]
        push ds:[2]
        pop es:[9*4+2]    ;将中断向量表int9 中断例程的入口恢复为原来的地址

        mov ax,4c00h
        int 21h
    delpy:
        push ax
        push dx
        mov dx,3h ;循环30000h次
        mov ax,0
    s1: 
        sub ax,1 ; (ax)=(ax)-1
        sbb dx,0

        cmp ax,0
        jne s1
        cmp dx,0
        jne s1

        pop dx
        pop ax
        
        ret
    ;------------------新的int 9 中断例程----------------------
    int9: 
        push ax
        push bx
        push es

        in al,60h

        pushf
        pushf
        pop bx
        and bh,11111100b
        push bx
        popf 
        call dword ptr ds:[0]; 对int 指令进行模拟模拟 ，调用原来的int 9中断例程

        cmp al,1
        jne int9ret

        mov ax, 0b800h
        mov es, ax         
        inc byte ptr es:[160*12+40*2+1] ;属性加1 改变颜色

    int9ret:
        pop es 
        pop bx
        pop ax

        iret

code ends
end start    
```
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/15.09.56.gif)


在DOS下，按F1键后改变当前屏幕的显示颜色，其他的键照常处理
```
;p15-3.asm
;使用的是按键1.没有使用f1按键 因为和mac快捷点冲突
assume cs:code
stack segment
    db 128 dup(0)
stack ends 


code segment
    start:
        mov ax,stack
        mov ss,ax
        mov sp,128
        
        push cs
        pop ds ;设置ds为cs地址

        mov ax,0
        mov es,ax ;目标段地址

        mov si,offset int9  ;设置ds:si指向源地址
        mov di,204h         ;设置es:di指向目的地址
        mov cx,offset int9end - offset int9 ;设置cx为传输长度
        cld
        rep movsb

        push es:[9*4]
        pop es:[200h]
        push es:[9*4+2]
        pop es:[202h] ; 将原来的入口地址保存在es:[200h] ，es:[202h] 地址

        cli  ;IF置0的原因就是，在进入中断处理程序后，禁止其他的可屏蔽中断。
        mov word ptr es:[9*4],204h
        mov word ptr es:[9*4+2],0
        sti

        mov ax,4c00h
        int 21h
        
    int9:
        push ax
        push bx
        push cx
        push es

        in al,60h

        pushf ;pushf的功能是将标志寄存器的值压栈，
        call dword ptr cs:[200h];此中断例程执行是 (cs)=0 模拟的是int 9 指令

        cmp al,02h;判断是不是1
        jne int9ret

        mov ax, 0b800h
        mov es, ax      
        mov bx,1
        mov cx,2000
    s:
        inc byte ptr es:[bx]
        add bx,2
        loop s

    int9ret: 
        pop es
        pop cx
        pop bx
        pop ax

        iret 
    int9end: nop

code ends
end start    

```
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/6.22.35.gif)

## 实验15 

在DOS下，按下“A”键后，除非不在松开，如果松开“A”后，除非不在松开，如果松开，就显示满屏的的“A”

程序分析
1. 程序可以参考 p15-3.asm
2. A的通码是：1EH，断码是：1EH+80H=9EH,在之前我们捕捉的都是通码。这次需要捕捉断码
```
;------------------------------------------
;s15.asm
;在DOS下，按下“A”键后，除非不在松开，如果松开“A”后，除非不在松开，如果松开，就显示满屏的的“A”
;-----------------------------------------
assume cs:code
stack segment
    db 128 dup(0)
stack ends 


code segment
    start:
        
        push cs
        pop ds ;设置ds为cs地址

        mov ax,0
        mov es,ax ;目标段地址

        mov si,offset int9  ;设置ds:si指向源地址
        mov di,204h         ;设置es:di指向目的地址
        mov cx,offset int9end - offset int9 ;设置cx为传输长度
        cld
        rep movsb

        push es:[9*4]  ;将向量表中9号ip压栈
        pop es:[200h]  ;弹栈到0:200H处
        push es:[9*4+2] ;将向量表中9号cs压栈
        pop es:[202h] ; 将原来的入口地址保存在es:[200h] ，es:[202h] 地址

        cli  ;IF置0的原因就是，在进入中断处理程序后，禁止其他的可屏蔽中断。
        mov word ptr es:[9*4],204h
        mov word ptr es:[9*4+2],0
        sti

        mov ax,4c00h
        int 21h
        
    int9:
        push ax
        push bx
        push cx
        push es

        in al,60h ;从端口60h读取数据
        ;模拟的是int 9 指令
        pushf ;pushf的功能是将标志寄存器的值压栈，
        call dword ptr cs:[200h];此中断例程执行是 (cs)=0 

        cmp al,9Eh;判断是不是断码a
        jne int9ret

        mov ax, 0b800h
        mov es, ax      
        mov bx,0      ;修改字符，修改高位
        mov cx,2000
    s:
        mov byte ptr es:[bx],'A'
        add bx,2
        loop s

    int9ret: 
        pop es
        pop cx
        pop bx
        pop ax

        iret 
    int9end: nop

code ends
end start    


```
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/17.13.16.gif)