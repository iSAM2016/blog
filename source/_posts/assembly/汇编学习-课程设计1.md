---
title: 汇编学习-课程设计1
date: 2021-09-25 12:55:07
tags:
categories:
- 汇编
---

程序分析：
1.  实验七，最终的结果，一行数据中包含年份，空格，收入，空格，雇员数，空格，人均收入，现在的内存结构就是我们希望的显示在屏幕显示的结构。 这样我们直接读取table 的数据，将数字变为字符，放到暂存数据段中，然后放到显存数据段上，显示在屏幕中。把实验七的程序改造成一个子程序，叫做layout,这样就可以获取table中的数据结构了
2.  但是table中的数据，“年份”数据是字符数据，我们可以直接使用，在年份字符后边加零即可，其他数据是数据，我们需要通过 dtoc程序 变为对应的ASCII字符
3.  题目已经说明，收入是dd类型数据，dtoc程序中，使用的还是ax 需要根据实验10-2 divdw 进行从新计算
4.  大体流程 layoy在table中放置元数据 --- 循环读取个元数据，dtoc将数据变为字符放到数据暂存区，并计算显示位置坐标---- show_str 显示字符
5.  由于有很多的子程序，建议分别运行调试，然后在组合起来，这样效果好些，注意使用栈来保护变量

### dtoc 子程序改写
将dword型数据数据转变为十进制的字符串，字符串以0为结尾。

1. 在之前的dtoc的实验中，当商为零的时候程序开始跳转。这个程序需要调用divdw,返回参数：ax dword低16位，dx dword高16位，需要判断ax和bx的值带到cx，判断cx是否为零。可是cx只有一个.我们只要求ax和dx的和是不是为零即可
2. 如果有两个以上的子程序，千万要查看是不是用了同时相同的寄存器，导致的错误，尤其是cx ax dx,cx一般用来做循环判断，其他用来做参数
3. 还有大坑 修改了 dl dh cl ch 就是修改了dx cx 需要对dx cx 保护处理

```
;sj1-1.asm
;查看结果 -d ds:100
assume cs:code

data segment
    db 10 dup (0)
data ends

code segment
    start:
        ; 选择数字 5a9768h=>5937000
        mov ax,data
        mov ds,ax ;数据段

        mov ax,9768h
        mov dx,005ah
        mov si,0   ;记忆在数据栈中有多少个数据
        call dtoc
       
        mov ax, 4c00H
        int 21H
    ;---------------
    ;将dword数字转为字符串
    ;
    ;参数: ax dword低位
    ;参数: dx dword高位
    ;返回：无
    ;---------------
    dtoc:
        push ax
        push dx
        push cx
        push bx ;divdw中使用到了bx
        push bp ;divdw中使用到了bp
        push si ;数据入栈保护
        
    dtoc_count:
        mov cx,10    ;除数为零
        mov bx,0     ;在divdw中使用过，恢复为零
        call divdw   ;返回：ax 结果的低16位 dx 结果的低16位 bp 余数 

        push ax      ;我在这个地方调试了好久
        push dx      ;保护数据，一会需要做加法判断是否为零

        add ax,dx    
        mov cx,ax    ;判断商为是否为零

        pop dx
        pop ax       ;跳出之前要恢复原来的数值

        jcxz last    ;跳出，数字循环完毕
        add bp,30H   ;获取十进制数码对应的ASII码
        push bp      ;存储余数
        
        inc si       ;在栈中有多少个数据 解决题5
        jmp short dtoc_count ;继续读取字符串

    ;处理边界值 最后一次商为零，也需要压栈
    dtoc_last: 
        add bp,30H    ;字符零，ASCII是30h
        push bp       ;边界值为零
        inc si  

        mov cx,si        ;loop 循环读取存储的字符数据  关键点
        mov si,0         ;数据偏移地址
    dtoc_write:
        pop ds:[si] ;将asii写入数据段
        inc si
        loop dtoc_write
    exitdtoc:
        pop si 
        pop bp 
        pop bx 
        pop cx
        pop dx
        pop ax
        ret
    ;-------------
    ;被除数是32位的除法
    ;
    ;参数：ax dword低16位
    ;参数：dx dword高16位
    ;参数：cx 除数
    ;返回：ax 结果的低16位 
    ;返回：dx 结果的低16位
    ;返回：bp 余数 不在使用cx避免混乱
    ;-------------
    divdw:                      ;子程序开始
        push ax             ;将被除数低16位先压栈保存。
        mov ax, dx          ;（ax）=（dx）
        mov dx, 0000H       ;
        div cx              ;此时（dx）=0000H，（ax）=000FH，组合成0000000FH。
        mov bx, ax          ;将H/N结果的商先保存在bx中，（bx）=0001H
        
        pop ax              ;将L值弹栈到ax
        div cx              ;此时（dx）=0005H，（ax）=4240H,组合成54240H
        mov bp, dx          ;返回值（cx）等于最终结果的余数
        mov dx, bx          ;最终结果高16位值=（bx）

        ret

code ends
end start
```

### 把实验七改写为子程序

这个改造简单
```
;sj1-2.asm
;查看结果 -d ds:0 22f
assume ds:data, es:table, cs:code 

data segment  
    db '1975','1976','1977','1978','1979','1980','1981','1982','1983'  
    db '1984','1985','1986','1987','1988','1989','1990','1991','1992'  
    db '1993','1994','1995' ;以上是表示21年的21个字符串

    dd 16,22,382,1356,2390,8000,16000,24486,50065,97479,140417,197514  
    dd 345980,590827,803530,1183000,1843000,2759000,3753000,4649000,5937000  ;以上是表示21年 公司总收入的21个dword型数据

    dw 3,7,9,13,28,38,130,220,476,778,1001,1442,2258,2793,4037,5635,8226  
    dw 11542,14430,15257,17800;以上是表示21公司雇员人数的21个Word型数据。

data ends  
table segment  
    db 21 dup ('year summ ne ?? ')  
table ends

code segment
start:
    ;初始化两个数据段，准备读取数据
    mov ax,data
    mov ds,ax     ;初始化data数据段
    
    mov ax,table
    mov es,ax     ;初始化table 数据段
    call layout

    mov ax,4c00H
    int 21H
;-----------
; 将data数据写入table段中
; 参数 data段地址  tabled段地址
;---------
layout:
    push ax
    push bx
    push si
    push cx
    push di
    push ds
    push es

    mov bx,0      ;累积计数器
    mov si,0      ;累积计数器
    mov cx,21     ;循环次数
    mov di,0      ;累积计数器

layout_write:
    ; 年份
    mov ax,[bx]   
    mov es:[si],ax;写入年份数据前两位
    mov ax,[bx+2]   
    mov es:[si+2],ax;写入年份数据后两位

    ;空格
    mov al,20H
    mov es:[si+4],al
    
    ; 收入
    mov ax,[bx+84]    ;收入的第一个字节
    mov es:[si+5],ax
    mov ax,[bx+84+2]  ;收入的第二个字节
    mov es:[si+5+2],ax

     ;空格
    mov al,20H
    mov es:[si+9],al

    ; 雇员
    mov ax,[di+168]    
    mov es:[si+10],ax
    
    ;空格
    mov al,20H
    mov es:[si+12],al

    ; 人均
    mov ax,[bx+84]   ;低16位
    mov dx,[bx+86]   ;高16位

    mov bp,[di+168]
    div bp                 
    mov es:[si+13],ax     ;将商的结果（ax）写入table段中

    ;空格
    mov al,20H
    mov es:[si+15],al


    add bx,4        ; 根据年份进行递增，遍历data
    add di,2        ;  
    add si,16       ; 遍历table  查看 -d ds:0 22f
    loop layout_write    

    pop es
    pop ds
    pop di
    pop cx
    pop si
    pop bx
    pop ax
    ret

code ends
end start
```

### 主程序 
1. 解决从table中读取数据，这个好解决，我们在sj1-2的思路反过来即可
2. 需要多做调试，可以分段调试 

```
assume ds:data, es:table, cs:code

data segment
    db 10 dup(0)       
data ends
table segment  
    db '1975'
    db ' '
    dd 100  ;以上是表示21年 公司总收入的1个dword型数据
    db ' '
    dw 1    ;以上是表示21公司雇员人数的1个Word型数据。
    db ' '
    dw 100    
    db ' '
   
    db '1975'
    db ' '
    dd 5937000  ;以上是表示21年 公司总收入的1个dword型数据
    db ' '
    dw 17800    ;以上是表示21公司雇员人数的1个Word型数据。
    db ' '
    dw 33 
    db ' '
table ends  

code segment
;-----------
; 主程序
;---------
start:
    mov ax,table
    mov es,ax     ;获取table 数据段es
    mov bx,data
    mov ds,bx     ;获取数据段 ds

    mov si,0      ;读取table累积计数器
    mov cx,2     ;循环次数
    mov bx,0      ;读取table偏移地址
    mov dh,3      ;屏幕的行数  测试的时候可以写3以上的数字，写0换行看不到效果 这里是个大坑，dh 复制相当于修改了dx 需要对dx做保护

main_read:
    push cx 
    ;年份  分别调试一
    mov ax,es:[bx]    ;读取年份数据前两位，年份是字符串直接放到内存中
    mov ds:[si],ax
    mov ax,es:[bx+2]  ;读取年份数据后两位
    mov ds:[si+2],ax
    mov byte ptr ds:[si+4], 0 

    mov dl,0           ;所在列的数,show_str的参数
    mov ch, 0          ;ch清零，防止高8位不为零。
    mov cl, 2  
    call show_str
    
    ; 收入 分别调试2
    push dx           ;大坑 在之前赋值了dh为3
    mov ax,es:[bx+5]  ;收入的第一个字节 低16位
    mov dx,es:[bx+5+2];收入的第二个字节 高16
    call dtoc
    pop dx   

    mov dl,20          ;所在列的数,show_str的参数
    mov ch, 0          ;ch清零，防止高8位不为零。
    mov cl, 2  
    call show_str

    ; 雇员
    push dx            ;大坑 在之前赋值了dh为3
    mov ax,es:[bx+10]
    mov dx,0           ;dx需要置为零
    call dtoc
    pop dx   

    mov dl,40          ;所在列的数,show_str的参数
    mov ch, 0          ;ch清零，防止高8位不为零
    mov cl, 2  
    call show_str

    ; 人均
    push dx            ;大坑 在之前赋值了dh为3
    mov ax,es:[bx+13] 
    mov dx,0
    call dtoc
    pop dx   

    mov dl,50        ;所在列的数,show_str的参数
    mov ch,0          ;ch清零，防止高8位不为零
    mov cl,2  
    call show_str

    add bx,16            ; 一行内存是16个字节
    mov si,0
    add dh,1
     ; 收入 分别调试3
    pop cx
    loop main_read 

   
   mov ax,4c00H
   int 21H



;---------------
;将dword数字转为字符串
;
;参数: ax dword低位
;参数: dx dword高位
;返回：无
;---------------
dtoc:
    push ax
    push dx
    push cx
    push bx ;divdw中使用到了bx
    push bp ;divdw中使用到了bp
    push si ;数据入栈保护

    
dtoc_count:
    mov cx,10    ;除数为零
    mov bx,0     ;在divdw中使用过，恢复为零
    mov bp,0
    call divdw   ;返回：ax 结果的低16位 dx 结果的低16位 bp 余数 

    push ax      ;我在这个地方调试了好久
    push dx      ;保护数据，一会需要做加法判断是否为零

    add ax,dx    
    mov cx,ax    ;判断商为是否为零

    pop dx
    pop ax       ;跳出之前要恢复原来的数值

    jcxz dtoc_last    ;跳出，数字循环完毕
    add bp,30H   ;获取十进制数码对应的ASII码
    push bp      ;存储余数
    
    inc si       ;在栈中有多少个数据 解决题5
    jmp short dtoc_count ;继续读取字符串

;处理边界值 最后一次商为零，也需要压栈
dtoc_last: 
    add bp,30H    ;字符零，ASCII是30h
    push bp       ;边界值为零
    inc si  

    mov cx,si        ;loop 循环读取存储的字符数据  关键点
    mov si,0         ;数据偏移地址
dtoc_write:
    pop ds:[si] ;将asii写入数据段
    inc si
    loop dtoc_write
exitdtoc:
    pop si 
    pop bp 
    pop bx 
    pop cx
    pop dx
    pop ax
    ret
;-------------
;被除数是32位的除法
;
;参数：ax dword低16位
;参数：dx dword高16位
;参数：cx 除数
;返回：ax 结果的低16位 
;返回：dx 结果的低16位
;返回：bp 余数 不在使用cx避免混乱
;-------------
divdw:                      ;子程序开始
    push ax             ;将被除数低16位先压栈保存。
    mov ax, dx          ;（ax）=（dx）
    mov dx, 0000H       ;
    div cx              ;此时（dx）=0000H，（ax）=000FH，组合成0000000FH。
    mov bx, ax          ;将H/N结果的商先保存在bx中，（bx）=0001H
    
    pop ax              ;将L值弹栈到ax
    div cx              ;此时（dx）=0005H，（ax）=4240H,组合成54240H
    mov bp, dx          ;返回值（cx）等于最终结果的余数
    mov dx, bx          ;最终结果高16位值=（bx）

    ret

;---------------
;屏幕指定位置显示字符串
;
;参数dh 屏幕的行数
;参数dl 所在行的列数
;参数cl ; 我们暂时固定属性 颜色属性（此处应是二进制数0000 0010） 
;需要临时数据段地址
;---------------
show_str: 
    push ax
    push dx
    push es ;保护es
    push cx
    push bx 
    push bp
    push si 

    mov ax,0B800H ;在汇编源程序中，数据不能以字母开头，所以要在前面加0。
    mov es,ax     ;显示缓冲区的段地址
    
    ;计算dl * 2 
    mov ax,0
    mov al,2
    mul dl
    mov bx,ax ;把结果放到dl

    ; 计算 dh * 160 结果在ax中
    mov ax,0   ;（ax）= 0，防止高位不为零  
    mov al,160 ;0a0H
    mul dh
    add bx,ax  ;获取最终否偏移量

    mov di,0   ;di作为字符的偏移零
    mov al,cl  ; 将属性放到al中，因为后边的程序会用到cx
show_str_read:
    mov cl,ds:[si]
    mov ch,0             ;cx中 ch固定为零，关键在于cl的变化是否为零 
    jcxz show_str_read_ok              ;跳出，字符串操作完毕
    mov es:[bx+di+0],cl  ;对字符串业务操作 写入字符
    mov es:[bx+di+1],al  ; 写入属性
    inc si               ;下一个字符串
    add di,2        
    jmp short show_str_read       ;继续读取字符串
show_str_read_ok:
    pop si 
    pop bp 
    pop bx 
    pop cx
    pop es
    pop dx
    pop ax
    ret

code ends
end start
```

## 最终程序

```
;sj1.asm
assume cs:code

data segment
    db 10 dup(0)       
data ends
datas segment  
    db '1975','1976','1977','1978','1979','1980','1981','1982','1983'  
    db '1984','1985','1986','1987','1988','1989','1990','1991','1992'  
    db '1993','1994','1995' ;以上是表示21年的21个字符串

    dd 16,22,382,1356,2390,8000,16000,24486,50065,97479,140417,197514  
    dd 345980,590827,803530,1183000,1843000,2759000,3753000,4649000,5937000  ;以上是表示21年 公司总收入的21个dword型数据

    dw 3,7,9,13,28,38,130,220,476,778,1001,1442,2258,2793,4037,5635,8226  
    dw 11542,14430,15257,17800;以上是表示21公司雇员人数的21个Word型数据。

datas ends  
table segment  
    db 21 dup ('year summ ne ?? ')  
table ends

code segment
;-----------
; 主程序
;---------
start:
     ;初始化两个数据段，准备读取数据
    mov ax,datas
    mov ds,ax     ;初始化datas数据段
    
    mov ax,table
    mov es,ax     ;初始化table 数据段
    call layout

    mov ax,table
    mov es,ax     ;获取table 数据段es
    mov bx,data
    mov ds,bx     ;获取数据段 ds

    mov si,0      ;读取table累积计数器
    mov cx,21     ;循环次数
    mov bx,0      ;读取table偏移地址
    mov dh,3      ;屏幕的行数  测试的时候可以写3以上的数字，写0换行看不到效果 这里是个大坑，dh 复制相当于修改了dx 需要对dx做保护

main_read:
    push cx 
    ;年份  分别调试一
    mov ax,es:[bx]    ;读取年份数据前两位，年份是字符串直接放到内存中
    mov ds:[si],ax
    mov ax,es:[bx+2]  ;读取年份数据后两位
    mov ds:[si+2],ax
    mov byte ptr ds:[si+4], 0 

    mov dl,0           ;所在列的数,show_str的参数
    mov ch, 0          ;ch清零，防止高8位不为零。
    mov cl, 2  
    call show_str
    
    ; 收入 分别调试2
    push dx           ;大坑 在之前赋值了dh为3
    mov ax,es:[bx+5]  ;收入的第一个字节 低16位
    mov dx,es:[bx+5+2];收入的第二个字节 高16
    call dtoc
    pop dx   

    mov dl,20          ;所在列的数,show_str的参数
    mov ch, 0          ;ch清零，防止高8位不为零。
    mov cl, 2  
    call show_str

    ; 雇员
    push dx            ;大坑 在之前赋值了dh为3
    mov ax,es:[bx+10]
    mov dx,0           ;dx需要置为零
    call dtoc
    pop dx   

    mov dl,40          ;所在列的数,show_str的参数
    mov ch, 0          ;ch清零，防止高8位不为零
    mov cl, 2  
    call show_str

    ; 人均
    push dx            ;大坑 在之前赋值了dh为3
    mov ax,es:[bx+13] 
    mov dx,0
    call dtoc
    pop dx   

    mov dl,50        ;所在列的数,show_str的参数
    mov ch,0          ;ch清零，防止高8位不为零
    mov cl,2  
    call show_str

    add bx,16            ; 一行内存是16个字节
    mov si,0
    add dh,1
     ; 收入 分别调试3
    pop cx
    loop main_read 

   
   mov ax,4c00H
   int 21H
;-----------
; 将data数据写入table段中
; 参数 data段地址  tabled段地址
;---------
layout:
    push ax
    push bx
    push si
    push cx
    push di
    push ds
    push es

    mov bx,0      ;累积计数器
    mov si,0      ;累积计数器
    mov cx,21     ;循环次数
    mov di,0      ;累积计数器
layout_write:
    ; 年份
    mov ax,[bx]   
    mov es:[si],ax;写入年份数据前两位
    mov ax,[bx+2]   
    mov es:[si+2],ax;写入年份数据后两位

    ;空格
    mov al,20H
    mov es:[si+4],al
    
    ; 收入
    mov ax,[bx+84]    ;收入的第一个字节
    mov es:[si+5],ax
    mov ax,[bx+84+2]  ;收入的第二个字节
    mov es:[si+5+2],ax

     ;空格
    mov al,20H
    mov es:[si+9],al

    ; 雇员
    mov ax,[di+168]    
    mov es:[si+10],ax
    
    ;空格
    mov al,20H
    mov es:[si+12],al

    ; 人均
    mov ax,[bx+84]   ;低16位
    mov dx,[bx+86]   ;高16位

    mov bp,[di+168]
    div bp                 
    mov es:[si+13],ax     ;将商的结果（ax）写入table段中

    ;空格
    mov al,20H
    mov es:[si+15],al


    add bx,4        ; 根据年份进行递增，遍历data
    add di,2        ;  
    add si,16       ; 遍历table  查看 -d ds:0 22f
    loop layout_write    

    pop es
    pop ds
    pop di
    pop cx
    pop si
    pop bx 
    pop ax

    ret

;---------------
;将dword数字转为字符串
;
;参数: ax dword低位
;参数: dx dword高位
;返回：无
;---------------
dtoc:
    push ax
    push dx
    push cx
    push bx ;divdw中使用到了bx
    push bp ;divdw中使用到了bp
    push si ;数据入栈保护

    
dtoc_count:
    mov cx,10    ;除数为零
    mov bx,0     ;在divdw中使用过，恢复为零
    mov bp,0
    call divdw   ;返回：ax 结果的低16位 dx 结果的低16位 bp 余数 

    push ax      ;我在这个地方调试了好久
    push dx      ;保护数据，一会需要做加法判断是否为零

    add ax,dx    
    mov cx,ax    ;判断商为是否为零

    pop dx
    pop ax       ;跳出之前要恢复原来的数值

    jcxz dtoc_last    ;跳出，数字循环完毕
    add bp,30H   ;获取十进制数码对应的ASII码
    push bp      ;存储余数
    
    inc si       ;在栈中有多少个数据 解决题5
    jmp short dtoc_count ;继续读取字符串

;处理边界值 最后一次商为零，也需要压栈
dtoc_last: 
    add bp,30H    ;字符零，ASCII是30h
    push bp       ;边界值为零
    inc si  

    mov cx,si        ;loop 循环读取存储的字符数据  关键点
    mov si,0         ;数据偏移地址
dtoc_write:
    pop ds:[si] ;将asii写入数据段
    inc si
    loop dtoc_write
exitdtoc:
    pop si 
    pop bp 
    pop bx 
    pop cx
    pop dx
    pop ax
    ret
;-------------
;被除数是32位的除法
;
;参数：ax dword低16位
;参数：dx dword高16位
;参数：cx 除数
;返回：ax 结果的低16位 
;返回：dx 结果的低16位
;返回：bp 余数 不在使用cx避免混乱
;-------------
divdw:                      ;子程序开始
    push ax             ;将被除数低16位先压栈保存。
    mov ax, dx          ;（ax）=（dx）
    mov dx, 0000H       ;
    div cx              ;此时（dx）=0000H，（ax）=000FH，组合成0000000FH。
    mov bx, ax          ;将H/N结果的商先保存在bx中，（bx）=0001H
    
    pop ax              ;将L值弹栈到ax
    div cx              ;此时（dx）=0005H，（ax）=4240H,组合成54240H
    mov bp, dx          ;返回值（cx）等于最终结果的余数
    mov dx, bx          ;最终结果高16位值=（bx）

    ret

;---------------
;屏幕指定位置显示字符串
;
;参数dh 屏幕的行数
;参数dl 所在行的列数
;参数cl ; 我们暂时固定属性 颜色属性（此处应是二进制数0000 0010） 
;需要临时数据段地址
;---------------
show_str: 
    push ax
    push dx
    push es ;保护es
    push cx
    push bx 
    push bp
    push si 

    mov ax,0B800H ;在汇编源程序中，数据不能以字母开头，所以要在前面加0。
    mov es,ax     ;显示缓冲区的段地址
    
    ;计算dl * 2 
    mov ax,0
    mov al,2
    mul dl
    mov bx,ax ;把结果放到dl

    ; 计算 dh * 160 结果在ax中
    mov ax,0   ;（ax）= 0，防止高位不为零  
    mov al,160 ;0a0H
    mul dh
    add bx,ax  ;获取最终否偏移量

    mov di,0   ;di作为字符的偏移零
    mov al,cl  ; 将属性放到al中，因为后边的程序会用到cx
show_str_read:
    mov cl,ds:[si]
    mov ch,0             ;cx中 ch固定为零，关键在于cl的变化是否为零 
    jcxz show_str_read_ok              ;跳出，字符串操作完毕
    mov es:[bx+di+0],cl  ;对字符串业务操作 写入字符
    mov es:[bx+di+1],al  ; 写入属性
    inc si               ;下一个字符串
    add di,2        
    jmp short show_str_read       ;继续读取字符串
show_str_read_ok:
    pop si 
    pop bp 
    pop bx 
    pop cx
    pop es
    pop dx
    pop ax
    ret

code ends
end start

```

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/QQ20210926-000546@2x.png)