---
title: 汇编学习-第十三章
date: 2021-09-29 11:04:52
tags:
categories:
- 汇编
---

# int指令
中断信息来自cpu的内部和外部。

int指令的格式为：int n ，n为中断类型码，它的功能是引发中断过程。

CPU执行int n指令，相当于引发一个n号中断的中断过程。 
1. 取出中断类型编码n
2. 标志寄存器入栈IF=0,TF=0
3. cs，ip入栈
4. (ip)=(n*4),(cs)=(n * 4 + 2)

在程序中使用int指令调用任何一个中断的中断处理程序(中断例程)

# 编写供应用程序调用的中断例程
## 求一word型数据的平方
```
;----------------------
;p13-sqr.asm
;安装中断7ch的中断例程，并计算word类型数据平方
;参数：ax
;返回： dx，ax中分别存放高16位和低16位
; 程序使用-t 一步一步的执行
;----------------------
assume cs:code

code segment
    start:
        call copy_new_int7ch
        call set_new_int7ch
       
        mov ax,3456
        int 7ch
        
        mov ax,4c00h
        int 21h
    sqr:  
		mul ax
		iret  ;CPU执行int 7ch指令进入中断例程之前，标志寄存器、当前的CS和IP被压入栈
		      ;在执行完中断例程后，应该用iret 指令恢复int 7ch执行前的标志寄存器和CS、IP的
    sqr_end: nop                                            
    set_new_int7ch:
        mov ax,0
        mov es,ax
        
        mov word ptr es:[7ch*4], 200h
        mov word ptr es:[7ch*4+2], 0

        ret
    copy_new_int7ch:
        mov ax,cs
        mov ds,ax
        mov si,offset sqr					;设置ds:si指向源地址

        mov ax,0
        mov es,ax
        mov di,200h							;设置es:di指向目的地址
        
        mov cx,offset sqr_end -  sqr	;设置cx为传输长度
        cld									;设置传输方向为正
        rep movsb

        ret
        
code ends
end start
```
运行结果
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211007221713.png)

3456 * 3456 = b64000h

```
;----------------------
;p13-ca.asm
;将一个全是字母，以0结尾的字符转为大写
;参数：ax
;返回： 无
;----------------------
assume cs:code
data segment
    db 'conversation',0
data ends
code segment
    start:
        call copy_new_int7ch ;安装程序
        call set_new_int7ch  ;设置程序入口
        
        mov ax,data
        mov ds,ax
        mov si,0
        
        int 7ch ;测试
        
        mov ax,4c00h
        int 21h
   
    set_new_int7ch:
        mov ax,0
        mov es,ax
        
        mov word ptr es:[7ch*4], 200h
        mov word ptr es:[7ch*4+2], 0

        ret
    copy_new_int7ch:
        mov ax,cs
        mov ds,ax
        mov si,offset capital					;设置ds:si指向源地址

        mov ax,0
        mov es,ax
        mov di,200h							;设置es:di指向目的地址
        
        mov cx,offset capital_end - capital	;设置cx为传输长度
        cld									;设置传输方向为正
        rep movsb

        ret
    capital:  
		push cx
        push si
    change:
        mov cl,[si]
        mov ch,0
        jcxz ok
        and byte ptr [si],11011111b
        inc si
        jmp short change 
    ok:
        pop si
        pop cx
        
		iret  ;CPU执行int 7ch指令进入中断例程之前，标志寄存器、当前的CS和IP被压入栈
		      ;在执行完中断例程后，应该用iret 指令恢复int 7ch执行前的标志寄存器和CS、IP的
    capital_end: nop                                            
        
code ends
end start
```

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211008093925.jpg)

# BIOS和DOS所提供的中断例
在系统板的ROM中存放着一套程序，称为BIOS（基本输入输出系统）

BIOS中主要包含以下几部分内容

* 硬件系统的检测和初始化程序；
* 外部中断和内部中断的中断例程；
* 用于对硬件设备进行I/O操作的中断例程；
* 其他和硬件系统相关的中断例程。

程序员在编程的时候，可以用int 指令直接调用BIOS和DOS系统提供的中断例程，来完成某些工作。
和硬件设备相关的DOS中断例程中，一般都调用了BIOS的中断例程。

## BIOS和DOS中断例程的安装过程
BIOS和DOS提供的中断例程是如何安装到内存中的呢？

1. 开机后，CPU一加电，初始化（CS）= 0FFFFH，（IP）= 0，自动从FFFF:0单元开始执行程序。FFFF:0处有一条转跳指令，CPU执行该指令后，转去执行BIOS中的硬件系统检测和初始化程序。
2. 初始化程序将建立BIOS所支持的中断向量，即将BIOS提供的中断例程的入口地址登记在中断向量表中。
注意，对于BIOS所提供的中断例程，只需将入口地址登记在中断向量表中即可，因为它们是固化到ROM中的程序，一直在内存中存在。
3. 硬件系统检测和初始化完成后，调用int 19h进行操作系统的引导。从此将计算机交由操作系统控制。
4. DOS启动后，除完成其他工作外，还将它所提供的中断例程装入内存，并建立相应的中断向量。

## BIOS中断例程应用
一般来说，一个供程序员调用的中断例程中往往包括多个子程序，中断例程内部用传递进来的参数来决定执行哪一个子程序。

BIOS和DOS提供的中断例程，都用 ah 来传递内部子程序的编号。

```
;p13-6.asm
;在屏幕的第5行12列显示3个红底高亮闪烁的绿色的'a'
assume cs:code 

code segment
    ;int 10h中断例程的"设置光标位置"功能
    mov ah, 2  ;设置光标调用第10h号中断例程的2号子程序，功能为设置光标位置(可以提供光标所在的行号、列号和页号作为参数)

    ;设置光标到第0页，第5行，第12列
    mov bh, 0;
    mov dh, 5; dh中放行号
    mov dl, 12; dl中放列号
    int 10h

    ;int10h中断例程的"在光标位置显示字符"功能。
    mov ah,9 ;调用第10h号中断例程的9号子程序，功能为在光标位置显示字符
    ;提供要显示的字符、颜色属性、页号、字符重复个数作为参数
    mov al,'a'  ;字符
    mov bl,11001010b  ;颜色属性
    mov bh,0  ;第0页
    mov cx,3  ;字符重复个数
    int 10h

code ends 
end
```

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/10.41.03.gif)

bh中页号的含义：内存地址空间中，B8000H~BFFFFH共32kB的空间，为80*25彩色字符模式的显示缓冲区。
一屏的内容在显示缓冲区中共占4000个字节。显示缓冲区分为8页，每页4KB（约4000B），显示器可以显示任意一页的内容。一般情况下，显示第0页的内容。也就是说，通常情况下，B8000H~B8F9FH中的4000个字节的内容将出现在显示器上。

## DOS中断例程应用
int 21h中断例程是DOS提供的中断例程，4ch号功能，即程序返回功能
```
;p13-7.asm
assume cs:code 
 
data segment 
	db	'Welcome to masm',  '$'     ;“$”本身并不显示，只起到边界的作用
data ends 

code segment
start:	mov ah, 2 ;10号中断设置光标位置功能
		mov bh, 0 ;第0页
		mov dh, 5;77dh中放行号
		mov dl, 12 ;dl中放列号
		int 10h 
		
		mov ax, data 
		mov ds, ax 
		mov dx, 0 ;ds:dx指向字符串的首地址data:0  （参数）
		mov ah, 9 ;调用第21h号中断例程的9号子程序，功能为在光标位置显示字符串，可以提供要显示字符串的地址作为参数
		int 21h 
		
		mov ax, 4c00h ;21号中断程序返回功能
		int 21h 
code ends
end start
```
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211008105519.jpg)

## 实验13 编写，应用中断例程

1. 编写并安装int 7ch中断例程，功能为显示一个用0结尾的字符串，中断例程安装在0：200处。
   
程序分析： 请直接参考实验十
```
;s13-1.asm
assume cs:code 
 
data segment 
	db	'Welcome to masm!',  0     ;“$”本身并不显示，只起到边界的作用
data ends 

code segment
    start:
        mov dh,10 ;行号
        mov dl,10 ;列号
        mov cl,2 ;颜色

        call copy_new_int7ch ;安装程序
        call set_new_int7ch  ;设置程序入口
        
        mov ax,data
        mov ds,ax            ;ds:[si]指向字符串。
        mov si,0
        
        int 7ch ;测试
        
        mov ax,4c00h
        int 21h
   
    set_new_int7ch:
        push dx ;入栈保护
        push cx

        mov ax,0
        mov es,ax
        
        mov word ptr es:[7ch*4], 200h
        mov word ptr es:[7ch*4+2], 0

        pop cx
        pop dx

        ret
    copy_new_int7ch:
        push dx
        push cx

        mov ax,cs
        mov ds,ax
        mov si,offset show_str					;设置ds:si指向源地址

        mov ax,0
        mov es,ax
        mov di,200h							;设置es:di指向目的地址
        
        mov cx,offset show_str_end - show_str	;设置cx为传输长度
        cld									;设置传输方向为正
        rep movsb

        pop cx
        pop dx

        ret
    ;-----------------------------------------
    ;show_str功能 :按行和列及字符属性显示字符串  
    ;入口参数：dh-行数、dl-列数、cl-字符属性、ds:[si]指向字符串。
    ;返回值：无
    ;-----------------------------------------
    show_str: 
        push dx
        push cx
        push si ;数据入栈保护

        mov ax,0B800H ;在汇编源程序中，数据不能以字母开头，所以要在前面加0。
        mov es,ax     ;显示缓冲区的段地址

        ;计算dl * 2 
        mov ax,0
        mov al,2
        mul dl
        mov bx,ax ;把结果放到dl

        ; 计算 dh * 160 结果在ax中
        mov ax,0   ;（ax）= 0，防止高位不为零  
        mov al,160 ;0a0H
        mul dh
        add bx,ax  ;获取最终否偏移量

        mov di,0   ;di作为字符的偏移零
        mov al,cl  ; 将属性放到al中，因为后边的程序会用到cx
    read:
        mov cl,ds:[si]
        mov ch,0             ;cx中 ch固定为零，关键在于cl的变化是否为零 
        jcxz ok              ;跳出，字符串操作完毕
        mov es:[bx+di+0],cl  ;对字符串业务操作 写入字符
        mov es:[bx+di+1],al  ; 写入属性
        inc si               ;下一个字符串
        add di,2        
        jmp short read       ;继续读取字符串
    ok:
        pop si 
        pop cx
        pop dx

		iret  ;CPU执行int 7ch指令进入中断例程之前，标志寄存器、当前的CS和IP被压入栈
		      ;在执行完中断例程后，应该用iret 指令恢复int 7ch执行前的标志寄存器和CS、IP的
    show_str_end: nop                                            
        
code ends
end start
```
![]((https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211008112945.jpg)

3. 分别在屏幕中显示第2，4，6，8行显示4句英文诗句，并补全程序

```
assume cs:code

code segment
    s1: db 'Good,better,best,','$'
    s2: db 'Never let it rest,','$'
    s3: db 'Till good is better,','$'
    s4: db 'And better,best.','$'
    s: dw offset s1, offset s2, offset s3, offset s4
    row:  db 2, 4, 6, 8
start :
        mov ax, cs
        mov ds, ax           ;将ds也指向了cs段
        mov bx, offset s     ;(bx)=s标号地址
        mov si, offset row   ;（si）=row标号的地址。
        mov cx, 4            ;计数器置为4,4行字符串

    ok:    
        ;在DOS窗口设置光标的位置， 
        mov bh,0   ;BIOS中的10h中断例程的入口参数设置，bh（页号）=0  
        mov dh, [si]  ;入口参数：dh（行号）=（si）
        mov dl, 0  ;入口参数：dl（列数）=0
        mov ah, 2 ;10h例程中的2号子程序，功能：设置光标位置。
        int 10h   ;调用中断例程

       ;开始显示字符串。调用21h例程，9号子程序
        mov dx,[bx]          ;入口参数：dx=（bx），每个字符串的首地址。
        mov ah,9             ;dos系统中21h例程中的9号子程序
        int 21h              ;调用中断例程，功能：显示字符串（以$结尾的）
        inc si               ;si按字节定义的。每次增量是1个字节。
        add bx,2             ;bx是按照字定义的，每次增量是2个字节。
        loop ok

        mov ax,4c00h
        int 21h
code ends
end start    
```
![]((https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211008115142.jpg)