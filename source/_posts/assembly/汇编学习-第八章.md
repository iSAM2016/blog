---
title: 汇编学习-第八章
date: 2021-09-08 18:02:24
tags:
categories:
- 汇编
---

# bx、si、di和bp
在8086CPU中，只有这4个寄存器可以用在`[…]`中来进行内存单元的寻址。

在[ ]中，这4个寄存器可以单个出现，或只能以4种组合出现：`bx和si、bx和di、bp和si、bp和di`。

只要在`[……]`中使用寄存器bp，而指令中没有显性地给出段地址, 段地址就默认在ss中.

# 机器指令处理的数据在什么地方
数据处理大致可分为3类：读取、写入、运算。

在机器指令这一层来讲，并不关心数据的值是多少，而关心指*令执行前一刻*，它将要处理的数据所在的位置。指令在执行前，所要处理的数据可以在3个地方：
* CPU内部
* 内存
* 端口

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210910110725.jpg)

# 汇编语言中数据位置的表达
汇编语言中用3个概念来表达数据的位置

1. 立即数（idata）
```
mov ax, 1                 ;对于直接包含在机器指令中的数据（执行前在CPU的指令缓冲器中）
add bx, 2000h             ;在汇编语言中称为：立即数（idata）
or bx, 00010000b
mov al, 'a'
```
2. 寄存器

```
mov ax, bx     ;指令要处理的数据在寄存器中，在汇编指令中给出相应的寄存器名。
mov ds, ax 
push bx 
mov ds:[0], bx 
push ds 
mov ss, ax
mov sp, ax
```

3. 段地址（SA）和偏移地址（EA）
```
;指令要处理的数据在内存中，在汇编指令中可用[X]的格式给出EA，SA在某个段寄存器中。
mov ax, [0]
mov ax, [di]
mov ax, [bx+8]
mov ax, [bx+si]
mov ax, [bx+si+8]   ;以上段地址默认在ds中

mov ax, [bp]
mov ax, [bp+8]
mov ax, [bp+si]
mov ax, [bp+si+8]   ;以上段地址默认在ss中

mov ax, ds:[bp]
mov ax, es:[bx]
mov ax, ss:[bx+si]
mov ax, cs:[bx+si+8] ;显式给出存放段地址的寄存器
```
# 寻址方式

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210910112504.jpg)

# 指令要处理的数据有多长
8086CPU的指令，可以处理两种尺寸的数据，`byte`和`word`

通过寄存器名指明要处理的数据的尺寸。
例如： `mov al, ds:[0]` 寄存器al指明了数据为1字节

在没有寄存器名存在的情况下，用操作符X ptr指明内存单元的长度，X在汇编指令中可以为word或byte。
例如：`mov byte ptr ds:[0]`, 1 byte ptr 指明了指令访问的内存单元是一个字节单元
`mov word ptr ds:[0],1`

有些指令默认了访问的是字单元还是字节单元
例如，`push [1000H]`，**push 指令只进行字操作**

# 寻址方式的综合应用
```x86asm
;p81.asm
assume cs:codesg

seg segment 
    db 64 dup  (0) ;偏移到60H ;偏移到60H  dup 后面会讲解
	db 'DEC' 
	db 'Ken Oslen'
    dw 137;排名转为16进制
    dw 40 
    db 'PDP' 
seg ends

codesg segment 
start:
    mov ax,seg ;确定DEC公司记录的位置
    mov ds,ax
    mov bx,60H

    mov word ptr [bx+0ch],137 ;
    mov word ptr [bx+0eh],70 ; 收入字段增加70

    mov si,0                 ;用si来定位产品字符串的字符
    mov byte ptr [bx+10h+si], 'V'
    inc si
    mov byte ptr [bx+10h+si], 'A'
    inc si
    mov byte ptr [bx+10h+si], 'X'

	mov ax, 4c00h 
	int 21h 
codesg ends 
end start
```
查看初始化数据
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210910143245.jpg)

执行完
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210910143332.jpg)

c语言描述

```c
struct company
{
    char cn[3];/*公司名称*/
    char hn[9];/*总裁姓名*/
    int pm;/*排名*/
    int sr;/*收入*/
    char cp[3];/*著名产品*/
};
//sizeof (struct company) == 24

int main()
{
    /*定义一个公司记录的变量，内存中将存有一条公司的记录*/
    struct company dec = {"DEC", "Ken Olsen", 137, 40, "PDP"};

    int i;

    dec.pm = 38;
    dec.sr = dec.sr + 70;

    i = 0;
    dec.cp[i] = 'V'; //mov byte ptr [bx].10h[si], 'V'
    i++;
    dec.cp[i] = 'A';
    i++;
    dec.cp[i] = 'X';

    return 0;
}
```

# div指令、dd、dup指令
div是除法指令

* 除数：有8位和16位两种，在一个寄存器或内存单元中。

* 被除数：默认放在AX或DX和AX中，
    * 如果除数为8位，被除数则为16位，默认在AX中存放；
    * 如果除数为16位，被除数则为32位，在DX和AX中存放，DX存放高16位，AX存放低16位。

* 结果：
    * 如果除数为8位，则AL存储除法操作的商，AH存储除法操作的余数；
    * 如果除数为16位，则AX存储除法操作的商，DX存储除法操作的余数

# 伪指令dd
db和dw定义字节型数据和字型数据。

dd是用来定义dword（double word，双字）型数据的伪指令

# 操作符dup
dup在汇编语言中同db、dw、dd等一样，也是由编译器识别处理的符号。
它和db、dw、dd等数据定义伪指令配合使用，用来进行数据的重复

`db 3 dup (0)`   ;定义了3个字节，它们的值都是0，相当于db 0，0，0。
`db 3 dup (0, 1, 2)` ;定义了9个字节，它们是0、1、2、0、1、2、0、1、2，相当`于db 0，1，2，0，1，2，0，1，2。
`db 3 dup ('abc', 'ABC')` ;定义了18个字节，它们是abcABCabcABCabcABCC，相当于db 'abc', 'ABC' ,'abc' , 'ABC, 'abc', 'ABC'。

dup 的使用格
```
db 重复的次数 dup(重复的字节型数据)
dw 重复的次数 dup（重复的字节型数据）
dd 重复的次数 dup(重复的双字型数据)
```
# 小结

16位CPU所含有的寄存器有：
* 4个数据寄存器(AX、BX、CX和DX)
* 2个变址和指针寄存器(SI和DI) 2个指针寄存器(SP和BP) 
* 4个段寄存器(ES、CS、SS和DS)
* 1个指令指针寄存器(IP) 1个标志寄存器(Flags)
* 一共14个寄存器，或者16个

# 实验7 寻址方式在结构化数据访问中的应用

 `db 21 dup ('year summ ne ?? ')  `初始化table 的一行为16个字节
## 读取数据
1.  data 中定义了连续定义了db,dd,dw,所以他们在内存是连续存在的（错误的理解为，定义了多个段，每个段后面会补零占用16个字节）
    1.  db 是字节类型  单位：一个字节（年份）存储的是字符串类型， 每个年份是4位，连续的4个字节是一个年份。共计 0-83（84个字节), 我们分两次把年份读取完
    2.  dd 是双字类型  单位：4个字节(收入) 84-167(84个字节) 我们分两次把收入读取完
    3.  dw 是字类型    单位：2个字节（人数）168-209(42个字节)
   现在看来，他们混在一起。寻找一下相对位置关系，当年份取得地址x(x>=0)的时候，收入地址是(84+x), 
    4. 人数地址,我们使用di累加器
2. 可以使用ds 来做data 段的寻址， es用来寻址table
3. 使用bx来定位data 数据段，用si定位table

## 写入数据
1. 定位table每行数据，每行的第一个数据，第一个地址相差10H
2. 每行年份地址是y, 收入地址是y+5， 雇员地址是 y+10 人均地址是y+13 
3. 对于空格的处理，空格的ASCII码是：32（20H）是一个字节，使用使用al 进行传送

## 计算人均
* 如果除数为16位，被除数则为32位，在DX和AX中存放，DX存放高16位，AX存放低16位。
  

<!-- 
它们在内存单元中所占用的连续空间是不同的。（问题提出：为什么要使用不同的数据类型？这个在C语言及其他高级语言中同样适用。不同的数据类型匹配不同的数据，做到合理安排数据在内存中存储，不浪费空间，提高运行速度。） -->

```x86asm
;s7.asm
assume ds:data, es:table, cs:code 

data segment  
    db '1975','1976','1977','1978','1979','1980','1981','1982','1983'  
    db '1984','1985','1986','1987','1988','1989','1990','1991','1992'  
    db '1993','1994','1995' ;以上是表示21年的21个字符串

    dd 16,22,382,1356,2390,8000,16000,24486,50065,97479,140417,197514  
    dd 345980,590827,803530,1183000,1843000,2759000,3753000,4649000,5937000  ;以上是表示21年 公司总收入的21个dword型数据

    dw 3,7,9,13,28,38,130,220,476,778,1001,1442,2258,2793,4037,5635,8226  
    dw 11542,14430,15257,17800;以上是表示21公司雇员人数的21个Word型数据。

data ends  
table segment  
    db 21 dup ('year summ ne ?? ')  
table ends

code segment
start:
    ;初始化两个数据段，准备读取数据
    mov ax,data
    mov ds,ax     ;初始化data数据段
    
    mov ax,table
    mov es,ax     ;初始化table 数据段

    mov bx,0      ;累积计数器
    mov si,0      ;累积计数器
    mov cx,21     ;循环次数
    mov di,0      ;累积计数器

s:
    ; 年份
    mov ax,[bx]   
    mov es:[si],ax;写入年份数据前两位
    mov ax,[bx+2]   
    mov es:[si+2],ax;写入年份数据后两位

    ;空格
    mov al,20H
    mov es:[si+4],al
    
    ; 收入
    mov ax,[bx+84]    ;收入的第一个字节
    mov es:[si+5],ax
    mov ax,[bx+84+2]  ;收入的第二个字节
    mov es:[si+5+2],ax

     ;空格
    mov al,20H
    mov es:[si+9],al

    ; 雇员
    mov ax,[di+168]    
    mov es:[si+10],ax
    
    ;空格
    mov al,20H
    mov es:[si+12],al

    ; 人均
    mov ax,[bx+84]   ;低16位
    mov dx,[bx+86]   ;高16位

    mov bp,[di+168]
    div bp                 
    mov es:[si+13],ax     ;将商的结果（ax）写入table段中

    ;空格
    mov al,20H
    mov es:[si+15],al


    add bx,4        ; 根据年份进行递增，遍历data
    add di,2        ;  
    add si,16       ; 遍历table  查看 -d ds:0 22f
    loop s    

    mov ax,4c00H
    int 21H

code ends
end start
```

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210913140711.jpg)