---
title: 汇编学习-第十六章
date: 2021-10-09 09:02:38
tags:
categories:
- 汇编
---

# 描述了单元长度的符号

```
;程序中，code、a、b、start、s都是标号。这些标号仅仅表示了内存单元的地址
assume cs:code
code segment
         a : db 1,2,3,4,5,6,7,8  ;在后面加有“：”的地址标号，只能在代码段中使用，不能在其他段中使用。
         b : dw 0
start :mov si,offset a
         mov bx,offset b
         mov cx,8
    s : mov al,cs:[si]
         mov ah,0
         add cs:[bx],ax
         inc si
         loop s
         mov ax,4c00h
         int 21h
code ends
end start
```

```
assume cs:code
code segment
          a db 1,2,3,4,5,6,7,8 ;标号a、b后面没有":"，因此它们是可以同时描述内存地址和单元长度的标号。
                               ;标号a，描述了地址code:0，和从这个地址开始，以后的内存单元都是字节单元
          b dw 0               ;标号b描述了地址code:8，和从这个地址开始，以后的内存单元都是字单元。
start :  mov si,0
          mov cx,8
    s :   mov al,a[si]
          mov ah,0
          add b,ax
          inc si
          loop s
          mov ax,4c00h
          int 21h
code ends
end start
```

使用数据标号来描述存储数据的单元的地址和长度

# 检测16.1
```
assume cs:code
code segment
          a dw 1,2,3,4,5,6,7,8 ;标号a、b后面没有":"，因此它们是可以同时描述内存地址和单元长度的标号。
                               ;标号a，描述了地址code:0，和从这个地址开始，以后的内存单元都是字节单元
          b dd 0               ;标号b描述了地址code:8，和从这个地址开始，以后的内存单元都是字单元。
    start:  
        mov si,0
        mov cx,8
    s:  
        mov ax,a[si]         ;a的数据结构是字类型两个字节，b是4个字节 将a的内容送到ax
        add word ptr b[0],ax ;将ax的值和b标号的低16位做加法，连个数相加需要形同的数据类型，必须都是字
        adc word ptr b[2] ,0 ;代位进加法，将0与上以一指令中的cf相加，结果存储在b的高16位单元中，目的是防止低16位产生仅为，同样要指定我字为存储结构
        add si,2 ; dw是一个字 俩个字节
        loop s
        
        mov ax,4c00h
        int 21h
code ends
end start
```

# 在其他段中使用数据标号
```
assume cs:code,ds:data ；用伪指令assume将标号所在的段和一个段寄存器联系起来(编译器需要)
data segment          
          a db 1,2,3,4,5,6,7,8
          b dw 0
data ends
code segment
start:  mov ax,data
          mov ds,ax ;真正确定ds寄存器
          mov si,0
          mov cx,8
s:       mov al,a[si] ;编译为：mov al,[si+0] 默认所访问单元的段地址在ds
          mov ah,0
          add b,ax ;编译为：add [8],ax
          inc si
          loop s
          mov ax,4c00h
          int 21h
code ends
end start
```

# 检测16.2
```
; 即使在assume这个伪指令中将ds和data联系在一起（C语言的全局声明意思）。如果没有将data的段地址赋值给ds，那么[si]寻址的内存单元是错误的，并不是ds:[si]或a[si]。
;如果在assume中没有ds:data，那么在code段中使用a和b是不可能的。因为a和b在code段是不可以见的。
assume cs:code, es:data

data segment
    a db 1,2,3,4,5,6,7,8 
    b dw 0
data ends        

code segment  
    start:  
        mov ax,data
        mov ds,ax ;真正确定ds寄存器
        
        mov si,0
        mov cx,8
    s:  
        mov al,a[si];使用ds定位
        mov ah,0
        add b,ax
        inc si
        loop s

        mov ax,4c00h
        int 21h
code ends
end start
```

# 编程 字符映射
建立一张表，表中依次存储字符“0”~“F”，我们可以通过数值0 ~ 15直接查找到对应的字符

```
;p16-1.asm
assume cs:code

code segment
start:  
		mov al,10

        call showbyte

        mov ax,4c00h
        int 21h

;子程序：
;用al传送要显示的数据

showbyte:
        jmp short show

        table db '0123456789ABCDEF'	;字符表

show:   push bx
        push es

        mov ah,al
        shr ah,1           
        shr ah,1
        shr ah,1
        shr ah,1			    ;右移4位，ah中得到高4位的值
        and al,00001111b		;al中为低4位的值

        mov bl,ah
        mov bh,0
        mov ah,table[bx]		;用高4位的值作为相对于table的偏移，取得对应的字符

        mov bx,0b800h
        mov es,bx
        mov es:[160*12+40*2],ah

        mov bl,al
        mov bh,0
        mov al,table[bx]		;用低4位的值作为相对于table的偏移，取得对应的字符
        
        mov es:[160*12+40*2+2],al

        pop es
        pop bx
        ret

code ends
end start
```

# 编程 实现一个setscreen
```
;-----------------------------------------
;0清屏，1设置前景色，2设置背景色，3向上滚动一行
;参数： ah传递功能号 编号如标题
;      对于1，2 号功能，用al传送颜色
;-----------------------------------------
assume cs:code

code segment
    start:  
		mov ah,3
        mov al,2
        call setscreen

        mov ax,4c00h
        int 21h

    setscreen:
        jmp  short set
        table dw sub1,sub2,sub3,sub4
    set:
        push bx

        cmp ah,3 ;判断功能编号是否大于3
        ja sert
        mov bl,ah
        mov bh,0
        add bx,bx ;根据ah中的功能号计算对应子程序在table中的偏移

        call word ptr table[bx]  ;调用对应的功能子程序
    sert: 
        pop bx
        ret 
    
;-----------------------------------------
;0清屏
;返回值： 无
;-----------------------------------------
    sub1:
        push bx
        push cx
        push es

        mov bx,0b800h
        mov es,bx
        mov bx,0
        mov cx,2000

    sub1s:
        mov byte ptr es:[bx],' '
        add bx,2
        loop sub1s

        pop es
        pop cx
        pop bx
        ret 
;-----------------------------------------
;1设置前景色
;参数： al 传递颜色 
;返回值： 无
;-----------------------------------------
    sub2:
        push bx
        push cx
        push es

        mov bx,0b800h
        mov es,bx
        mov bx,0
        mov cx,2000
    sub2s:
        and byte ptr es:[bx], 11111000b 
        or es:[bx],al
        add bx,2
        loop sub2s

        pop es
        pop cx
        pop bx
        ret
;-----------------------------------------
;2设置背景色
;参数 al 传递颜色 
;返回值： 无
;-----------------------------------------
    sub3:
        push bx
        push cx
        push es

        mov cl,4
        shl al,cl
        mov bx,0b800h
        mov es,bx
        mov bx,1
        mov cx,2000

    sub3s:
        and byte ptr es:[bx], 10001111b 
        or es:[bx],al
        add bx,2
        loop sub3s

        pop es
        pop cx
        pop bx
        ret
;-----------------------------------------
;3向上滚动一行
;参数： 无
;返回值： 无
;-----------------------------------------
    sub4:
        push cx
        push si
        push di 
        push es
        push ds

        mov si,0b800h
        mov es,si
        mov ds,si
        mov si,160 ;ds:si指向地n+1行
        mov di,0   ;es:di指向第n行
        cld 
        mov cx,24

    sub4s:
        push cx
        mov cx,160
        rep movsb ;复制
        pop cx
        loop sub4s

        mov cx,80
        mov si,0
    sub4s1:
        mov byte ptr [160*24+si],' ' ;最后一行
        add si,2
        loop sub4s1

        pop ds
        pop es
        pop di
        pop si
        pop cx
        ret

code ends
end start    
```
# 实验16

思考：
1. 编程思路参考实验13和setscreen 
2. 看一下课本的描述
`指令add b,ax` 
`编译为：add[8],ax`

b被翻译成了8，这是一个相对地址，相对ds来计算的。同理在setscreen中基准点是table（ds段）,程序拷贝写到了0:200（cs段） 处，所有table段的相对地址引用，都会是错误的，因为基准点变了。我们要使用`org 200H`通知编译器从新开始重新计算标号,
```
;----------------------
;s16.asm
;编写一个新的7ch中断例程
;0清屏，1设置前景色，2设置背景色，3向上滚动一行
;参数： ah传递功能号 编号如标题
;      对于1，2 号功能，用al传送颜色
;----------------------
assume cs:code

code segment
    start:
        call copy_new_int7ch
        call set_new_int7ch
        ;设置参数        
        mov ah,2
        mov al,2
        ;中断调用
        int 7ch
        
        mov ax,4c00h
        int 21h
        
        ;通知编译器从200H开始重新计算标号
        org 200H   
    setscreen:  
        jmp  short set
        table dw sub1,sub2,sub3,sub4
    set:
        push bx

        cmp ah,3 ;判断功能编号是否大于3
        ja sert
        mov bl,ah
        mov bh,0
        add bx,bx ;根据ah中的功能号计算对应子程序在table中的偏移

        call word ptr table[bx]  ;调用对应的功能子程序
    sert: 
        pop bx
    	
        iret  ;CPU执行int 7ch指令进入中断例程之前，标志寄存器、当前的CS和IP被压入栈
		      ;在执行完中断例程后，应该用iret 指令恢复int 7ch执行前的标志寄存器和CS、IP的
    
;-----------------------------------------
;0清屏
;返回值： 无
;-----------------------------------------
    sub1:
        push bx
        push cx
        push es

        mov bx,0b800h
        mov es,bx
        mov bx,0
        mov cx,2000

    sub1s:
        mov byte ptr es:[bx],' '
        add bx,2
        loop sub1s

        pop es
        pop cx
        pop bx
        ret 
;-----------------------------------------
;1设置前景色
;参数： al 传递颜色 
;返回值： 无
;-----------------------------------------
    sub2:
        push bx
        push cx
        push es

        mov bx,0b800h
        mov es,bx
        mov bx,0
        mov cx,2000
    sub2s:
        and byte ptr es:[bx], 11111000b 
        or es:[bx],al
        add bx,2
        loop sub2s

        pop es
        pop cx
        pop bx
        ret
;-----------------------------------------
;2设置背景色
;参数 al 传递颜色 
;返回值： 无
;-----------------------------------------
    sub3:
        push bx
        push cx
        push es

        mov cl,4
        shl al,cl
        mov bx,0b800h
        mov es,bx
        mov bx,1
        mov cx,2000

    sub3s:
        and byte ptr es:[bx], 10001111b 
        or es:[bx],al
        add bx,2
        loop sub3s

        pop es
        pop cx
        pop bx
        ret
;-----------------------------------------
;3向上滚动一行
;参数： 无
;返回值： 无
;-----------------------------------------
    sub4:
        push cx
        push si
        push di 
        push es
        push ds

        mov si,0b800h
        mov es,si
        mov ds,si
        mov si,160 ;ds:si指向地n+1行
        mov di,0   ;es:di指向第n行
        cld 
        mov cx,24

    sub4s:
        push cx
        mov cx,160
        rep movsb ;复制
        pop cx
        loop sub4s

        mov cx,80
        mov si,0
    sub4s1:
        mov byte ptr [160*24+si],' ' ;最后一行
        add si,2
        loop sub4s1

        pop ds
        pop es
        pop di
        pop si
        pop cx
        ret
    setscreen_end: nop                                            
    set_new_int7ch:
        mov ax,0
        mov es,ax
        
        mov word ptr es:[7ch*4], 200h
        mov word ptr es:[7ch*4+2], 0

        ret
    copy_new_int7ch:
        mov ax,cs
        mov ds,ax
        mov si,offset setscreen					;设置ds:si指向源地址

        mov ax,0
        mov es,ax
        mov di,200h							;设置es:di指向目的地址
        
        mov cx,offset setscreen_end -  setscreen	;设置cx为传输长度
        cld									;设置传输方向为正
        rep movsb

        ret
        
code ends
end start
```
![]((https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211009154725.jpg)