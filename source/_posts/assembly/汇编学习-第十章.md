---
title: 汇编学习-第十章
date: 2021-09-17 15:08:46
categories:
- 汇编
---
> 此节重点记忆汇编指令，不必记忆公式

# ret 和 retf

call和ret指令都是转移指令，它们都修改IP，或同时修改CS和IP。

ret指令用栈中的数据，修改IP的内容，从而实现近转移；

retf指令用栈中的数据，修改CS和IP的内容，从而实现远转移。

影响栈的元素（SS:SP）

CPU执行ret指令时，相当于进行： pop IP

* (IP) = ((ss) * 16 + (sp))
* (sp) = (sp) + 2

CPU执行retf指令时，相当于进行：pop IP, pop CS

* (IP) = ( (ss) * 16 + (sp))
* (sp) = (sp) + 2
* (CS) = ( (ss) * 16 + (sp))
* (sp) = (sp) + 2

```
;p10-1-1.asm
assume cs:code

stack segment
 db 16 dup(8)
stack ends

code segment
        mov ax,4c00H
        int 21h
    start:  
        mov ax,stack
        mov ss,ax   
        mov sp,16 ; 栈顶

        mov ax,0
        push ax ;sp=sp-2
        mov bx,0
        ret      ; (ip)= (ss * 16) + (sp) ss为stack 段地址  
                 ; sp 设置为为10H
                 ; push ax sp=sp-2(一个子为两个字节)  sp为EH
                 ; (IP) = ((ss) * 16 + (sp)) 获取栈的中ss:[14]的内容为零
                 ; (sp)=(sp)+2  sp 为10Hcd
code ends
end start
```
---

```
;p10-1-2.asm
assume cs:code

stack segment
 db 16 dup(8)
stack ends

code segment
        mov ax,4c00H
        int 21h
    start:  
        mov ax,stack
        mov ss,ax   
        mov sp,16 ; 栈顶

        mov ax,0
        push cs ;sp=sp-2
        push ax ;sp=sp-2
        mov bx,0
        retf     ; (ip)= (ss * 16) + (sp) ss为stack 段地址  
                 ; sp 设置为为10H
                 ; push cs sp=sp-2(一个字为两个字节)  sp为EH 
                 ; push ax sp=sp-2(一个子为两个字节)  sp为CH
                 ; (IP) = ((ss) * 16 + (sp)) 获取栈的中ss:[12]的内容为0
                 ; (sp)=(sp)+2  sp 为EH
                 ; (CS) = ( (ss) * 16 + (sp)) 获取cs的地址
                ; (sp) = (sp) + 2 为10H
code ends
end start
```

## 检测10.1 
补全程序，实现内存1000：0000 处开始执行指令
```
;p10-1.asm
assume cs:code

stack segment
 db 16 dup(8)
stack ends

code segment
        mov ax,4c00H
        int 21h
    start:  
        mov ax,stack
        mov ss,ax   
        mov sp,16 ; 栈顶

        mov ax,1000H;cs 地址先进入栈
        push ax ;sp=sp-2
        mov ax,0  ;ip地址后进入栈
        push ax
        retf     ; (ip)= (ss * 16) + (sp) ss为stack段地址  
                 ; sp 设置为为10H
                 ; push ax sp=sp-2(一个字为两个字节)  sp为EH  cs 地址先进入栈
                 ; push ax sp=sp-2(一个子为两个字节)  sp为CH  ip 地址后进入栈
                 ; (IP) = ((ss) * 16 + (sp)) 获取栈的中ss:[12]的内容为0
                 ; (sp)=(sp)+2  sp 为EH
                 ; (CS) = ( (ss) * 16 + (sp)) 获取cs的地址
                 ; (sp) = (sp) + 2 为10H
code ends
end start
```

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210917170814.jpg)


# call 指令
call指令经常跟ret指令配合使用，因此CPU执行call指令，进行两步操作：

* （1）将当前的 IP 或 CS和IP 压入栈中；
* （2）转移（jmp）。

call指令不能实现短转移，除此之外，call指令实现转移的方法和 jmp 指令的原理相同。

## 依据位移进行转移的call指令
`call 标号（将当前的IP压栈后，转移到标号处执行指令）`

cpu 执行此格式call指令时，进行的是如下操作： 

1. 
   * (sp)=(sp)-2
   * ((ss)*16+(sp))=(ip)
2. 
   * (ip)=(ip)+16位位移
  
* 16 位位移=标号处的地址-call指令后的第一个字节的地址
* 16位位移的范围为-32768-3276，用**补码**来表示
* 16位位移由编译程序在编译时算出

CPU执行此种格式的call指令时，相当于进行 

* `push IP` 
* `jmp near ptr 标号`

## 检测10.2
```
;p10-2.asm
assume cs:codesg
codesg segment
    start: 
            mov ax,0
            call s
            inc ax
        
        s: pop ax
            mov ax,4c00h
            int 21h
codesg ends
end start
```
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210918104757.jpg)

题目中给出了，机器码，我们可以数字节

* `mov ax,0` 三个字节，执行完，ip为3 cs:[3] 指向 `call s`
* `call s` 三个字节，执行完，ip 为6 
* 栈存储IP的值（push IP）存储6
* ip 指向一条语句cs:[7]
* 跳转到标号s， 并执行`pop ax`, 取出6 放到ax 为6

## 转移的目的地址在指令中的call指令
`call far ptr 标号` 实现的是段间转移

CPU执行此种格式的call指令时，相当于进行：
* `push CS`
* `push IP`
* `jmp far ptr 标号`

```
;p10-3.asm
;题目中给出 cs的初始值是 1000h
assume cs:codesg
codesg segment
    mov ax,4c00h
    int 21h
start: 
    mov ax,0
    call far ptr s  ;CS为1000h入栈，IP为8（数一下机器码），跳转到s
    inc ax          ; 不执行

s: 
    pop ax          ;ax=8, 取栈顶8
    add ax,ax       ;ax=16
    pop bx          ;bx=1000h
    add ax,bx       ;ax=1010h(16 为10H)
    mov bx,0
    push bx
    ret
codesg ends
end start
```
注意： 我的cs地址不是1000H

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210918112314.jpg)
```
;p10-3.asm 实验
assume cs:codesg
codesg segment
    mov ax,4c00h
    int 21h
start: 
    mov ax,0
    call far ptr s  ;CS为076AH ，IP为DH(8+5=13)，跳转到s
    inc ax          ; 不执行

s: 
    pop ax          ;ax=DH, 取栈顶13
    add ax,ax       ;ax=1AH （26）
    pop bx          ;bx=076AH
    add ax,bx       ;ax=0784h
    mov bx,0
    push bx
    ret
codesg ends
end start
```
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210918120341.jpg)


![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210918120424.jpg)

## 转移地址在寄存器中的call指令
`call 16位寄存器`

CPU执行此种格式的call指令时，相当于进行：
* `push IP`
* `jmp 16位寄存器`

先回忆一下bp
```
mov ax, [bp]
mov ax, [bp+8]
mov ax, [bp+si]
mov ax, [bp+si+8]   ;以上段地址默认在ss中
```

sp的默认段是ss

```
;p10-4.asm 实验
assume cs:codesg
codesg segment
start: 
    mov ax,6    ;执行完后ip为3 
    call ax     ;执行完后ip为5 首先将IP压进栈中， jmp 6
    inc ax 
    
    mov bp,sp   ;跳转到这里 此处的含义 ((ss*16)+bp)=((ss*16)+sp)
    add ax,[bp] ;将（ax）= ((ss*16)+bp)(获取栈顶元素5) + (ax)(6)  ax为BH（11）
codesg ends
end start
```
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210918143648.jpg)

## 转移地址在内存call指令

`call word ptr 内存单元地址`

CPU执行此种格式的call指令时，相当于进行：

* `push IP`
* `jmp word ptr 内存单元地址`

```
mov sp, 10h         ;直接将10h赋值到sp
mov ax, 0123h
mov ds:[0], ax
call word ptr ds:[0] ;push IP 意味着sp=sp-2 变为了EH，ds:[0] 为0123h 指令跳转到0123h 即 (IP)=0123H
;执行后，(IP)=0123H，(sp)=0EH
```

`call dword ptr 内存单元地址`

CPU执行此种格式的call指令时，相当于进行：

* `push CS`
* `push IP`
* `jmp dword ptr 内存单元地址`

```
mov sp, 10h
mov ax, 0123h
mov ds:[0], ax
mov word ptr ds:[2],0
call dword ptr ds:[0]
;执行后，（cs）=0 (IP)=0123H，(sp)=0EH
```
## 检测10.5
```
;p10-5-1
assume cs:code
stack segment
    dw 8 dup (0);0-15 16个字节
stack ends

code segment
    start: 
        mov ax,stack
        mov ss,ax   ;将stack段地址放在 ss 中
        mov sp,16   ;设置sp为16 是10H 指向栈顶 栈的结构请看图，栈的存储位置是0-15 
        低16位为栈顶
        mov ds,ax   ;将stack段地址放在 ds 中
        mov ax,0    ;设置ax 为零
        call word ptr ds:[0EH]
        ; push IP  此时的cs:ip 指向的是第一个`inc c`, 将IP偏移数存储到栈顶的位置 此时 sp=sp-2  就是14（0EH）
        ; 地址跳转到ds:[0EH]  就是stack 栈顶,就是读取到，也就是从 inc ac 开始执行又跳了回来 
        ; 此时 ax=0

        inc ax
        inc ax
        inc ax
        ; ax=3
        mov ax,4c00H
        int 21H
code ends
end start
```

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210918163559.jpg)


这道题目得数字节数，重点是理解`call dword ptr ss:[0]` 可以直接debug 进行观察

```
;p10-5-2
assume cs:code
data segment
    dw 8 dup (0); 初始8个字，16个字节空间
data ends
code segment
    start: 
        mov ax,data
        mov ss,ax
        mov sp,16                    ;初始化栈顶
        mov word ptr ss:[0],offset s ;(ss:[0])=001ah(到标号s共计26个字节)
        mov ss:[2],cs                ;(ss:[2])=cs
        call dword ptr ss:[0]        ;cs 入栈，IP入栈 跳转到cs:0001h，此时的IP是19H  
        nop
    s:  
        mov ax,offset s              ;ax为0001h
        sub ax,ss:[0ch]              ;ax=1ah-19h=1
        mov bx,cs                    ;bx=cs
        sub bx,ss:[0eH]              ;bx=(bx)-(ss:[0eH])=> cs-cs=0
        
        mov ax,4c00H
        int 21H
code ends
end start
```

## call 和 ret 的配合使用
分析下面程序

```
assume cs:code
code segment
start:	mov ax,1
	    mov cx,3
     	call s ;（1）CPU指令缓冲器存放call指令，IP指向下一条指令（mov bx, ax），执行call指令，IP入栈，jmp
     	
	    mov bx,ax	;（4）IP重新指向这里  bx = 8
     	mov ax,4c00h
     	int 21h
     s: add ax,ax
     	loop s;（2）循环3次ax = 8
	    ret;（3）return : pop IP
code ends
```

## mul指令

mul是乘法指令，使用 mul 做乘法的时候：相乘的两个数：要么都是8位，要么都是16位。

* 8 位： AL中和 8位寄存器或内存字节单元中；
* 16 位： AX中和 16 位寄存器或内存字单元中。

结果

* 8位：AX中；
* 16位：DX（高位）和 AX（低位）中。

格式：`mul 寄存器` 或 `mul 内存单元`
```
;计算100*10
;100和10小于255，可以做8位乘法
mov al,100
mov bl,10
mul bl

;结果： (ax)=1000（03E8H）
```

```
;计算100*10000
;100小于255，可10000大于255，所以必须做16位乘法，程序如下：
mov ax,100
mov bx,10000
mul bx

;结果： (ax)=4240H，(dx)=000FH     （F4240H=1000000）
```

## 参数和结果传递的问题

调用者将参数送入参数寄存器，从结果寄存器中获取到返回值，子程序从参数寄存器取到参数，将返回值送入到结果寄存器

但是寄存器的数量都是有限的。 这个时候我们将批量数据放到内存中，然后将他们所在内存空间的首地址放在寄存器中，传递给需要的子程序，对于具有批量数据的返回结果，也可以用这个方法

解决寄存器冲突的问题， 在子程序开始将子程序中所用到的寄存器中的内容都保存起来，在子程序返回前在恢复，可以用栈来保存寄存器的内容

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210922180747.jpg)

## 实验10 编写子程序

### 显示字符串

参考实验9

1. （读数据）数据要存储在在是字符高位，也就是每行的偶数位置 显示字符（ASCII）
   * `welcome to masm!` 可以直接在数据段，初始化数据，会自动生成ASCII，能确定内存的位置，初始化字符，使用db,题目已经明确从data数据段开始读。
     * 如果完整读取字符串？
       * 字符串是是随机变化的，我们不能设置固定cx的数值来循环读字符串，我们可以采用`jcxz` 指令来判断是否已经读取完字符串。 当cx为0时，就不在读取字符串，进行跳转。现在明白题目中的字符后边为啥有个零了吗？
```
;关键代码
read:
    mov cl,[si]
    mov ch,0        ;cx中 ch固定为零，关键在于cl的变化是否为零 
    jczx ok         ;跳出，字符串操作完毕
    对字符串业务操作
    inc si          ;下一个字符串
    jmp shrot read ;继续读取字符串
```
  
2. (写数据) `welcome to masm!` 
   * 显示缓冲区的段地址为B800,可以设置es, 字符位置为：es:[bx+si], 属性位es:[bx+si+1]
   * 测试一下显示缓冲区
     * -e b8a0:0
     * b8a0:0000 (依次输入： 41 02 42 02 43 02 ) 查看效果
     * 也就是说从B800:0000H开始，偶数地址是显示的字符，奇数地址是负责前面字符的显示属性的。也就是说在显示缓冲区中，每2个字节负责屏幕上一个字符的显示。

3. (布局)需要让三个行`welcome to masm!` 显示指定的位置，指定行（dh）和列（dl，单位是字，一个字符，两个字节）
   1. 边界条件-换行：一个字符可能出现在两个或以上行，我们只要按内存顺序显示即可
   2. 空白填充：指定行和列意味着字符的前后都有空白字符，我们要准确定位字符的开始位置，我们到定位的是偏移地址。dh * 160 + dl * 2 这里有乘法计算， dh dl  的值最都小于255, 我们使用8位乘法

```
; 关键代码
mov al,dl
mov bl,2
mul bl
;结果在ax中

```

4. 子程序没有返回值

```
;最终程序
;s9-1.asm
;-g 可以一次性把程序执行完
assume cs:code
data segment
    db 'Welcome to masm!', 0        ;内存data段中定义一个字符串
data ends

code segment
    start:   ;字符串参数
        mov dh, 8           ;屏幕的行数
        mov dl, 3           ;所在行的列数
        mov ch, 0           ;ch清零，防止高8位不为零。
        mov cl, 2           ;颜色属性（此处应是二进制数0000 0010）

        mov ax, data
        mov ds, ax
        mov si, 0           ;将ds：si指向字符串
        call show_str

        mov ax, 4c00H
        int 21H
    ;show_str功能 :按行和列及字符属性显示字符串  
    ;入口参数：dh-行数、dl-列数、cl-字符属性、ds:[si]指向字符串。
    ;返回值：无
    
    show_str: 
        push dx
        push cx
        push si ;数据入栈保护

        mov ax,0B800H ;在汇编源程序中，数据不能以字母开头，所以要在前面加0。
        mov es,ax     ;显示缓冲区的段地址

        ;计算dl * 2 
        mov ax,0
        mov al,2
        mul dl
        mov bx,ax ;把结果放到dl

        ; 计算 dh * 160 结果在ax中
        mov ax,0   ;（ax）= 0，防止高位不为零  
        mov al,160 ;0a0H
        mul dh
        add bx,ax  ;获取最终否偏移量

        mov di,0   ;di作为字符的偏移零
        mov al,cl  ; 将属性放到al中，因为后边的程序会用到cx
    read:
        mov cl,ds:[si]
        mov ch,0             ;cx中 ch固定为零，关键在于cl的变化是否为零 
        jcxz ok              ;跳出，字符串操作完毕
        mov es:[bx+di+0],cl  ;对字符串业务操作 写入字符
        mov es:[bx+di+1],al  ; 写入属性
        inc si               ;下一个字符串
        add di,2        
        jmp short read       ;继续读取字符串
    ok:
        pop si 
        pop cx
        pop si
        ret

    code ends
    end start
```

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210923142038.jpg)