---
title: 汇编学习-第六章
date: 2021-09-06 16:21:35
categories:
- 汇编
---

# 包含多个段的程序

程序取得所需空间方法有两种： 
  * 加载程序的时候为程序分配
    * 在源程序中定义段来进行内存空间的获取
  * 程序在执行的过程向系统申请

在程序中，定义我们希望处理的数据，这些数据就会被编译，连接程序作为程序的一部分，写入到可执行文件中。当可执行文件中的程序被载入内存时候，这些数据同时被加载到内存中。同时我们要处理的数据也就自然获取了存储类型

# 在代码段中使用数据
```x86asm
;p61.asm(6.2)
;计算 8 个数据的和存到 ax 寄存器
assume cs:code 

code segment 

	dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h ;define word 定义8个字形数据

	start:	mov bx, 0  ;标号start 指定程序的入口
			mov ax, 0  
			
			mov cx, 8
	s:		add ax, cs:[bx]
			add bx, 2
			loop s 
			
			mov ax, 4c00h 
			int 21h 
code ends
end start    ;end除了通知编译器程序结束外，还可以通知编译器程序的入口在什么地方
	     	 ;用end指令指明了程序的入口在标号start处，也就是说，“mov bx，0”是程序的第一条指令。 即指定了CS:IP指向的位置
```
* 将文件放到 /MYDOSBox 目录
* masm p61; link p61;
* debug p61.exe

# 在代码中使用栈
```x86asm
assume cs:code 
code segment 
    dw 0123h，0456h，0789h，0abch，0defh，0fedh，0cbah，0987h; 0(0H)-15(0FH)单元
	dw 0，0，0，0，0，0，0，0，0，0，0，0，0，0，0，0; 使用16(10H)-47(2FH)单元作为栈使用 30H 为栈顶; 16个字形数据
	
    start: mov ax,cs
           mov ss,ax
           mov sp,30H ;将设置栈顶ss:sp指向栈底cs:30
           mov bx,0
           
           mov cs,8
    s:     push cs:[bx]; 获取((cs*16) + bx)的内容放入到栈顶
           add bx,2
           loop s  ; 以上将代码段0~15单元中的8个字型数据依次入栈

           mov bx, 0
		   mov cx, 8
    s0:    pop cs:[bx] ; 取出栈顶元素放入到（(cs*16)+bx）
           add bx,2
           loop s0 ; 以上依次出栈8个字型数据到代码段0~15单元中
        
           mov ax，4c00h 
		   int 21h 
codesg ends 
end start	
;指明程序的入口在start处
```

## 检测 6.1 

填写： `mov cs:[bx],ax` ;0123H等的数据存放在cs起始位置
```x86asm
assume cs:codesg
codesg segment
        dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h
start:  mov ax,0
        mov ds,ax
        mov bx,0
        mov cx,8
    s:  mov ax,[bx]
        mov cs:[bx],ax
        add bx,2
        loop s
        mov ax,4c00h
        int 21h
codesg ends
end start
```

## 检测6.2
 
* 栈开始的地方，是cs
* 0123h-0987H共计8个字，16个字节(0H-FH)，栈开始的地址是cs:10H, 10个”0“字共计是20个字节， cs:10H-cs:23H， 则栈顶为 24H
```x86asm
assume cs:codesg
codesg segment
        dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h
        dw 0,0,0,0,0,0,0,0,0,0
start:  mov ax, cs;
        mov ss,ax
        mov sp, 24h;
        mov ax,0
        mov ds,ax
        mov bx,0
        mov cx,8
    s:  push [bx]
         pop cs:[bx];
        add bx,2 
        loop s
        mov ax,4c00h
        int 21h
codesg ends
end start
```

将数据、代码、栈放入不同的段

```x86asm
assume cs:code,ds:data,ss:stack; 代码 数据 栈 不同的段有不同的名字，cs和code 相连， ds和data相连, ss和stack相连

data segment  
	dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h ;0-15单元
data ends 

stack segment 
	dw 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0 ;0-31单元
stack ends 

code segment 
	start:	mov ax, stack;将名称为“stack”的段的段地址送入ax
			mov ss, ax
			mov sp, 20h  ;设置栈顶ss:sp指向stack:20。 20h = 32d
			
			mov ax, data ;将名称为“data”的段的段地址送入ax
			mov ds, ax   ;ds指向data段
			
			mov bx, 0    ;ds:bx指向data段中的第一个单元
			
			mov cx, 8
	s:	    push [bx]
			add bx, 2
			loop s       ;以上将data段中的0~15单元中的8个字型数据依次入栈
			
			mov bx, 0
			
			mov cx, 8
	s0:		pop [bx]
			add bx, 2
			loop s0      ;以上依次出栈8个字型数据到data段的0~15单元中
			
			mov ax, 4c00h 
			int 21h 
code ends
end start
;“end start”说明了程序的入口，这个入口将被写入可执行文件的描述信息，
;可执行文件中的程序被加载入内存后，CPU的CS:IP被设置指向这个入口，从而开始执行程序中的第一条指令
```

# 实验5 编写调试具有多个段的入口
## 将以下的程序编译

```x86asm
;s51.asm
assume cs:code,ds:data,ss:stack

data segment
  dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h
data ends

stack segment
  dw 0,0,0,0,0,0,0,0
stack ends

code segment
  start:  mov ax,stack
          mov ss,ax
          mov sp,16

          mov ax,data
          mov ds,ax

          push ds:[0]
          push ds:[2]
          pop ds:[2]
          pop ds:[0]

          mov ax,4c00h
          int 21h
code ends
end start
```
* 将文件放到 /MYDOSBox 目录
* masm s51; link s51;
* debug s51.exe

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210907100115.jpg)

1. cpu执行程序，程序返回前，data段中的数据是多少？
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210907105026.jpg)

2. cpu 执行程序，程序返回前 cs= 076CH  ss=076BH  ds=076AH
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210907102157.jpg)

3. 程序加载后，code 段的段地址为X, 则data 段的段地址为 X-1， 则 stack段的段地址为 x-2

## 将以下的程序编译

```x86asm
;s52.asm
assume cs:code,ds:data,ss:stack
data segment
  dw 0123h,0456h
data ends

stack segment
  dw 0,0
stack ends

code segment
  start:  mov ax,stack
          mov ss,ax
          mov sp,16

          mov ax,data
          mov ds,ax

          push ds:[0]
          push ds:[2]
          pop ds:[2]
          pop ds:[0]

          mov ax,4c00h
          int 21h
code ends
end start
```

1. cpu执行程序，程序返回前，data段中的数据是多少？

我们什么也没执行，此时我们在data段定义的数据在哪？在ds：0100H处（原来讲过，程序最开始时ds：00~ds：100H是留给程序与操作系统通讯使用的psp内存段，参见书中p92）
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210907110727.jpg)

执行以后

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210907142547.jpg)

2. cpu 执行程序，程序返回前 cs= 076CH  ss=076BH  ds=076AH
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210907112056.jpg)

现象： 数据和栈中只定义初始化了4个字节的内存，和前一个实验初始化的数据不一样，但是cs,ss,ds 返回的值却是一样的

虽然我们在data段和stack段中只定义初始化了4个字节的内存，但在汇编中，直接给你分配了16个字节的空间，不足的按00补全。

数据段和栈段在程序加载后实际占据的空间都是以16个字节为单位的。如果不足，以0补全填充。

3. 程序加载后，code 段的段地址为X, 则data 段的段地址为 X-1， 则 stack段的段地址为 x-2

4. 对于如下定义的段：
```x86asm
name segment
     ......
name ends
```
如果段中数据位N个字节，程序加载后，该段实际占据空间为：`（N/16的取整数+1）*16`个字节

如果N小于16，那么实际占用16个字节；如果N大于16，那么实际占用`（N/16的取整数+1）*16`个字节。

segment 会影响内存的分布

## 将下面的程序编译
```x86asm
;s53.asm
assume cs:code,ds:data,ss:stack
code segment
start:
    mov ax,stack      
    mov ss,ax
    mov sp,16      

    mov ax,data       
    mov ds,ax
    push ds:[0]
    push ds:[2]
    pop ds:[2]
    pop ds:[0]

    mov ax,4c00h
    int 21h
code ends

data segment
    dw 0123h,0456h
data ends

stack segment
    dw 0,0
stack ends

end start
```

1. cpu执行程序，程序返回前，data段中的数据是多少？

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210907114208.jpg)
2. cpu 执行程序，程序返回前 cs=076AH  ss=076EH  ds=076DH

这次只不过是将data和stack段放到了code段后面了。那么就要注意它们段地址的变化了。

3. 程序加载后，code 段的段地址为X, 则data 段的段地址为 X+3H， 则 stack段的段地址为 x+4H

data和stack由于定义的都是小于16个字节，一律按照16个字节分配空间，其余补00；剩余的36个字节就是code段真正的可执行的机器码。

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/E6731B6D55B3.png)

上： 代码段 中： data段  下： stack段

4. 如果将(1)、(2)、(3)题中的最后一条伪指令“end start”改为“end”(也就是说不指明程序的入口)，则那个程序仍然可以正确执行？请说明原因。

如果不指明程序的（code段的）入口，并且使用end替换end start，都能正常运行。但只有（3）题中程序可以正确的执行（因为只有它是在内存中可执行代码在最前面）。

讲解：因为如果不指明入口，程序会从加载进内存的第一个单元起开始执行，前二个题中，定义的是数据，但CPU还是将数据当做指令代码执行了。只不过程序执行时逻辑上是错误了。但真的能执行的。

如果指明了程序的入口，CPU会直接从入口处开始执行真正的机器码，直到遇到中断指令返回。此种方式能够确保程序逻辑上的正确。因此有必要为程序来指明入口。

5. 编写code段中的代码，将a段和b段数据依次相加，结果存入c段

```x86asm
;s55.asm
assume cs:code
a segment
    db 1,2,3,4,5,6,7,8 ;  d=define b=byte 字节型  共计8个字节
a ends
b segment
    db 1,2,3,4,5,6,7,8
b ends
cz segment
    db 0,0,0,0,0,0,0,0
cz ends

code segment
start:
    mov ax,a
    mov ds,ax ;ds指向a段

    mov ax,b
    mov es,ax ;es指向b段

    mov bx,0  ;累加计数器，作用是提供位置坐标
    mov cx,8  ;循环计数器，用户是循环数据
s:  
    mov dl,[bx] ;获取a段的数据，将低8位放到(dl)=((ds)*16 + (bx))
    add dl,es:[bx] ;获取b段的数据，将数据相加放到(dl)=((dl)+(es)* 16 + (bx))  
    push ds     ;保护ds值，因为下面用到ds了，
    
    mov ax,cz    ;获取c段地址,有的编译器不认识C这个段的标号，故改成了CZ
    mov ds,ax   ;ds执行c段
    mov [bx],dl ;((ds) * 16 + bx) = (dl)
    
    pop ds      ;将ds恢复到原来的值
    inc bx      ;bx 等增
    loop s

    mov ax,4c00h
    int 21h

code ends
end start
```

思路：
* db的含义：初始化字数据 d=define b=byte
* 分别取出A段和B段数据依次相加，结果存入c段，使用ds, 存储A 段数据，使用es,存储B段
*  根据以上的结论我们知道，当一个数据段不足16个字节时，按16个字节分配内存空间，其余的补0。a、b段都是定义了8个字节的数值。并且是相邻的，那么他们相对于自己的段地址的偏移地址是相同的。比如我们同时取啊a,b 的第三（idata）个元素，a+idata  b+idata
* 系统自动创建的栈空间

执行
* 在未执行前，观察一下  a,b cz在内存中的哪里
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210908095502.jpg)

* 执行程序，观察结果
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210908100915.jpg)

## 编写code段中代码，用push指令将a段中前8个字型数据逆序存储到b段中

```x86asm
;s56.asm
assume cs:code
a segment
    dw 1,2,3,4,5,6,7,8,9,0ah,0bh,0ch,0dh,0eh,0fh,0ffh; 32 个字节
a ends

b segment
    dw 0,0,0,0,0,0,0,0 ;16个字节
b ends

code segment
start:
      mov ax,a ;
      mov ds,ax ;ds指向a段

      mov ax,b
      mov ss,ax ;ss指向b段
      mov sp,16 ;初始化栈顶，注意不是15，0-15共计16个字节，需要一个额外的字节

      mov cx,8
      mov bx,0
  s: 
      push [bx] ;获取a段数据
      add bx,2
      loop s

    mov ax,4c00h
    int 21h

code ends
end start
```

思路：
* dw的含义：初始化字数据 d=define b=word
* 理解掌握栈的原理，先进后出，从高地址向低地址发展。也就是说先压栈的数据，在栈底，最后被pop出。
* 把b段当做栈，把数据直接压进栈，push和pop操作的是一个字

结果：
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210908110030.jpg)

## CX寄存器的变化
目前我们了解到 cx作用是 loop中，当做计数器。

### 原始文件
我们观察一下cx的变化，执行s56文件，观察在未执行程序的时候cx的值
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210908110304.jpg)

有一个初始值 4F(H)

当执行`mov cx,8` cx 发生了改变。达到了预期目的，4F（H）是代表啥
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210908110750.jpg)

### 修改一
将s56 改为
```x86asm
assume cs:code

code segment
start:
    mov cx,10 ;

    mov ax,4c00h
    int 21h

code ends
end start
```
cx 变动如下
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210908111317.jpg)
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210908112759.jpg)
### 修改二
将s56 改为
```x86asm
assume cs:code

code segment
start:
    mov ax,4c00h
    int 21h

code ends
end start
```
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210908111855.jpg)

### 继续观察cx

可以判断，在程序加载的开始，cx代表的是code 段中可执行的字节数。回过头看原始的s56 4FH(79)

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210908113351.jpg) 

此时共计可执行字节是， 076D:0-076D:1E 共计1F（31）个执行字节，可是还差79-31=48个字节。（好特殊的数字 16 的三倍）,其实是包含了a段和b段的字节数，正好48个。在以前的结论我们知道，初始化数据空间会按16的倍数初始化

结论：
在程序执行前，cx代表的所有”段“中的字节数量。