---
title: 汇编学习-第十四章
date: 2021-09-29 15:40:33
tags:
categories:
- 汇编
---
# 端口
在PC机系统中，和CPU通过总线相连的芯片除各种存储器外，还有以下3种芯片。

* 各种接口卡（比如，网卡、显卡）上的接口芯片，它们控制接口卡进行工作；
* 主板上的接口芯片，CPU通过它们对部分外设进行访问；
* 其他芯片，用来存储相关的系统信息，或进行相关的输入输出处理。

在这些芯片中，都有一组可以由CPU读写的寄存器。这些寄存器，它们在物理上可能处于不同的芯片中，
但是它们在以下两点上相同。

* 都和CPU的总线相连，这种连接是通过它们所在的芯片进行的；
* CPU对它们进行读或写的时候都通过控制线向它们所在的芯片发出端口读写命令。

从CPU的角度，将这些寄存器都当作端口，对它们进行统一编址，从而建立了一个统一的端口地址空间。
每一个端口在地址空间中都有一个地址。在访问端口的时候，CPU通过端口地址来定位端口。因为端口所在的芯片和CPU通过总线相连，

CPU可以直接读写以下3个地方的数据。

* CPU内部的寄存器；
* 内存单元；
* 端口。

# 检测14

* 编程，读取CMOS RAM的2号单元内容

```
;j14-1.asm
assume cs:code
code segment

start:  mov al,2        ; （al）=2 2号单元
        out 70h,al      ;将al送入端口70h，选中2号单元
        in al,71h       ;从端口71h处读出2号单元内容，送入al

        mov ax,4c00h
        int 21h

    code ends
    end start
```

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210930095502.jpg)

2. 编程，向CMOS RAM的2号单元写入0
   
```
;j14-2.asm
assume cs:code
code segment

start:  
    mov al, 2       ;（al）=2 2号单元 
    out 70h, al     ;将al送入端口70h，选中2号单元
    mov al, 0       ;（al）=0 写入端口的内容  
    out 71h, al     ;将（al）=0写入到71h端口的2号单元内。

    mov ax,4c00h
    int 21h

code ends
end start
```
# shl和shr指令
shl和shr是逻辑移位指令

shl是逻辑左移指令，它的功能为：

* 将一个寄存器或内存单元中的数据向左移位；
* 将最后移出的一位写入CF中；
* 最低位用0补充。

如果移动位数大于一，必须将移动位数放在cl中。

将X逻辑左移一位，相当于执行X=X*2。将X逻辑右移一位，相当于执行X=X/2

shr是逻辑右移指令，同理
# 检测14.2
题目中已经给出了提示，

1. (ax) * 2左移1位， (ax) * 8是左移3位

```
;j14-3.asm
assume cs:code
code segment

start:  
    mov ax,3       ;我们计算3*10
    mov bx,ax
    shl ax,1       ;左移可以扩大两倍
    
    mov cl,3       ;左移3位扩大8倍，需要单独设置3
    shl bx,cl      
    add ax,bx      ;相加 计算出来是3eh

    mov ax,4c00h
    int 21h

code ends
end start
```
# CMOS ram 中存储的时间

在CMOS RAM中，存放着当前的时间：年、月、日、时、分、秒。长度都为1个字节。

BCD码是以4位二进制数表示十进制数码的编码方法 4 == 0100B
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210930113538.jpg)
一个字节可表示两个BCD码。则CMOS RAM存储时间信息的单元中，存储了用两个BCD码表示的两位十进制数，高4位的BCD码表示十位，低4位的BCD码表示个位。比如，00010100b表示14。

## 编程，在屏幕中间显示当前的月份

```
p14.asm
;编程，在屏幕中间显示当前的月份。
assume cs:code
code segment 
    start:	
        mov al,8 ;从CMOS RAM的8号单元读出当前月份的BCD码。
        out 70h,al 
        in al, 71h ;从数据端口71h中取得指定单元中的数据,注意是一个字节的数据
        
        mov ah, al ;al中为从CMOS RAM的8号单元中读出的数据
        mov cl, 4
        shr ah, cl ;右移四位空出四位,ah中为月份的十位数码值
        and al, 00001111b ;al中为月份的个位数码值
        
        add ah, 30h ;BCD码值+30h=十进制数对应的ASCII
        add al, 30h 
        
        mov bx, 0b800h 
        mov es, bx 
        mov bl, 02h ;文字属性
        mov byte ptr es:[160*12+40*2], ah ;显示月份的十位数码
        mov byte ptr es:[160*12+40*2+1], bl 
        mov byte ptr es:[160*12+40*2+2], al ;接着显示月份的个位数码
        mov byte ptr es:[160*12+40*2+3], bl 
        
        mov ax,4c00h
        int 21h
code ends
end start
```

# 实验14 访问CMOS RAM

程序分析

1. 我们需要 年月日时分秒 信息，共计6个信息。需要到cmos中查找
2. 需要找6次信息，我们需要编写子程序，以防代码过多
3. 读到的信息显示在屏幕上，要转化为对应的ASCII ，可以参考在p14
```
;s14.asm

assume cs:code
code segment 
    start:	
        mov ax,0b800h 
        mov es,ax 
        mov bl,02h ;文字属性
        mov di,160*12+40*2

        mov al,9 ;从CMOS RAM的9单元读出当前月份的BCD码。
        call read_time 

        mov byte ptr es:[di-2], '/';
        mov byte ptr es:[di-1], bl 

        mov al,8 ;从CMOS RAM的8单元读出当前月份的BCD码。
        call read_time 

        mov byte ptr es:[di-2], "/" ;
        mov byte ptr es:[di-1], bl 

        mov al,7 ;从CMOS RAM的8单元读出当前日份的BCD码。
        call read_time 

        mov byte ptr es:[di-2], " " ;
        mov byte ptr es:[di-1], bl 

        mov al,4 ;从CMOS RAM的8单元读出当前日份的BCD码。
        call read_time 

        mov byte ptr es:[di-2], ":" ;
        mov byte ptr es:[di-1], bl 

        mov al,2 ;从CMOS RAM的8单元读出当前日份的BCD码。
        call read_time 

        mov byte ptr es:[di-2], ":" ;
        mov byte ptr es:[di-1], bl 

        mov al,0 ;从CMOS RAM的8单元读出当前日份的BCD码。
        call read_time 

        mov ax,4c00h
        int 21h
        ;------
        ;读取时间信息
        ; 参数 ax 端口地址 di显示缓冲区
        ; 返回值： 无
        ;-----
    read_time:
        push ax

        out 70h,al 
        in al, 71h ;从数据端口71h中取得指定单元中的数据,注意是一个字节的数据
        
        mov ah, al ;al中为从CMOS RAM的8号单元中读出的数据
        mov cl, 4
        shr ah, cl ;右移四位空出四位,ah中为月份的十位数码值
        and al, 00001111b ;al中为月份的个位数码值
        
        add ah, 30h ;十位 BCD码值+30h=十进制数对应的ASCII
        add al, 30h ;个位

        mov byte ptr es:[di], ah ;显示十位数码
        mov byte ptr es:[di+1], bl 
        mov byte ptr es:[di+2], al ;接着显示个位数码
        mov byte ptr es:[di+3], bl 

        add di,6 ;di增加6个字节 3个字。因为十位和个位占用两个字，一会符号会占用一个位置

        pop ax

        ret
        
code ends
end start
```
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210930140916.jpg)