---
title: 汇编学习-第九章
date: 2021-09-13 15:48:12
categories:
- 汇编
---

可以修改IP，或同时修改CS和IP的指令统称为转移指令。概括地讲，转移指令就是可以控制CPU执行内存中某处代码的指令。

8086CPU的转移行为有以下几类。

* 只修改IP时，称为段内转移，比如：`jmp ax`。
* 同时修改CS和IP时，称为段间转移，比如：`jmp 1000:0`。
* 由于转移指令对IP的修改范围不同，段内转移又分为：短转移和近转移。
    * 短转移IP的修改范围为-128 ~ 127。
    * 近转移IP的修改范围为-32768 ~ 32767。

8086CPU的转移指令分为以下几类。

* 无条件转移指令（如：jmp）
* 条件转移指令
* 循环指令（如：loop）
* 过程
* 中断

# 操作符offset

操作符offset在汇编语言中是由编译器处理的符号，它的功能是取得标号的偏移地址。
```x86asm
;将s处的一条指令复制到s0处
assume cs:codesg
codesg segment
 s:   mov ax, bx           ;mov ax,bx的机器码占用两个字节
      mov si, offset s     ;获得标号s的偏移地址
      mov di, offset s0    ;获得标号s0的偏移地址
      
      mov ax,cs:[si]
      mov cs:[di],ax
      
 s0:  nop                     ;（nop的机器码占一个字节）
      nop
 codesg ends
 ends
```

# jmp指令
jmp为无条件转移，转到标号处执行指令可以只修改IP，也可以同时修改CS和IP；

jmp指令要给出两种信息：
* 转移的目的地址
* 转移的位移 （段间转移、段内短转移，段内近转移）（是位移不是位置）
  
`jmp short 标号（段内短转移）`

指令`jmp short 标号`的功能为(IP)=(IP)+8位位移，转到标号处执行指令

* 8位位移 = “标号”处的地址 - jmp指令后的第一个字节的地址；
  * 改变的是一个字节（8个bit），所以叫8位位移
* short指明此处的位移为8位位移；
* 8位位移的范围为-128~127，用补码表示
* 8位位移由编译程序在编译时算出。

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210914105230.jpg)

##  观察jmp short 标号 指令
```
;p91-1.asm
assume cs:codesg

codesg segment 
start:
    mov ax,0123H ;对应的机器码是 B82301 mov对应B8(三个字节)
    jmp short s  ;对应的机器码是 EB03  jmp对应EB(两个字节)
    add ax,1     ;对应的机器码是 050100 add对应05(三个字节)
s:  
    inc ax

	mov ax, 4c00h 
	int 21h 
codesg ends 
end start
```
cs:[0] 中的机器码，对应的指令如图，
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210916102319.jpg)


在一般的汇编指令中，汇编的指令中的idata(立即数)，无论它是表示一个数据，还是内存单元偏移地址，都会在对应的指令中出现，比如 `mov ax,0123H` 对应的机器码是 `B82301`

`jmp 008` 代表的是，转移到`cs:0008`，执行 `inc ax` 

`EB 03` 中的03 计算 

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210916104433.jpg)

## 转移的目的地址在指令中的jmp指令
`jmp far ptr 标号`（段间转移或远转移）

指令 `jmp far ptr 标号` 功能如下：

* (CS) = 标号所在段的段地址；
* (IP) = 标号所在段中的偏移地址。
* far ptr指明了指令用标号的段地址和偏移地址修改CS和IP。
  
```x86asm
assume cs:codesg
codesg segment
   start: mov ax, 0
		  mov bx, 0
          jmp far ptr s ;s被翻译成转移的目的地址0B01 BD0B
          db 256 dup (0) ;转移的段地址：0BBDH，偏移地址：010BH
    s:    add ax,1
          inc ax
codesg ends
end start
```
## 转移地址在内存中的jmp指令

转移地址在内存中的jmp指令有两种格式：
1. `jmp word ptr 内存单元地址`  段内转移

功能：从内存单元地址处开始存放着一个字，是转移的目的*偏移*地址。
```x86asm
mov ax,0123H
mov ds:[0], ax
jmp word ptr ds:[0]
;执行后，(IP)=0123H
```

2. `jmp dword ptr 内存单元地址`（段间转移）

功能：从内存单元地址处开始存放着两个字，高地址处的字是转移的目的段地址，低地址处是转移的目的偏移地址。

* (CS)=(内存单元地址+2) 目的段地址
* (IP)=(内存单元地址) 目的偏移
  
```x86asm
mov ax, 0123H
mov ds:[0], ax
mov word ptr ds:[2], 0;段地址
jmp dword ptr ds:[0]  ;偏移地址
;执行后，
;(CS)=0
;(IP)=0123H
;CS:IP 指向 0000:0123。
```

## 检测9.1 

jmp word ptr [bx+1] 是段内转移，修改了偏移地址IP，让CS:IP指向程序的第一条指令，需要让IP的值0， 也就是ds(data):[1]要为零, data段的第二个字节为零

```x86asm
;p92.asm
assume cs:codesg
data segment 
    dw 2 dup (0) ;ds:[1]要为零
data endst
codesg segment
   start: mov ax, data
		  mov ds, ax
          mov bx,0
          jmp word ptr [bx+1]

codesg ends
end start
```
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210914094010.jpg)

---

```x86asm
;p93
;和上一道题目类似，但是这个是段间转移。让CS:IP指向程序的第一条指令。需要让偏移地址IP的值0，cs地址保持是原始的地址,内存单元dword 是两个字， 第一个字对应的是IP 要为0,低二个字是cs要保持原来的值不变

assume cs:codesg
data segment 
    dd 12345678H
data ends
codesg segment
   start: mov ax,data
          mov ds,ax
          mov bx,0
          mov [bx],bx
          mov [bx+2],cs
          jmp dword ptr ds:[0]

codesg ends
end start
```
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210914113546.jpg)


---
```x86asm
;p94
assume cs:codesg
codesg segment
   start: mov ax,2000H
          mov es,ax
          jmp dword ptr es:[1000H]

codesg ends
end start
```
要注意cs,ip的高位和低位,和内存的高位低位的位置关系

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210914110413.jpg)

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/WX20210901-173318.png)

# jcxz指令和loop指令
## jcxz指令
jcxz指令为有条件转移指令，所有的有条件转移指令都是短转移，在对应的机器码中包含转移的位移，而不是目的地址。对IP的修改范围都为-128~127。

指令格式：`jcxz 标号（如果(cx)=0，则转移到标号处执行。）`

当(cx) = 0时，(IP) = (IP) + 8位位移
* 8位位移 = “标号”处的地址 - jcxz指令后的第一个字节的地址；
* 8位位移的范围为-128~127，用补码表示；
* 8位位移由编译程序在编译时算出。

当(cx)!=0时，什么也不做（程序向下执行）

我们从jcxz的功能可以看出，jcxz标号的功能相当于
`if((cx)==0) jmp shot 标号`

## 检测点9.2

先参考检测9.3，然后在做这个实验

标号s前面是将ds：bx指向了段地址为2000H的内存段。s标号到jmp short s是一个循环（死循环，除非有跳出语句）。

先执行 `-e 2000:0` 数据填写 3 2 0。


![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210914151000.jpg)
```x86asm
;p95.asm
assume cs:codesg
codesg segment
   start:  
        mov ax,2000H
        mov ds,ax
        mov bx,0
    s:   
        mov cl,[bx]; cx的低位
        mov ch,0   ; cx的高位固定为零
        jcxz ok; 跳转到ok
        inc bx ;寻址
        jmp short s
    ok:
        mov dx, bx
        
        mov ax, 4c00h 
	    int 21h 
codesg ends
end start
```

## loop指令

loop指令为循环指令，所有的循环指令都是短转移，在对应的机器码中包含转移的位移，而不是目的地址。

对IP的修改范围都为-128~127。

指令格式：`loop 标号((cx)=(cx)- 1`，如果(cx) ≠ 0，转移到标号处执行)。
操作：
* `(cx)=(cx)-1`；
* 如果 `(cx)!=0`，`(IP)=(IP)+8位位移`。

* 8位位移 = 标号处的地址 - loop指令后的第一个字节的地址；
* 8位位移的范围为-128~127，用补码表示；
* 8位位移由编译程序在编译时算出。
* 如果（cx）= 0，什么也不做（程序向下执行）。

## 检测9.3

比较有意思的题目

先执行 `-e 2000:0` 数据填写 3 2 0。

loop 跳出循环的标志是 判断 `(cx)=(cx)-1` 运行之后，cx的结果是否为零。 如果你不填写这个空格，进行调试代码，你会发现loop 会进入死循环。

 由于是逐个字节的比较，bx的偏移量应该是以字节为单元。我们使用的cx寄存器是16位的，我们只需要低8位的cl寄存器就可以了。为了保证ch为0，首先必须置零。它们组合在一起就是cx的整体值（dh+dl）

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210914145448.jpg)

我们需要让cx 变为零的时候，cx 自动加一 ，这样我们才能让loop 跳出循环

```x86asm
;p96.asm
assume cs:codesg
codesg segment
   start:  
        mov ax,2000H
        mov ds,ax
        mov bx,0
    s:  mov cl,[bx]; cx的低位
        mov ch,0   ; cx的高位固定为零
        
        inc cx ;需要让cx变为一，才能跳出循环
        inc bx
        loop s
    ok:
        dec bx ;dec指令的功能和inc 相反，dec bx进行的操作为（bx）=(bx)-1
        mov dx,bx
        mov ax, 4c00h 
	    int 21h 
codesg ends
end start
```

# 实验8 分析一个奇怪的程序
```
;s8.asm
assume cs:codesg
codesg segment

        mov ax, 4c00H
        int 21H

start:  
        mov ax, 0
    s:  nop  ; 
        nop  ; 

        mov di, offset s  ; 将s 的标号处的偏移地址给di
        mov si, offset s2 ; 将s2 的标号处的偏移地址给si
        mov ax, cs:[si]   ; 将cs:si 指向的内存单元的内容赋值给了ax
        mov cs:[di], ax   ; s2标号的机器码赋值给s标号处，就是两个nop的位置

    s0: jmp short s
    
    s1: mov ax, 0
        int 21H
        mov ax, 0

    s2: jmp short s1
        nop
codesg ends
end start
```

1.  程序的入口是`end start` 
2.  `mov ax, 0`将ax设置为零
3.  标号语句`nop`作用，在运行时在代码段分配一个字节的空间,机器码是90,它的作用是方便在程序运行时代码段分配空间，感觉是占位符的作用，cpu 不会执行
4.  标号s处理完成之后，目的就是 s2标号的机器码赋值给s标号处，就是两个nop的位置，也就是将`jmp short s1` 这条指令复制到了s的标号后边。
5. 我们分析一下 `jmp short s1`（程序还没执行这条语句），short 是短转移， 如何计算确定偏移地址IP. 根据公式 `8位位移 = “标号”处的地址 - jmp指令后的第一个字节的地址` 
   
    计算偏移地址 ![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/WX20210915-174811@2x.png)
   
    我们代码位移的偏移量是10个字节，但是是-10
   
    将-10 翻译为16进制 F6H， `jmp short s1` 对应的机器码是 `EBF6`，EB代表jmp指令，F6代表了自此偏移地址开始，向前偏移10个字节）
6. 程序执行 s0 标号的内容 `jmp short s`, 又一个跳转，我们按照5的思路分析，确定一下IP 偏移地址
7. 程序来到s标号处，此处执行 机器码 `BEF6`，执行它 向前偏移10个字节。  `jmp short s` 占用两个字节，`mov ax,0` 3个字节， `int 21` 两个字节， `mov ax, 4c00h` 三个字节， 正好执行 `mov ax, 4c00h ;int 21` 这两条语句， 代表程序结束。

实操Debug

1. 加载程序，查看cs:0 处的内存，查看 两个 `nop` 位置

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210915183817.jpg)

2. 验证步骤5, 当我们执行完 `mov cs:[di], ax` 我们查看cs:[8] 处的内存，我们看到 确实是 `EBF6`

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210915185008.jpg)

## 实验9 根据材料编程

自行画一下显示缓冲区布局，大概如图

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/4F83792E-1B12-4A10-9A9E-F09360E1B41A.png)

1. （读数据）先解决文字排版，数据要存储在在是字符高位，也就是每行的偶数位置 显示字符（ASCII）
   * `welcome to masm!` 可以直接在数据段，初始化数据，会自动生成ASCII，能确定内存的位置，初始化字符，使用db
   * 读取的数据放在al中
  
2. (写数据) `welcome to masm!` 一共16 个字符，需要循坏16 次写完
   * 显示缓冲区的段地址为B800,可以设置es, 字符位置为：es:[bx+si], 属性位es:[bx+si+1]
   * 测试一下显示缓冲区
     * -e b8a0:0
     * b8a0:0000 (依次输入： 41 02 42 02 43 02 ) 查看效果
     * 也就是说从B800:0000H开始，偶数地址是显示的字符，奇数地址是负责前面字符的显示属性的。也就是说在显示缓冲区中，每2个字节负责屏幕上一个字符的显示。

3. (布局)需要让三个行`welcome to masm!` 显示在屏幕中间，
   1. 水平：`welcome to masm!` 共计16个字符， 显示缓冲区每行80个字符（160个字节），那么从第32个字符（第64个字节）开始，可以在中间显示
   2. 垂直： 共计25行  在 12，13，14行书写可以显示在中间
   3. 测试一下第一行： 计算一下偏移字节 11*160 + 64 =1824（字节）(720H)  低位为：B800:720
   4. -e B800:7A0
   5. (依次输入（32个数）： 41 02 41 02 41 02 41 02 41 02 41 02 41 02 41 02 41 02 41 02 41 02 41 02 41 02 41 02 41 02 41 02)  
   6. 第一行和第二行相差 64 个字符128 个字节
   7. 测试一下第二行： 计算一下偏移字节 （11*160 + 64）+ 16 * 2 + 128=1984（7C0）  低位为：B800:7C0
   8.  -e B800:7C0
   9. (依次输入（32个数）： 41 02 41 02 41 02 41 02 41 02 41 02 41 02 41 02 41 02 41 02 41 02 41 02 41 02 41 02 41 02 41 02)  

 1.  题目的关键点是有循环嵌套，
     1.  有三个字符串显示
     2.  每个字符串有16个字符

```
;s91.asm
assume cs: codesg, ds: datasg 

datasg segment 
	db 'welcome to masm!' 
    db 02H,02H,24H,71H         ;字符显示的属性值 依次是绿色 绿底红色  白底蓝色
datasg ends

codesg segment 
	start:	
        mov ax,datasg 
		mov ds,ax      ;数据段地址

        mov ax,0B800H  ;在汇编源程序中，数据不能以字母开头，所以要在前面加0。
        mov es,ax     ;显示缓冲区的段地址

		mov bx,0      ;设置db段偏移量
        mov si,1824   ;显示缓冲区偏移量
        mov di,16     ;属性偏移量
		mov cx,4      ;3个字符串循环 因为回车会导致换行 我这用了4个字符
	s:	
        push cx       ;保护cx
        mov cx,16     ;开始循环字符串

    write:
        mov al,[bx]    ;读取字符
		mov es:[si],al ;((es)*16 + si)=(al)写内容
        mov ah,[di]    ;读取字符
        mov es:[si+1],ah  ;写属性
		inc bx 
        add si,2  
		loop write

        mov bx, 0      ;恢复bx从新读取字符串
        inc di
        add si,128    ;换行（128）
        pop cx        ;恢复cx 

        loop s
			
		mov ax, 4c00h 
		int 21h 
codesg ends 
end start
```
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210916175828.jpg)