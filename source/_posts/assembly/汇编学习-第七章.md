---
title: 汇编学习-第七章
date: 2021-09-08 14:18:13
categories:
- 汇编
---

# 更灵活的定位内存地址方法

## and 和 or
and指令：逻辑与指令，按位进行与运算。

```x86asm
mov al, 01100011B
and al, 00111011B
```
和&& 的逻辑类似

or指令：逻辑或指令，按位进行或运算。
```x86asm
mov al, 01100011B
or al, 00111011B
```
和|| 的逻辑类似

## 关于ASCII码
世界上有很多编码方案，有一种方案叫做ASCII编码，是在计算机系统中通常被采用的。简单地说，所谓编码方案，就是一套规则，它约定了用什么样的信息来表示现实对象。比如说，在ASCII编码方案中，用61H表示“a”，62H表示“b”。最通用的信息交换标准

[ASCII全部编码](https://baike.baidu.com/item/ASCII/309296)

```x86asm
;p73.asm
assume cs:code,ds:data 

data segment 
	db 'unIx'   ;相当于“db 75H，6EH，49H，58H”
	db 'foRK'   ;
data ends 

code segment
start:	mov al, 'a'  ;相当于“mov al, 61H”，“a”的ASCI码为61H；
		mov bl, 'b'
		
		mov ax, 4c00h 
		int 21h 
code ends
end start
```
查看data 数据
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210908145829.jpg)

查看 `al` `bl` 数据
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210908145928.jpg)
71=72
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210908150938.jpg)

小写字母的ASCII码值比大写字母的ASCII码值大20H


```x86asm
;p74.asm
assume cs:codesg,ds:datasg 

datasg segment 
	db 'BaSiC'
	db 'iNfOrMaTion'
datasg ends

codesg segment 
	start:	mov ax, datasg 
			mov ds, ax	;设置ds 指向 datasg段
		
			mov bx, 0	;设置（bx）=0，ds:bx指向’BaSic’的第一个字母
			
			mov cx, 5     	 ;设置循环次数5，因为’Basic'有5个字母
	s:		mov al, [bx]     ;将ASCII码从ds:bx所指向的单元中取出
			and al, 11011111B;将al中的ASCII码的第5位置为0，变为大写字母
			mov [bx], al	 ;将转变后的ASCII码写回原单元
			inc bx		     ;（bx）加1，ds:bx指向下一个字母
			loop s 
			
			mov bx, 5	;设置（bx）=5，ds:bx指向，iNfOrMaTion'的第一个字母
			
			mov cx, 11	;设置循环次数11，因为‘iNfOrMaTion'有11个字母
	s0:		mov al, [bx]
			or al, 00100000B;将a1中的ASCII码的第5位置为1，变为小写字母
			mov [bx], al 
			inc bx
			loop s0
			
			mov ax, 4c00h 
			int 21h 
codesg ends
end start
```
程序未执行前
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210908151817.jpg)

程序执行后
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210908152118.jpg)

## `[bx+idata]`
`[bx+idata]`表示一个内存单元, 
例如：`mov ax, [bx+200]` 代表的数学公式： `(ax)=((ds) * 16 + bx + 200)`
该指令也可以写成如下格式：

```x86asm
mov ax, [200+bx]
mov ax, 200[bx]
mov ax, [bx].200
```
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210908155016.jpg)
执行完成
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210908154919.jpg)

改写;p74.asm 程序
```x86asm
;p75.asm
assume cs:codesg,ds:datasg 

datasg segment 
	db 'BaSiC'
	db 'iNfOrMaTion'
datasg ends

codesg segment 
	start:	mov ax, datasg 
			mov ds, ax	;设置ds 指向 datasg段
		
			mov bx, 0	;设置（bx）=0，ds:bx指向’BaSic’的第一个字母
			
			mov cx, 5     	 ;设置循环次数5，因为’Basic'有5个字母
	s:		mov al, [bx]     ;将ASCII码从ds:bx所指向的单元中取出
			and al, 11011111B;将al中的ASCII码的第5位置为0，变为大写字母
			mov [bx], al	 ;将转变后的ASCII码写回原单元

		    mov al, [bx+5]
			or al, 00100000B;将a1中的ASCII码的第5位置为1，变为小写字母
			mov [bx+5], al 
			inc bx
			loop s
			
			mov ax, 4c00h 
			int 21h 
codesg ends
end start
```

## SI 、DI 与 寻址方式的灵活应用
### si 、di
si和di是8086CPU中和bx功能相近的寄存器，si和di不能够分成两个8位寄存器来使用。 si di 容量是字
```
;p771.asm
assume cs: codesg, ds: datasg 

datasg segment 
	db 'welcome to masm!' ;用si和di实现将字符串‘welcome to masm！
	db '................'
datasg ends

codesg segment 
	start:	mov ax,datasg 
			mov ds,ax 
			mov si,0  ;设置db1段偏移数
			mov di,16 ;设置db2段偏移数

			mov cx, 8
	s:		mov ax, 0[si]  ;[0 + si]
			mov [di], ax   ;[16 + si] 使用[bx +idata]方式代替di，使程序更简洁
			add si, 2      ;一个字
            add di, 2 
			loop s 
			
			mov ax, 4c00h 
			int 21h 
codesg ends 
end start
```
执行程序前
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210908174428.jpg)

执行以后
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20210908174532.jpg)

### [bx + si] 和 [bx + di]
`[bx+si]`和[`bx+di]`的含义相似

`[bx+si]`表示一个内存单元，它的偏移地址为（bx）+（si）

指令`mov ax, [bx + si]`的含义：将一个内存单元字数据的内容送入ax，段地址在ds中

该指令也可以写成如下格式：`mov ax, [bx][si]`

改写p771.asm
```
;p772.asm
assume cs: codesg, ds: datasg 

datasg segment 
	db 'welcome to masm!' ;用si和di实现将字符串‘welcome to masm！
	db '................'
datasg ends

codesg segment 
	start:	mov ax,datasg 
			mov ds,ax 
			mov si,0  ;设置db1段偏移数

			mov cx, 8
	s:		mov ax, 0[si]  ;[0 + si]
			mov 16[si], ax   ;[16 + si] 使用[bx +idata]方式代替di，使程序更简洁
			add si, 2        ;一个字
			loop s 
			
			mov ax, 4c00h 
			int 21h 
codesg ends 
end start
```

### `[bx+si+idata]`和`[bx+di+idata]`
`[bx+si+idata]`表示一个内存单元，它的偏移地址为`（bx）+（si）+idata`

指令`mov ax，[bx+si+idata]`的含义：将一个内存单元字数据的内容送入ax，段地址在ds中

### 不同的寻址方式的灵活应用
[idata]用一个常量来表示地址，可用于直接定位一个内存单元；
[bx]用一个变量来表示内存地址，可用于间接定位一个内存单元；
[bx+idata]用一个变量和常量表示地址，可在一个起始地址的基础上用变量间接定位一个内存单元；
[bx+si]用两个变量表示地址；
[bx+si+idata]用两个变量和一个常量表示地址。

<!--  TODO://实验6 一般来说，在需要暂存数据的时候我们都应该使用栈 -->
