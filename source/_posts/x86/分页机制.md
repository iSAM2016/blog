---
title: 真象还原-分页机制
date: 2021-10-17 09:50:46
tags:
categories:
- 操作系统
---

# 获取物理内存的方法
> 真象还原第5章
计算机首先要获取物理内存，才能在此基础上继续工作。

有三总方法，获取物理内存，都是 BIOS 中断 0x15 的 3 个子功能，子功能号要存放到寄存器 EAX 或 AX 中，如下
* EAX=0xE820：遍历主机上全部内存。
* AX=0xE801： 分别检测低 15MB 和 16MB～4GB 的内存，最大支持 4GB。
* AH=0x88：最多检测出 64MB 内存，实际内存超过此容量也按照 64MB 返回。

## EAX=0xE820：遍历主机上全部内存

BIOS 中断 0x15 的子功能 0xE820 能够获取系统的内存布局，由于系统内存各部分的类型属性不同，
BIOS 就按照类型属性来划分这片系统内存，所以这种查询呈迭代式，每次 BIOS 只返回一种类型的内存信息

内存信息的内容是用地址范围描述符来描述的，用于存储这种描述符的结构称之为地址范围描述符ARDS。

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017100919.jpg)

其中的 Type 字段用来描述这段内存的类型。

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017101445.jpg)

为什么 BIOS 会按类型来返回内存信息呢？原因是这段内存可能是。
* 系统的 ROM。
* ROM 用到了这部分内存。
* 设备内存映射到了这部分内存。
* 由于某种原因，这段内存不适合标准设备使用。

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017101728.jpg)

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017101753.jpg)

根据表 5-3 中的说明，此中断的调用步骤如下。
1. 填写好“调用前输入”中列出的寄存器。
2. 执行中断调用 int 0x15。
3. 在 CF 位为 0 的情况下， “返回后输出”中对应的寄存器便会有对应的结果。

## 利用BIOS中断0x15子功能0xe801获取内存
此方法最大只能识别 4GB 内存

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017101934.jpg)

此中断的调用步骤如下。
1. 将 AX 寄存器写入 0xE801。
2. 执行中断调用 int 0x15。
3. 在 CF 位为 0 的情况下， “返回后输出”中对应的寄存器便会有对应的结果。

## 利用 BIOS 中断 0x15 子功能 0x88 获取内存

此方法识别最大 64MB 的内存。
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017102113.jpg)

中断返回后， AX 寄存器中的值，其单位是 1KB。此中断的调用步骤如下。
1. 将 AX 寄存器写入 0x88。
2. 执行中断调用 int 0x15。
3. 在 CF 位为 0 的情况下， “返回后输出”中对应的寄存器便会有对应的结果。

# 启用内存分页机制

我们的程序虽然已经进了保护模式，地址空间到达了前所未有的 4GB.如果没有分页机制，内存碎片过程，无法解决。如图

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017104901.jpg)
# 一级分页

分页机制要建立在图 5-6 所示分段机制的基础上，也就是说，段部件的工作依然免不了，所以分页只能是在分段之后进行的，其过程如图 5-7 所示
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017105038.jpg)

图 5-7 说明， CPU 在不打开分页机制的情况下，是按照默认的分段方式进行的，段基址和段内偏移地址经过段部件处理后所输出的线性地址， CPU 就认为是物理地址。

如果打开了分页机制，段部件输出的线性地址就不再等同于物理地址了，我们称之为虚拟地址，它是逻辑上的，是假的，不应该被送上地址总线）。 CPU 必须要拿到物理地址才行，此虚拟地址对应的物理地址需要在页表中查找，这项查找工作是由页部件自动完成的。

分页机制的思想是：通过映射，可以使连续的线性地址与任意物理内存地址相关联，逻辑上连续的线性地址其对应的物理地址可以不连续

分页机制的作用有两方面
* 将线性地址转换成物理地址。
* 用大小相等的页代替大小不等的段。

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017105606.jpg)
由于有了线性地址到真实物理地址的这层映射，经过段部件输出的线性地址便有了另外一个名字:*虚拟地址*

可以肯定不是采用线性地址和物理地址一一映射的关系。
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017112144.jpg)

所以需要改变比例关系。

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017112402.jpg)

若滑块指向第 12 位，内存块大小则为 2 的 12 次方，即 4KB(0x1000)，内存块数量则为 2 的 20 次方， 1M，即1048576 个。这里所说的内存块，其官方名称是页， CPU 中采用的页大小就是4KB.
计算公式如下

`4*2^30(4GB)=4*2^10(4KB)*2^20(个)`

页是地址空间的计量单位，并不是专属物理地址或线性地址，只要是 4KB(0x1000) 的地址空间都可以称为一页，*所以线性地址的一页也要对应物理地址的一页*。

一页大小为 4KB，这样一来， 4GB 地址空间被划分成 4GB/4KB=1M 个页， 也就是 4GB 空间中可以容纳 1048576 个页， 页表中自然也要有 1048576 个页表项，这就是我们要说的一级页表。

首先我们要明确。在 32 位保护模式下任何地址都是用 32 位二进制表示的，包括虚拟地址也是。
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017114530.jpg)

在进行地址转化的时候，有两个前提。

* 分页机制打开前要将页表地址加载到控制寄存器 cr3 中，这是启用分页机制的先决条件之一， 也就是在一级页表已经生成了


![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017142215.jpg)

*一级页表转换原理*

在我们的机器上即使只有 512MB 的内存，每个进程自己的内存空间也是 4GB，这是指虚拟内存空间。当一个程序加载时，操作系统既要在左边虚拟内存分配空间，又在右边的物理内存中分配相应的页面（页不一定是连续的），假设分配到了段空间（段是连续的）0x00200000 长度为8200 字节

此时段部件输出的是线性地址，或者叫虚拟地址。我们在来看一下， 线性地址转为物理地址的过程。
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/QQ20211017-160749.png)

如果执行指令`mov edx,[0x20002]`

此时段部件是0x0020000，加上指令中给出的偏移量是0x2002,得到线性地址为0x00202002。 线性地址高20位是表格索引，0x00202.乘以4是0x00808,就是表内偏移。看图从该单元去除一个0x00007000是页的物理地址。

线性地址的低12位是页内偏移量，用页的物理地址加上页内偏移量是 0x00007002，就是要访问的物理地址

# 二级页表

要解决的问题，不要一次性地将全部页表项建好，需要时动态创建页表项。

无论是几级页表，标准页的尺寸都是 4KB，所以 4GB 线性地址空间最多有 1M 个标准页。

* 一级页表是将这 1M 个标准页放置到一张页表中
* 二级页表是将这 1M 个标准页平均放置 1K 个页表中。每个页表中包含有 1K 个页表项。页表项是 4 字节大小，页表包含 1K 个页表项，故页表大小为4KB，这恰恰是一个标准页的大小。

页目录表来存储这些页表。每个页表的物理地址在页目录表中都以页目录项（PDE）的形式存储，页目录项大小同页表项一样，都用来描述一个物理页的物理地址，其大小都是 4 字节，而且最多有 1024 个页表，所以页目录表也是 4KB 大小，同样也是标准页的大小

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017165045.jpg)

线性地址（虚拟地址）转换为绝对地址过程.

0x1234567 的二进制形式是： 0000 0001 0010 0011 0100 0101 0110 0111。

1. 用虚拟地址的高 10 位(0000 0001 00b)(0x4)乘以 4(0x10)，作为页目录表内的偏移地址，加上页目录表的物理地址，所得的和，便是页目录项的物理地址。读取该页目录项，从中获取到页表的物理地址。(得到0x1000)
2. 用虚拟地址的中间 10 (10 0011 0100b)(0x234)位乘以 4（0x8d0），作为页表内的偏移地址，加上在第 1 步中得到的页表物理地址，所得的和(0x8d0+0x1000=0x18d0)，便是页表项的物理地址。读取该页表项，从中获取到分配的物理页地址(0xfa000)
3. 虚拟地址的低 12 位加上第 2 步中得到的物理页地址，所得的和便是最终转换的物理地址(0xfa000+0x567=0xfa567)

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017165332.jpg)


每个任务都有自己的页表。图 5-15 所示是多个进程并行时，在各自虚拟空间中的情况

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017170831.jpg)

# 补充说明虚拟内存空间

尽管有很多任务，而且每个任务都有自己的4GB虚拟内存空间，但是在整个系统中，物理页面是统一调配的，比如：

* 任务A有一个段，段基址为0x00050000 段长为3000 字节，操作系统为他分配了一个物理地址为0x08001000的页，过了一会任务B 开始加载
* 任务B有一个段，段基址为0x00050000 段长为4096 字节，操作系统为他分配了一个不同的，物理地址为0x00700000的页，
  
在这样的情况下任务A访问线性地址0x00050006 访问的其实是物理地址0x08001006,而任务B内访问同样的线性地址，访问的却是物理地址0x00700006

另一个疑惑点是：每个任务都有4GB的虚拟内存空间，而物理地址只有一个，最大也是4GB,根本不够分。操作系统可以将暂时不用的页退到磁盘，调入麻绳要使用的页，通过这种手段来实现分页内存管理。这就是内存比较小，程序越来越慢，磁盘工作指示灯不同闪烁的原因

# 各个地址的区别

* 在实模式下， “段基址+段内偏移地址”经过段部件的处理，直接输出的就是物理地址， CPU 可以直接用此地址访问内存。

* 在保护模式下， “段基址+段内偏移地址”称为线性地址，不过，此时的段基址已经不再是真正的地址了，而是一个称为选择子的东西。它本质是个索引，类似于数组下标，通过这个索引便能在 GDT 中找到相应的段描述符， 在该描述符中记录了该段的起始、 大小等信息， 这样便得到了段基址。 
    * 若没有开启地址分页功能，此线性地址就被当作物理地址来用。
    * 若开启了分页功能，此线性地就是虚拟地址。虚拟地址要经过 CPU 页部件转换成具体的物理地址

无论在实模式或是保护模式下，段内偏移地址又称为有效地址，也称为逻辑地址，这是程序员可见的地址。这是因为，最终的地址是由段基址和段内偏移地址组合而成的。

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211018093553.jpg)

# 页目录和页表项

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017171054.jpg)

属性不在单独介绍

# 启用页表机制
我们要按顺序做好三件事。

1. 准备好页目录表及页表。
2. 将页表地址写入控制寄存器 cr3。
3. 寄存器 cr0 的 PG 位置 1。

## 将页表地址写入控制寄存器 cr3

控制寄存器 cr3 用于存储页表物理地址，所以 cr3 寄存器又称为页目录基址寄存器（PDBR）。

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017171826.jpg)

由于页目录表所在的地址要求在一个自然页内，即页目录的起始地址是 4KB 的整数倍，低 12 位地址全是 0。所以，只要在 cr3 寄存器的第 31～12 位中写入物理地址的高 20 位就行了。

mov 指令中控制寄存器与通用寄存器互传数据的格式是： `mov cr[0～7]`， `r32` 或 `mov r32， cr[0～7]`。

## 寄存器 cr0 的 PG 位置 1

启动分页机制的开关是将控制寄存器 cr0 的 PG 位置 1，PG 位是 cr0 寄存器的最后一位： 第 31 位。

# 设计页表

设计页表其实就是设计内存布局

用户进程必须运行在低特权级，当用户进程需要访问硬件相关的资源时，需要向操作系统申请，由操作系统去做，之后将结果返回给用户进程。进程可以有无限多个，而操作系统只有一个，所以，操作系统必须“共享”给所有用户进程。(api调用)

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017172808.jpg)
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017173016.jpg)

*页表中实现用户进程共享操作系统的*

只要操作系统属于用户进程的虚拟地址空间就能实现共享。把 4GB 虚拟地址空间分成两部分，一部分专门划给操作系统，
另一部分就归用户进程使用，虚拟地址空间的 0～3GB 是用户进程， 3GB～4GB 是操作系统。让所有用户进程 3GB～4GB 的虚拟地址空间都指向同一个操作系统，也就是所有进程的虚拟地址 3GB～4GB， 本质上都是指向的同一片物理页地址，这片物理页上是操作系统的实体代码。

# 启用分页机制

页表将按照如下方式部署，如图 5-20 所示。

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017174441.jpg)

页目录表和页表都存在于物理内存之中，具体在物理地址 0x100000 处。

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211017174853.jpg)

```
;loader.S
   %include "boot.inc"
   section loader vstart=LOADER_BASE_ADDR
   LOADER_STACK_TOP equ LOADER_BASE_ADDR
   
;构建gdt及其内部的描述符
   GDT_BASE:   dd    0x00000000 
	       dd    0x00000000

   CODE_DESC:  dd    0x0000FFFF 
	       dd    DESC_CODE_HIGH4

   DATA_STACK_DESC:  dd    0x0000FFFF
		     dd    DESC_DATA_HIGH4

   VIDEO_DESC: dd    0x80000007	       ; limit=(0xbffff-0xb8000)/4k=0x7
	       dd    DESC_VIDEO_HIGH4  ; 此时dpl为0

   GDT_SIZE   equ   $ - GDT_BASE
   GDT_LIMIT   equ   GDT_SIZE -	1 
   times 60 dq 0					 ; 此处预留60个描述符的空位(slot)
   SELECTOR_CODE equ (0x0001<<3) + TI_GDT + RPL0         ; 相当于(CODE_DESC - GDT_BASE)/8 + TI_GDT + RPL0
   SELECTOR_DATA equ (0x0002<<3) + TI_GDT + RPL0	 ; 同上
   SELECTOR_VIDEO equ (0x0003<<3) + TI_GDT + RPL0	 ; 同上 

   ; total_mem_bytes用于保存内存容量,以字节为单位,此位置比较好记。
   ; 当前偏移loader.bin文件头0x200字节,loader.bin的加载地址是0x900,
   ; 故total_mem_bytes内存中的地址是0xb00.将来在内核中咱们会引用此地址
   total_mem_bytes dd 0					 
   ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

   ;以下是定义gdt的指针，前2字节是gdt界限，后4字节是gdt起始地址
   gdt_ptr  dw  GDT_LIMIT 
	    dd  GDT_BASE

   ;人工对齐:total_mem_bytes4字节+gdt_ptr6字节+ards_buf244字节+ards_nr2,共256字节
   ards_buf times 244 db 0
   ards_nr dw 0		      ;用于记录ards结构体数量

   loader_start:
   
;-------  int 15h eax = 0000E820h ,edx = 534D4150h ('SMAP') 获取内存布局  -------

   xor ebx, ebx		      ;第一次调用时，ebx值要为0
   mov edx, 0x534d4150	      ;edx只赋值一次，循环体中不会改变
   mov di, ards_buf	      ;ards结构缓冲区
.e820_mem_get_loop:	      ;循环获取每个ARDS内存范围描述结构
   mov eax, 0x0000e820	      ;执行int 0x15后,eax值变为0x534d4150,所以每次执行int前都要更新为子功能号。
   mov ecx, 20		      ;ARDS地址范围描述符结构大小是20字节
   int 0x15
   jc .e820_failed_so_try_e801   ;若cf位为1则有错误发生，尝试0xe801子功能
   add di, cx		      ;使di增加20字节指向缓冲区中新的ARDS结构位置
   inc word [ards_nr]	      ;记录ARDS数量
   cmp ebx, 0		      ;若ebx为0且cf不为1,这说明ards全部返回，当前已是最后一个
   jnz .e820_mem_get_loop

;在所有ards结构中，找出(base_add_low + length_low)的最大值，即内存的容量。
   mov cx, [ards_nr]	      ;遍历每一个ARDS结构体,循环次数是ARDS的数量
   mov ebx, ards_buf 
   xor edx, edx		      ;edx为最大的内存容量,在此先清0
.find_max_mem_area:	      ;无须判断type是否为1,最大的内存块一定是可被使用
   mov eax, [ebx]	      ;base_add_low
   add eax, [ebx+8]	      ;length_low
   add ebx, 20		      ;指向缓冲区中下一个ARDS结构
   cmp edx, eax		      ;冒泡排序，找出最大,edx寄存器始终是最大的内存容量
   jge .next_ards
   mov edx, eax		      ;edx为总内存大小
.next_ards:
   loop .find_max_mem_area
   jmp .mem_get_ok

;------  int 15h ax = E801h 获取内存大小,最大支持4G  ------
; 返回后, ax cx 值一样,以KB为单位,bx dx值一样,以64KB为单位
; 在ax和cx寄存器中为低16M,在bx和dx寄存器中为16MB到4G。
.e820_failed_so_try_e801:
   mov ax,0xe801
   int 0x15
   jc .e801_failed_so_try88   ;若当前e801方法失败,就尝试0x88方法

;1 先算出低15M的内存,ax和cx中是以KB为单位的内存数量,将其转换为以byte为单位
   mov cx,0x400	     ;cx和ax值一样,cx用做乘数
   mul cx 
   shl edx,16
   and eax,0x0000FFFF
   or edx,eax
   add edx, 0x100000 ;ax只是15MB,故要加1MB
   mov esi,edx	     ;先把低15MB的内存容量存入esi寄存器备份

;2 再将16MB以上的内存转换为byte为单位,寄存器bx和dx中是以64KB为单位的内存数量
   xor eax,eax
   mov ax,bx		
   mov ecx, 0x10000	;0x10000十进制为64KB
   mul ecx		;32位乘法,默认的被乘数是eax,积为64位,高32位存入edx,低32位存入eax.
   add esi,eax		;由于此方法只能测出4G以内的内存,故32位eax足够了,edx肯定为0,只加eax便可
   mov edx,esi		;edx为总内存大小
   jmp .mem_get_ok

;-----------------  int 15h ah = 0x88 获取内存大小,只能获取64M之内  ----------
.e801_failed_so_try88: 
   ;int 15后，ax存入的是以kb为单位的内存容量
   mov  ah, 0x88
   int  0x15
   jc .error_hlt
   and eax,0x0000FFFF
      
   ;16位乘法，被乘数是ax,积为32位.积的高16位在dx中，积的低16位在ax中
   mov cx, 0x400     ;0x400等于1024,将ax中的内存容量换为以byte为单位
   mul cx
   shl edx, 16	     ;把dx移到高16位
   or edx, eax	     ;把积的低16位组合到edx,为32位的积
   add edx,0x100000  ;0x88子功能只会返回1MB以上的内存,故实际内存大小要加上1MB

.mem_get_ok:
   mov [total_mem_bytes], edx	 ;将内存换为byte单位后存入total_mem_bytes处。


;-----------------   准备进入保护模式   -------------------
;1 打开A20
;2 加载gdt
;3 将cr0的pe位置1

   ;-----------------  打开A20  ----------------
   in al,0x92
   or al,0000_0010B
   out 0x92,al

   ;-----------------  加载GDT  ----------------
   lgdt [gdt_ptr]

   ;-----------------  cr0第0位置1  ----------------
   mov eax, cr0
   or eax, 0x00000001
   mov cr0, eax

   jmp dword SELECTOR_CODE:p_mode_start	     ; 刷新流水线，避免分支预测的影响,这种cpu优化策略，最怕jmp跳转，
					     ; 这将导致之前做的预测失效，从而起到了刷新的作用。
.error_hlt:		      ;出错则挂起
   hlt

[bits 32]
p_mode_start:
   mov ax, SELECTOR_DATA
   mov ds, ax
   mov es, ax
   mov ss, ax
   mov esp,LOADER_STACK_TOP
   mov ax, SELECTOR_VIDEO
   mov gs, ax

   ; 创建页目录及页表并初始化页内存位图
   call setup_page

   ;要将描述符表地址及偏移量写入内存gdt_ptr,一会用新地址重新加载
   sgdt [gdt_ptr]	      ; 存储到原来gdt所有的位置

   ;将gdt描述符中视频段描述符中的段基址+0xc0000000
   mov ebx, [gdt_ptr + 2]  
   or dword [ebx + 0x18 + 4], 0xc0000000      ;视频段是第3个段描述符,每个描述符是8字节,故0x18。
					      ;段描述符的高4字节的最高位是段基址的31~24位

   ;将gdt的基址加上0xc0000000使其成为内核所在的高地址
   add dword [gdt_ptr + 2], 0xc0000000

   add esp, 0xc0000000        ; 将栈指针同样映射到内核地址

   ; 把页目录地址赋给cr3
   mov eax, PAGE_DIR_TABLE_POS
   mov cr3, eax

   ; 打开cr0的pg位(第31位)
   mov eax, cr0
   or eax, 0x80000000
   mov cr0, eax

   ;在开启分页后,用gdt新的地址重新加载
   lgdt [gdt_ptr]             ; 重新加载

   mov byte [gs:160], 'V'     ;视频段段基址已经被更新,用字符v表示virtual addr

   jmp $

;-------------   创建页目录及页表   ---------------
setup_page:
;先把页目录占用的空间逐字节清0
   mov ecx, 4096
   mov esi, 0
.clear_page_dir:
   mov byte [PAGE_DIR_TABLE_POS + esi], 0
   inc esi
   loop .clear_page_dir

;开始创建页目录项(PDE)
.create_pde:				     ; 创建Page Directory Entry
   mov eax, PAGE_DIR_TABLE_POS
   add eax, 0x1000 			     ; 此时eax为第一个页表的位置及属性
   mov ebx, eax				     ; 此处为ebx赋值，是为.create_pte做准备，ebx为基址。

;   下面将页目录项0和0xc00都存为第一个页表的地址，
;   一个页表可表示4MB内存,这样0xc03fffff以下的地址和0x003fffff以下的地址都指向相同的页表，
;   这是为将地址映射为内核地址做准备
   or eax, PG_US_U | PG_RW_W | PG_P	     ; 页目录项的属性RW和P位为1,US为1,表示用户属性,所有特权级别都可以访问.
   mov [PAGE_DIR_TABLE_POS + 0x0], eax       ; 第1个目录项,在页目录表中的第1个目录项写入第一个页表的位置(0x101000)及属性(7)
   mov [PAGE_DIR_TABLE_POS + 0xc00], eax     ; 一个页表项占用4字节,0xc00表示第768个页表占用的目录项,0xc00以上的目录项用于内核空间,
					     ; 也就是页表的0xc0000000~0xffffffff共计1G属于内核,0x0~0xbfffffff共计3G属于用户进程.
   sub eax, 0x1000
   mov [PAGE_DIR_TABLE_POS + 4092], eax	     ; 使最后一个目录项指向页目录表自己的地址

;下面创建页表项(PTE)
   mov ecx, 256				     ; 1M低端内存 / 每页大小4k = 256
   mov esi, 0
   mov edx, PG_US_U | PG_RW_W | PG_P	     ; 属性为7,US=1,RW=1,P=1
.create_pte:				     ; 创建Page Table Entry
   mov [ebx+esi*4],edx			     ; 此时的ebx已经在上面通过eax赋值为0x101000,也就是第一个页表的地址 
   add edx,4096
   inc esi
   loop .create_pte

;创建内核其它页表的PDE
   mov eax, PAGE_DIR_TABLE_POS
   add eax, 0x2000 		     ; 此时eax为第二个页表的位置
   or eax, PG_US_U | PG_RW_W | PG_P  ; 页目录项的属性US,RW和P位都为1
   mov ebx, PAGE_DIR_TABLE_POS
   mov ecx, 254			     ; 范围为第769~1022的所有目录项数量
   mov esi, 769
.create_kernel_pde:
   mov [ebx+esi*4], eax
   inc esi
   add eax, 0x1000
   loop .create_kernel_pde
   ret


```

```
主引导程序 mbr.S
;------------------------------------------------------------
%include "boot.inc"
SECTION MBR vstart=0x7c00 ;SECTION 伪指令
    mov ax,cs
    mov ds,ax
    mov es,ax
    mov ss,ax
    mov fs,ax
    mov sp,0x7c00
    mov ax,0xb800
    mov gs,ax

    ; 清屏利用0x06号功能，上卷全部行，则可清屏
    ; -----------------------------------------------------------
    ;INT 0x10   功能号:0x06	   功能描述:上卷窗口
    ;------------------------------------------------------
    ;输入
    ;AH 功能号= 0x06
    ;AL = 上卷行数(如果为零,表示全部)
    ;BH = 上卷行属性
    ;(CL,CH) =窗口左上角的(X,Y)位置
    ;(DL,DH) = 窗口右下角(X,Y)位置
    ;无返回值
    mov     ax, 0x600
    mov     bx, 0x700
    mov     cx, 0        ; 左上角: (0, 0)
    mov     dx, 0x184f	; 右下角: (80,25),
                        ; 80个字符 25行
                        ; 从下标0开始,所以0x18=24,0x4f=79
    int     0x10         ; int 0x10


    ;;;;;;;;;   输出背景色为绿色，前景色为红色，并且跳动的字符串字符 “1 MBR”    ;;;;;;;;;
    mov byte [gs:0x00], '1'
    mov byte [gs:0x01], 0xa4

    mov byte [gs:0x02], ' '
    mov byte [gs:0x04], 0xa4

    mov byte [gs:0x04], 'M'
    mov byte [gs:0x05], 0xa4

    mov byte [gs:0x06], 'B'
    mov byte [gs:0x07], 0xa4

    mov byte [gs:0x08], 'R'
    mov byte [gs:0x09], 0xa4

    mov eax,LOADER_START_SECTOR ; 起始扇区 lba 地址 0x2 定义了loader在硬盘上的逻辑扇区地址 LBA
    mov bx,LOADER_BASE_ADDR ; 写入的地址 0x900 定义了loader在内存中的位置，MBR要把loader从硬盘读入后放到此处
    mov cx,1 ; 待读入的扇区数
    call rd_disk_m_16 ; 以下读取程序的起始部分(一个扇区)
   
    jmp LOADER_BASE_ADDR ;mbr程序结束，程序跳到laoder
   
    ;-------------------------------------------------------------------------------
    ;功能:读取硬盘 n 个扇区
    rd_disk_m_16:
    ;-------------------------------------------------------------------------------
    ; eax=LBA 扇区号
    ; bx=将数据写入的内存地址
    ; cx=读入的扇区数
    mov esi,eax ;备份 eax
    mov di,cx ;备份 cx
    ;读写硬盘:
    ;第 1 步:设置要读取的扇区数
    mov dx,0x1f2
    mov al,cl
    out dx,al ;写入的扇区数量 serctor count 
   
    mov eax,esi ;恢复 ax
   
    ;第 2 步:将 LBA 地址存入 0x1f3 ～ 0x1f6 
    
    ;LBA 地址 7～ 0 位写入端口 0x1f3
    mov dx,0x1f3;LBAlow
    out dx,al
   
    ;LBA 地址 15～ 8 位写入端口 0x1f4
    mov cl,8
    shr eax,cl
    mov dx,0x1f4
    out dx,al
   
    ;LBA 地址 23～ 16 位写入端口 0x1f5
    shr eax,cl
    mov dx,0x1f5
    out dx,al
   
    shr eax,cl
    and al,0x0f ;lba 第 24～ 27 位
    or al,0xe0 ; 设置 7～ 4 位为 1110,表示 lba 模式
    mov dx,0x1f6
    out dx,al
   
    ;第 3 步:向 0x1f7 端口写入读命令,0x20;
    mov dx,0x1f7
    mov al,0x20
    out dx,al

    ;第 4 步:检测硬盘状态
    .not_ready:
        ;同一端口,写时表示写入命令字,读时表示读入硬盘状态
        nop
        in al,dx
        and al,0x88 ;第 4 位为 1 表示硬盘控制器已准备好数据传输
        ;第 7 位为 1 表示硬盘忙
        cmp al,0x08
        jnz .not_ready ;若未准备好,继续等

        ;第 5 步:从 0x1f0 端口读数据
        mov ax, di
        mov dx, 256
        mul dx
        mov cx, ax
        ; di 为要读取的扇区数,一个扇区有 512 字节,每次读入一个字
        ; 共需 di*512/2 次,所以 di*256
        mov dx, 0x1f0
    .go_on_read:
        in ax,dx
        mov [bx],ax ;将数据写入的内存地址
        add bx,2
        loop .go_on_read
        ret

    times 510-($-$$) db 0
    db 0x55,0xaa
```
```
;boot.inc
;-------------	 loader和kernel   ----------

LOADER_BASE_ADDR equ 0x900 
LOADER_START_SECTOR equ 0x2
KERNEL_BIN_BASE_ADDR equ 0x70000
KERNEL_IMAGE_BASE_ADDR  equ   0x1500
KERNEL_START_SECTOR equ 0x9

PAGE_DIR_TABLE_POS equ 0x100000

;--------------   gdt描述符属性  -------------
DESC_G_4K   equ	  1_00000000000000000000000b   
DESC_D_32   equ	   1_0000000000000000000000b
DESC_L	    equ	    0_000000000000000000000b	;  64位代码标记，此处标记为0便可。
DESC_AVL    equ	     0_00000000000000000000b	;  cpu不用此位，暂置为0  
DESC_LIMIT_CODE2  equ 1111_0000000000000000b
DESC_LIMIT_DATA2  equ DESC_LIMIT_CODE2
DESC_LIMIT_VIDEO2  equ 0000_000000000000000b
DESC_P	    equ		  1_000000000000000b
DESC_DPL_0  equ		   00_0000000000000b
DESC_DPL_1  equ		   01_0000000000000b
DESC_DPL_2  equ		   10_0000000000000b
DESC_DPL_3  equ		   11_0000000000000b
DESC_S_CODE equ		     1_000000000000b
DESC_S_DATA equ	  DESC_S_CODE
DESC_S_sys  equ		     0_000000000000b
DESC_TYPE_CODE  equ	      1000_00000000b	;x=1,c=0,r=0,a=0 代码段是可执行的,非依从的,不可读的,已访问位a清0.  
DESC_TYPE_DATA  equ	      0010_00000000b	;x=0,e=0,w=1,a=0 数据段是不可执行的,向上扩展的,可写的,已访问位a清0.

DESC_CODE_HIGH4 equ (0x00 << 24) + DESC_G_4K + DESC_D_32 + DESC_L + DESC_AVL + DESC_LIMIT_CODE2 + DESC_P + DESC_DPL_0 + DESC_S_CODE + DESC_TYPE_CODE + 0x00
DESC_DATA_HIGH4 equ (0x00 << 24) + DESC_G_4K + DESC_D_32 + DESC_L + DESC_AVL + DESC_LIMIT_DATA2 + DESC_P + DESC_DPL_0 + DESC_S_DATA + DESC_TYPE_DATA + 0x00
DESC_VIDEO_HIGH4 equ (0x00 << 24) + DESC_G_4K + DESC_D_32 + DESC_L + DESC_AVL + DESC_LIMIT_VIDEO2 + DESC_P + DESC_DPL_0 + DESC_S_DATA + DESC_TYPE_DATA + 0x0b

;--------------   选择子属性  ---------------
RPL0  equ   00b
RPL1  equ   01b
RPL2  equ   10b
RPL3  equ   11b
TI_GDT	 equ   000b
TI_LDT	 equ   100b


;----------------   页表相关属性    --------------
PG_P  equ   1b
PG_RW_R	 equ  00b 
PG_RW_W	 equ  10b 
PG_US_S	 equ  000b 
PG_US_U	 equ  100b 

```
loader写入磁盘
```
nasm -I include/ -o loader.bin loader.S  && dd if=./loader.bin of=/Users/isam2016/opt/bochs/hd60M.img bs=512  seek=2  conv=notrunc
```
mbr写入磁盘
```
nasm -I include/ -o mbr.bin mbr.S  && dd if=./mbr.bin of=/Users/isam2016/opt/bochs/hd60M.img bs=512 count=1  conv=notrunc
```

# 用虚拟地址访问页表
* 获取页目录表物理地址：让虚拟地址的高 20 位为 0xfffff，低 12 位为 0x000，即 0xfffff000，这也是页目录表中第 0 个页目录项自身的物理地址。
* 访问页目录中的页目录项，即获取页表物理地址：要使虚拟地址为 0xfffffxxx，其中 xxx 是页目录项的索引乘以 4 的积。

* 访问页表中的页表项：要使虚拟地址高 10 位为 0x3ff，目的是获取页目录表物理地址。中间 10 位为页表的索引，因为是 10 位的索引值，所以这里不用乘以 4。低 12 位为页表内的偏移地址，用来定位页表项，它必须是已经乘以 4 后的值。
* 
公式为 `0x3ff<<22+中间 10 位<<12+低 12 位`。