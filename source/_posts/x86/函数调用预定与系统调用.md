---
title:  真象还原-函数调用预定与系统调用
date: 2021-10-19 11:31:56
tags:
categories:
- 操作系统
---

从这个章节 c语言的代码变多。 书中的代码，大家要敲一遍。当然可以略过boot模块。把它当做一个工具
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20220105135519.jpg)
# 函数调用约定

由于我们将c语言和汇编语言结合，所以会存在汇编代码和c代码相互理解调用的问题。

函数`调用约定`调用约定从字面上理解，他是调用函数的一套约定。 参数的传递方式，参数是存放在栈中

其优点如下:
1. 每个进程都有自己的栈
2. 参数的内存地址不用花精力去维护，已经有栈机制自动帮我们维护参数储存的问题解决了
   
那么接下来讨论另外两个问题
1. 参数存储在栈中，那么谁来负责回收参数所占的栈空间，是调用者？还是被调用者
2. 当参数很多的时候后，主调函数将参数以什么样的顺序传递

上面两个问题就涉及到具体的调用规定了

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/v2-31fd32d77c9d7c8cd9f0c3ecbe05819a_.jpg)

在这些调用约定中，我们最常用是以下几种约定
1. cdecl
2. stdcall
3. thiscall
   
* cdecl 是c默认的调用约定。
* stdcall 他是微软Win32 API的一准标准，我们常用的回调函数就是通过这种调用方式
* thiscall 是c++中非静态类成员函数的默认调用约定

# c库函数与系统调用  系统调用
 汇编语言和c语言编程分为两大类
 
1. 单独的汇编文件与单独的c语言文件编译成目标文件后，一起链接成可执行文件。（混合编程）
2. 在c语言中嵌入汇编代码，直接编译成可执行程序（内联汇编）

思考一下我们还能使用函数库吗？

系统调用是linux内核提供的一套子程序，主要是为了实现在用户态不能实现的功能，比如说最常见的读写硬盘文件，这些读写的方法肯定不能由用户程序来编写，而且用户程序也没有权限去直接操控硬件，这就需要操作系统的支持，需要操作系统提供读写硬盘的接口，即系统调用。

调用系统调用有两种方式:
1. 通过操作系统提供的库函数进行系统调用
2. 直接通过0x80中断与系统通信

我们想要自制kernel的话第一条路肯定走不通，因为没有库函数供我们调用，库函数都是我们自己写的。

通过中断的方式进行系统调用需要了解一下系统调用输入参数的传递方式

当输入的参数小于等于5个时，linux用寄存器传递参数。当输入的参数大于5个时，把参数按照顺序放入连续的内存中，并把这块内存的首地址放入ebx中

通过寄存器传递参数时,eax存放子功能号,ebx存放第一个参数,ecx存放第二个参数,edx存放第三个参数,esi存放第四个参数,edi存放地五个参数

参数在栈的的排列如图：
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20220105171211.jpg)

注意栈的扩展方向，和之前的汇编课程的方向不一样。

syscall_write.S演示了系统调用的两种方式。 

* 方式1 是用汇编模拟c语言中系统调用函数write
* 方式2：跨过函数库 直接进行系统调用
```asm
; syscall_write.S
section .data
str_c_lib: db "c  library say:s hello world!", 0xa; 0xa为LF ASCII 码
str_c_lib_len equ $-str_c_lib

str_syscall: db "syscall says: hello world", 0xa
str_syscall_len equ $-str_syscall

section .text
global _start;函数的入口 nasm通过global来指定入口
_start: 
;;;;;;;;;;;;;;;;;;;;; 方式1： 模拟c语言中系统调用函数write ;;;;;;;;;;;;;;;;;;;;;
push str_c_lib_len ; 按照c调用约定压入参数   字符串 的长度 参数c 
push str_c_lib  ; 字符串起始地址 参数 b
push 1 ;参数 a 固定参数

call simu_write   ;调用下面定义的simu_write
add esp,12        ; 调用者回收栈空间

;;;;;;;;;;;;;;;;;;;;; 方式2：跨过函数库 直接进行系统调用 ;;;;;;;;;;;;;;;;;;;;;;
mov eax,4       ;  第4号子功能 是write 系统调用（不是c函数write）
mov ebx,1       
mov ecx, str_syscall
mov edx, str_c_lib_len
int 0x80  ; 发起中断 通知linux 完成请求的功能

;;;;;;;;;;;;;;;;;;;;; 退出程序 ;;;;;;;;;;;;;;;;;;;;;
mov eax,1   ;发起1号功能是exit
int 0x80    ;发起中断，通知linux 完成请求的功能

;;;;;;;;;;;;;;;;;;;;; 下面自定义的 simu_write用来模拟c 库中系统调用函数 write
;;;;;;;;;;;;;;;;;;;;; 这里模拟他的实现原理
simu_write:
    push ebp; 备份保护ebp 在之前的汇编课程学过
    mov ebp, esp; 将栈顶地址赋值到ebp； 用ebp左右基址来访问栈中参数
    mov eax, 4  ;第四号子功能是write 系统调用 
    mov ebx, [ebp + 8]  ;第一个参数 a
    mov ecx, [ebp + 12] ;第二个参数 b
    mov edx, [ebp + 16] ;第三个参数 c
    int 0x80 ; 发起中断 通知linux f完成请求的功能
    pop ebp ; 恢复ebp
    ret
```
`nasm -f elf -o syscall_write.o syscall_write.S `（输出目标文件）

`ld -o syscall_write.bin syscall_write.o`(连接成可执行的文件)

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211117150436.jpg)

# c和汇编之间的调用-混合编程
如何实现c和汇编之间的调用。实现的原理就是把他们变成统一的机器码。
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20220105134146.jpg)
注意： 标黄的地方

```c
// C_with_S_c.c
extern void asm_print(char *, int); // 声明外部函数 通知编译器这个函数并不在当前文件中定义，在编辑连接节点将次函数重新定位 编排地址
/**
 * char *str 接收到字符窜内存地址
 * 计算字符串的长度， 并当参数调用汇编
 */
void c_print(char *str)
{
    int len = 0;
    while (str[len++])
        ;
    asm_print(str, len); // ### 2. C语言调用汇编
}
```

```asm
; C_with_S_s.S
section .data  
str:  db "asm_prin says hello world!",  0xa,  0
;0xa 是换行符, 0 是手工加上的字符串结束符\ 0 的 ASCII码  
str_len equ $-str

section .text
extern c_print ; 引用外部文件符号
global  _start;函数的入口 nasm通过global来指定入口
_start:
;;;;;;;;;;;; 调用 c 代码中的函数 c_print ;;;;;;;;;;;  
    push str               ; 传入参数 字符串内存地址
    call  c_print           ; 调用 c 函数 ### 1. 汇编调用c语言
    add esp, 4              ; 回收栈空间 

;;;;;;;;;;;;;;;;;;;  退出程序  ;;;;;;;;;;;;;;;;;;;;
    mov eax, 1             ;第 1 号子功能是 exit 系统调用 
    int  0x80              ;发起中断，通知 Li nux 完成请求的功能

global  asm_print        ;相当于 asm_print (str, size） 导出符号供外部引用
asm_print: 
    push ebp             ;备份 ebp 
    mov ebp, esp 
    mov eax, 4           ;第 4 号子功能是 write系统调用 
    mov ebx, 1          ; 此项固定为文件描述符 1，标准输出（stdout ）指向屏幕 
    mov ecx, [ebp+8]     ; 第 1 个参数  从c_print 获得字符地址
    mov edx, [ebp+12]   ; 第 2 个参数 从c_print 获得字符长度
    int  0x80            ; 发起中断，通知 Li nux 完成请求的功能 
    pop ebp              ; 恢复 ebp
    ret
```
两段代码互相调用
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211117153731.jpg)

* 在汇编代码中导出符号供外部引用是用的关键字global，引用外部文件的符号是用的关键字extern。 
* 在 C 代码中只要将符号定义为全局便可以被外部引用（一般情况下无需用额外关键字修饰，具体请参考C 语言手册），引用外部符号时用 extern 声明即可。

`nasm -f elf -o C_with_S_s.o C_with_S_s.S`（汇编输出可连接）

`gcc -c -o C_with_S_c.o C_with_S_c.c`（c输出可连接）

`ld -o C_with_S.bin C_with_S_s.o C_with_S_c.o`

最终输出
```
isam2016@ubuntu:~/os/c6$ ./C_with_S.bin 
asm_prin says hello world!
```

书中提供了print.S 打印实现方式. 
在c6/a 中执行

1. 将内核写入磁盘 
```
编译 print.S
nasm -f elf -o lib/kernel/print.o lib/kernel/print.S

编译 main.c
gcc -I lib/kernel/ -c -o kernel/main.o kernel/main.c

链接 main.o 和print .o 
ld -Ttext 0xc0001500 -e main -o kernel.bin  kernel/main.o lib/kernel/print.o

写入虚拟硬盘
dd if=kernel.bin of=/home/isam2016/opt/bochs/hd60M.img  bs=512 count=200 seek=9 conv=notrunc

```

2. 将loader.S 写入第二扇区
```
 nasm -I boot/include/ -o loader.bin boot/loader.S  && dd if=./loader.bin of=/home/isam2016/opt/bochs/hd60M.img bs=512  count=4 seek=2  conv=notrunc
```

3. mbr编译,写入到虚拟硬盘
```
nasm -I boot/include/ -o mbr.bin boot/mbr.S  && dd if=./mbr.bin of=/home/isam2016/opt/bochs/hd60M.img bs=512 count=1  conv=notrunc
```
运行bochs1,`输入c`

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20220105183713.jpg)


# c和汇编之间的调用-内联汇编

另一种汇编和 C 语言混合编程的方式便是在 C 语言里面写汇编语言。

gcc 默认支持的是 AT&T 语法风格的汇编语言

AT&T 汇编语法简介
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211117173834.jpg)

AT&T 中数字被优先认为是内存地址。

## AT&T 内存寻址

`segreg(段基址): base_address(offset_address, index, size)`
base_address 是基地址，可以为整数、变量名，可正可负。

offset_address 是偏移地址，index 是索引值，这两个必须是 8 个通用寄存器之一。

size 是个长度，只能是 1、2、4、8。

## 基本内联汇编
基本内联汇编是最简单的内联形式，其格式为：
```
asm [volatile] ("assembly code")
```
asm 是必须的，表示是内联汇编；
volatile 表示让编译器不要修改我的代码 ；

assembly code 的原则：

* 指令必须用双引号引起来，无论双引号中是一条指令或多条指令。
* 一对双引号不能跨行，如果跨行需要在结尾用反斜杠 ‘\’ 转移。
* 指令之间用分号’;’、换行符’\n’或换行符加制表符’\n’’\t’分隔。

在基本内联汇编中，若要引用 C 变量，只能将它定义为全局变量。如果定义为局部变量，链接时会找不到这两个符号。
<!-- 
## 扩展内联汇编
`asm [volatile] ("assembly code": output : input : clobber/modify)`

* assembly code: 用户写入得汇编指令。
* output: 用来指定汇编代码得数据如何输出给 C 代码使用。
* input: 用来指定 C 中数据如何输入给汇编使用。
* clobber/modify: 汇编代码执行后会破坏一些内存或寄存器资源，通过此项通知编译器，可能造成寄存器或内存数据得破坏，这样 gcc 就知道哪些寄存器或内存需要提前保护起来。

在扩展汇编指令中，%被用作占位符，寄存器前面要用%%

这些要求在扩展内联汇编中称为“约束”，作用就是把 C 代码中的操作数映射为汇编中使用的操作数，约束分 4 大类：

寄存器约束：要求 gcc 使用哪个寄存器

* a: 表示寄存器 eax/ax/al 
* b: 表示寄存器 ebx/bx/bl 
* c: 表示寄存器 ecx/cx/cl 
* d: 表示寄存器 edx/dx/dl
* D：表示寄存器edi/di
* S：表示寄存器esi/si
* q：表示任意这4个通用寄存器之一：eax/ebx/ecx/edx
* r：表示任意这6个通用寄存器之一：eax/ebx/ecx/edx/esi/edi
* g：表示可以存放到任意地点（寄存器和内存）。相当千除了同q一样外，还可以让gee安排在内存中
* A：把eax和edx组合成64位整数
* f：表示浮点寄存器
* t：表示第1个浮点寄存器
* u：表示第1个浮点寄存器
使用举例：
```c
#include<stdio.h>
int main(){
	int in_a = 1, in_b = 2, out_sum; 
	asm("addl %%ebx, %%eax":"=a"(out_sum) :"a"(in_a),"b"(in_b)); 
    //input的“a”(in_a)：表示eax = in_a
    //output的“=a”(out_sum)：表示eax = out_sum
    printf("sum is %d\n",out_sumJ; 
}
```
内存约束：直接将 input 和 output 中的 C 变量的内存地址作为内联汇编代码的操作数，直接进行内存读写

* m：表示操作数可以使用任意一种内存形式。
* o：操作数为内存变量，但访问它是通过偏移量的形式访问，即包含 offset—address 的格式。
使用举例：
```c
#include<stdio.h>
int main(){
	int in_a = 1, in_b = 2; 
	printf("in_b is %d\n", in_b); 
	asm("movb %b0, %1;"::"a"(in_a),"m"(in_b));
    //%1：序号占位符，就是 in_b 的地址
    //%b0：b表示低8位，0表示 in_a 的地址
    printf("in_b now is %d\n", in_b); 
}
```
立即数约束：要求 gcc 直接传递立即数给代码，不通过寄存器或内存，只能作为右值，只能放在 input 中

* i:表示操作数为整数立即数
* F:表示操作数为浮点数立即数
* I:表示操作数为0~31之间的立即数
* J:表示操作数为0~63之间的立即数
* N:表示操作数为0~255之间的立即数
* 0:表示操作数为0~32之间的立即数
* X:表示操作数为任何类型立即数

通用约束：0~9:此约束只用在input部分， 但表示可与output和input中第n个操作数用相同的寄存器或内存。

 # 机器模式简介
操作码就是指定操作数为寄存器中的哪个部分，初步了解h、b、W、K这几个操作码就够了。

寄存器按是否可单独使用，可分成几个部分，拿eax举例：

低部分的一字节：al

高部分的一字节：ah

两字节部分：ax

四字节部分：eax

* h：输出寄存器高位部分中的那一字节对应的寄存器名称，如ah、bh、ch、dh。
* b：输出寄存器中低部分1字节对应的名称，如al、bl、cl、d1。
* w：输出寄存器中大小为2个字节对应的部分，如ax、bx、ex、dx。
* k：输出寄存器的四字节部分，如eax、ebx、ecx、edx。 -->
