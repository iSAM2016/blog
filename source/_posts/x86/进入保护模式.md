---
title: 真象还原-进入保护模式
date: 2021-10-14 17:25:54
tags:
categories:
- 操作系统
---
> 真象还原-4.3
# 全局描述符

我们知道，为了让程序在内存中能自由浮动而又不影响它的正常执行，处理器将内存划分成逻辑上的段，并在指令中使用段内偏移地址。在保护模式下，对内存的访问仍然使用段地址和偏移地址，但是，在每个段能够访问之前，必须先进行登记。


和一个段有关的信息需要 8 个字节来描述，所以称为*段描述符（Segment Descriptor）*，每个段都需要一个描述符。为了存放这些描述符，需要在内存中开辟出一段空间。在这段空间里，所有的
描述符都是挨在一起，集中存放的，这就构成一个描述符表。

最主要的描述符表是*全局描述符表（Global Descriptor Table， GDT）(64KB)*，所谓全局，意味着该表是为整个软硬件系统服务的。在进入保护模式前，必须要定义全局描述符表。 


理论上，全局描述符表可以位于内存中的任何地方。但是，如图 11-2 所示，由于在进入保护模式之后，处理器立即要按新的内存访问模式工作，所以，必须在进入保护模式之前定义 GDT。但是，由于在实模式下只能访问 1MB 的内存，故 GDT 通常都定义在 1MB 以下的内存范围中。当然，允许在进入保护模式之后换个位置重新定义 GDT。

![](20211015100004.jpg)

为什么将该表称为“全局”描述符表？全局体现在多个程序都可以在里面定义自己的段描述符，是公用的。全局描述符表位于内存中， 需要用专门的寄存器指向它后， CPU 才知道它在哪里。 这个专门的寄存器便是 GDTR，如上图的指向关系

# 段描述符

每个段是需要进行登记管理的。需要一个记录本进行记录。段描述符是 8 字节大小，共计8*8=64连续的的位
![](20211015095342.jpg)

G位表示段界限粒度，为0时表示粒度为1字节，为1时表示粒度为4KB.实际段界限=(描述符中的段界限+1)\*粒度-1，假设段界限为0xfffff，G位为1时，实际段界限=0x100000*4KB-1 = 0xFFFFFFFF。

如果偏移地址超过了段界限，CPU会抛出异常S为表示是否是系统段。当S为1时，表示非系统段，为0表示系统段。

type字段的属性和S用关系，用一张图来表示

P位表示段是否位于内存中。

L位用来设置是否为64位代码段

D/B位表示有效地址及操作数的大小。对于代码段来说，此位是D位。为0时表示有效地址和操作数是16位。对于栈段来说，此为是B位，为0时表示使用的16位的栈指针寄存器

<!-- TODO: 图 -->

段描述和内存之间的关系。

![](20211015111206.jpg)

保护模式下地址总线宽度是 32 位，段基址需要用 32 位地址来表示。
<!-- TODO:偏移值 向上向下-->

# 选择子

回顾一下：
*在 32 位模式下，传统的段寄存器，如 CS、 SS、 DS、 ES，保存的不再是 16位段基地址，而是段的选择子，即，用于选择所要访问的段，因此，严格地说，它的新名字叫做段选择器。除了段选择器之外，每个段寄存器还包括一个 64 位的不可见部分，称为描述符高速缓存器，里面有段的基地址和各种访问属性。这部分内容程序不可访问，由处理器自动使用。*

![](20211015110222.jpg)

也很好理解，每个段都有一个专有描述符，当然要把段的信息全部放进去。

那么段选择器就空了，放的就是selector,选择子。

选择子是个索引值，此索引值在段描述符表中索引相应的段描述符，这样，便在段描述符
中得到了内存段的起始地址和段界限值等相关信息

选择子的结构
![](20211015111323.jpg)

由于段寄存器是 16 位，所以选择子也是 16 位

* RPL，即请求特权级，可以表示 0、 1、 2、 3 四种特权级。
* TI 位，即 Table Indicator，用来指示选择子是在 GDT 中，还是 LDT 中索引描述符。 TI为 0 表示在 GDT 中索引描述符， TI 为 1 表示在 LDT 中索引描述符。
* 选择子的高 13 位，即第 3～15 位是描述符的索引值，用此值在 GDT 中索引描述符。前面说过 GDT 相当于一个描述符数组，所以此选择子中的索引值就是 GDT 中的下标。

由于选择子的索引值部分是 13 位，即 2 的 13 次方是 8192，故最多可以索引 8192 个段，这和 GDT中最多定义 8192 个描述符是吻合的。

选择子的作用主要是确定段描述符，确定描述符的目的，一是为了特权级、界限等安全考虑，最主要
的还是要确定段的基地址.

例如选择子是 0x8，将其加载到 ds 寄存器后，访问 ds： 0x9 这样的内存，其过程是： 0x8 的低 2 位是RPL，其值为 00。第 2 是 TI，其值 0，表示是在 GDT 中索引段描述符。用 0x8 的高 13 位 0x1 在 GDT 中索引，也就是 GDT 中的第 1 个段描述符（GDT 中第 0 个段描述符不可用）。假设第 1 个段描述符中的 3个段基址部分， 其值为 0x1234。CPU 将 0x1234 作为段基址， 与段内偏移地址 0x9 相加， 0x1234+0x9=0x123d。

大概的关系是 
![](20211015132137.jpg)

在保护模式下，由于已经是32位地址线和32位寄存器，不在需要经段基值乘以16在于段内偏移地址相加

值得注意的是上面括号中提到了 GDT 中的第 0 个段描述符是不可用的，


进入保护模式需要经历三部
# lgdt(第一步)
进入保护模式需要有 GDT，但进入保护模式后，还可以再重新换个 GDT 加载。在保护模式下重新换个 GDT 的原因是实模式下只能访问低端 1MB 空间，所以 GDT 只能位于 1MB 之内。

根据操作系统的实际情况，有可能需要把 GDT 放在其他的内存位置，所以在进入保护模式后，访问的内存空间突破了 1MB，可以将 GDT 放在合适的位置后再重新加载进来。

如图 11-1 所示，为了跟踪全局描述符表，处理器内部有一个 48 位的寄存器，称为*全局描述符表寄存器（GDTR）*。

![](20211015094856.jpg)

这 48 位内存数据划分为两部分
* 前 16 位是 GDT 以字节为单位的界限值，所以这 16 位相当于GDT 的字节大小减 1。
* 后 32 位是 GDT 的起始地址。
  
由于 GDT 的大小是 16 位二进制，其表示的范围是 2的16次方等于65536字节(64KB)。每个描述符大小是8字节故， GDT中最多可容纳的描述符数量是65536/8=8192个，即 GDT 中可容纳 8192 个段或门。

也就是说GTD 限制了段的个数

lgdt 的指令格式是： `lgdt48 位内存数据`; 前16+后32位 

<!--  TODO: 加载GDRT -->
# 打开A20地址线（第二步）

实模式下的 wrap-around（地址回绕）
实模式下内存访问是采取“段基址：段内偏移地址”的形式，段基址要乘以 16 后再加上段内偏移地址。实模式下寄存器都是 16 位的，如果段基址和段内偏移地址都为 16 位的最大值，即 0xFFFF： 0xFFFF，最大地址是 0xFFFF0+0xFFFF， 即 0x10FFEF。由于实模式下的地址线是 20 位， 最大寻址空间是 1MB， 即 0x00000～0xFFFFF。超出 1MB 内存的部分在逻辑上也是正常的，但物理内存中却没有与之对应的部分。

但 80286 有 24 条地址线，即 A0～A23，也就是说 A20 地址线是开启的。如果访问 0x100000～0x10FFEF 之间的内存，系统将直接
访问这块物理内存，并不会像 8086/8088 那样回绕到 0。
<!-- TODO: 图片 -->

为了解决此问题， IBM 在键盘控制器上的一些输出线来控制第 21 根地址线（A20）的有效性，故被称为 A20Gate。

* 如果 A20Gate 被打开，当访问到 0x100000～0x10FFEF 之间的地址时， CPU 将真正访问这块物理内存。
* 如果 A20Gate 被禁止， 当访问 0x100000～0x10FFEF 之间的地址时， CPU 将采用 8086/8088 的地址回绕。

开启a20
```
in al， 0x92
or al， 0000_0010B
out 0x92， al
```

# 保护模式的开关，CR0 寄存器的 PE 位（第三步）
<!-- TODO: 图片 -->
我们要用到 CR0寄存器的第 0 位，即 PE 位， Protection Enable，此位用于启用保护模式，当打开此位后， CPU 才真正进入保护模式。

PE 为 0 表示在实模式下运行， PE 为 1 表示在保护模式下运行。
```
mov eax, cr0        ;行代码是将 cr0 写入 eax。
or eax, 0x00000001 ;通过或运算 or 指令将 eax 的第 0 位置 1。
mov cr0, eax       ;将 eax 写回 cr0，这样 cr0 的 PE 位便为 1 了。
```

# 进入内核
```
;loader.S
%include "boot.inc"
section loader vstart=LOADER_BASE_ADDR
LOADER_STACK_TOP equ LOADER_BASE_ADDR
jmp loader_start


; 构建全局描述符表，并填充段描述符，段描述符的大小为8字节，在这里将其分为低4字节与高4字节来定义
; dd=define double-word，为4字节
;--------------------------------------------------------

; gdt的起始地址为GDT_BASE的地址，且gdt的第0个描述符不可用，所以将其直接定义为0
GDT_BASE: dd 0x00000000
            dd 0x00000000

; 代码段
CODE_DESC: dd 0x0000ffff
            dd DESC_CODE_HIGH4

; 数据段和栈段
DATA_STACK_DESC: dd 0x0000ffff
                 dd DESC_DATA_HIGH4

; 显存段描述符
VIDEO_DESC: dd 0x80000007
            dd DESC_VIDEO_HIGH4

GDT_SIZE equ   $-GDT_BASE
GDT_LIMIT equ GDT_SIZE - 1
times 60 dq 0

SELECTOR_CODE equ (0x0001<<3) + TI_GDT + RPL0     ; 相当于(CODE_DESC - GDT_BASE)/8 + TI_GDT + RPL0
SELECTOR_DATA equ (0x0002<<3) + TI_GDT + RPL0     ; 同上
SELECTOR_VIDEO equ (0x0003<<3) + TI_GDT + RPL0    ; 同上 

gdt_ptr dw GDT_LIMIT    ;gdt的前2字节是段界限，后4字节是段基址
        dd GDT_BASE
loadermsg db 'loader in real.'

loader_start:
    mov sp, LOADER_BASE_ADDR
    mov bp, loadermsg
    mov cx, 15
    mov ax, 0x1301
    mov bx, 0x001f
    mov dx, 0x1800
    int 0x10

;---------------------------
;准备进入保护模式
;1. 打开A20
;2. 加载gdt
;3. 将cr0的PE位置1
;---------------------------


;-------打开A20--------
    in al, 0x92
    or al, 0000_0010b
    out 0x92, al

;-------加载gdt-------
    lgdt [gdt_ptr]

;------cr0第0位置1-----
    mov eax, cr0
    or eax, 0x00000001
    mov cr0, eax

    jmp SELECTOR_CODE:p_mode_start


[bits 32]
p_mode_start:
    mov ax, SELECTOR_DATA
    mov ds, ax
    mov es, ax
    mov ss, ax
    mov esp, LOADER_STACK_TOP
    mov ax, SELECTOR_VIDEO
    mov gs, ax

    mov byte [gs:160], 'P'

    jmp $
```

```
;boot.inc
LOADER_BASE_ADDR equ 0x900
LOADER_START_SECTOR equ 0x2

;----------------------------
;gdt描述符属性

DESC_G_4K   equ   1_00000000000000000000000b
DESC_D_32   equ    1_0000000000000000000000b
DESC_L      equ     0_000000000000000000000b    ;  64位代码标记，此处标记为0便可。
DESC_AVL    equ      0_00000000000000000000b    ;  cpu不用此位，暂置为0
DESC_LIMIT_CODE2  equ 1111_0000000000000000b
DESC_LIMIT_DATA2  equ DESC_LIMIT_CODE2
DESC_LIMIT_VIDEO2  equ 0000_000000000000000b
DESC_P      equ           1_000000000000000b
DESC_DPL_0  equ            00_0000000000000b
DESC_DPL_1  equ            01_0000000000000b
DESC_DPL_2  equ            10_0000000000000b
DESC_DPL_3  equ            11_0000000000000b
DESC_S_CODE equ              1_000000000000b
DESC_S_DATA equ   DESC_S_CODE
DESC_S_sys  equ              0_000000000000b
DESC_TYPE_CODE  equ           1000_00000000b    ;x=1,c=0,r=0,a=0 代码段是可执行的,非依从>的,不可读的,已访问位a清0.
DESC_TYPE_DATA  equ           0010_00000000b    ;x=0,e=0,w=1,a=0 数据段是不可执行的,向上>扩展的,可写的,已访问位a清0.

;DESC_CODE_HIGH4 = 1100 1111 1001 1000 0000 0000 代码段的高4字节
DESC_CODE_HIGH4 equ (0x00 << 24) + DESC_G_4K + DESC_D_32 + DESC_L + DESC_AVL + DESC_LIMIT_CODE2 + DESC_P + DESC_DPL_0 + DESC_S_CODE + DESC_TYPE_CODE + 0x00

;DESC_DATA_HIGH4 = 1100 1111 1001 0010 0000 0000
DESC_DATA_HIGH4 equ (0x00 << 24) + DESC_G_4K + DESC_D_32 + DESC_L + DESC_AVL + DESC_LIMIT_DATA2 + DESC_P + DESC_DPL_0 + DESC_S_DATA + DESC_TYPE_DATA + 0x00

;DESC_VIDEO_HIGH4= 1100 0000 1001 0010 0000 1011
DESC_VIDEO_HIGH4 equ (0x00 << 24) + DESC_G_4K + DESC_D_32 + DESC_L + DESC_AVL + DESC_LIMIT_VIDEO2 + DESC_P + DESC_DPL_0 + DESC_S_DATA + DESC_TYPE_DATA + 0x0b

;--------------   选择子属性  ---------------
RPL0  equ   00b
RPL1  equ   01b
RPL2  equ   10b
RPL3  equ   11b
TI_GDT   equ   000b
TI_LDT   equ   100b

```

```
;主引导程序 mbr.S
;------------------------------------------------------------
%include "boot.inc"
SECTION MBR vstart=0x7c00 ;SECTION 伪指令
    mov ax,cs
    mov ds,ax
    mov es,ax
    mov ss,ax
    mov fs,ax
    mov sp,0x7c00
    mov ax,0xb800
    mov gs,ax

    ; 清屏利用0x06号功能，上卷全部行，则可清屏
    ; -----------------------------------------------------------
    ;INT 0x10   功能号:0x06	   功能描述:上卷窗口
    ;------------------------------------------------------
    ;输入
    ;AH 功能号= 0x06
    ;AL = 上卷行数(如果为零,表示全部)
    ;BH = 上卷行属性
    ;(CL,CH) =窗口左上角的(X,Y)位置
    ;(DL,DH) = 窗口右下角(X,Y)位置
    ;无返回值
    mov     ax, 0x600
    mov     bx, 0x700
    mov     cx, 0        ; 左上角: (0, 0)
    mov     dx, 0x184f	; 右下角: (80,25),
                        ; 80个字符 25行
                        ; 从下标0开始,所以0x18=24,0x4f=79
    int     0x10         ; int 0x10


    ;;;;;;;;;   输出背景色为绿色，前景色为红色，并且跳动的字符串字符 “1 MBR”    ;;;;;;;;;
    mov byte [gs:0x00], '1'
    mov byte [gs:0x01], 0xa4

    mov byte [gs:0x02], ' '
    mov byte [gs:0x04], 0xa4

    mov byte [gs:0x04], 'M'
    mov byte [gs:0x05], 0xa4

    mov byte [gs:0x06], 'B'
    mov byte [gs:0x07], 0xa4

    mov byte [gs:0x08], 'R'
    mov byte [gs:0x09], 0xa4

    mov eax,LOADER_START_SECTOR ; 起始扇区 lba 地址 0x2 定义了loader在硬盘上的逻辑扇区地址 LBA
    mov bx,LOADER_BASE_ADDR ; 写入的地址 0x900 定义了loader在内存中的位置，MBR要把loader从硬盘读入后放到此处
    mov cx,4 ; 待读入的扇区数
    call rd_disk_m_16 ; 以下读取程序的起始部分(一个扇区)
   
    jmp LOADER_BASE_ADDR ;mbr程序结束，程序跳到laoder
   
    ;-------------------------------------------------------------------------------
    ;功能:读取硬盘 n 个扇区
    rd_disk_m_16:
    ;-------------------------------------------------------------------------------
    ; eax=LBA 扇区号
    ; bx=将数据写入的内存地址
    ; cx=读入的扇区数
    mov esi,eax ;备份 eax
    mov di,cx ;备份 cx
    ;读写硬盘:
    ;第 1 步:设置要读取的扇区数
    mov dx,0x1f2
    mov al,cl
    out dx,al ;写入的扇区数量 serctor count 
   
    mov eax,esi ;恢复 ax
   
    ;第 2 步:将 LBA 地址存入 0x1f3 ～ 0x1f6 
    
    ;LBA 地址 7～ 0 位写入端口 0x1f3
    mov dx,0x1f3;LBAlow
    out dx,al
   
    ;LBA 地址 15～ 8 位写入端口 0x1f4
    mov cl,8
    shr eax,cl
    mov dx,0x1f4
    out dx,al
   
    ;LBA 地址 23～ 16 位写入端口 0x1f5
    shr eax,cl
    mov dx,0x1f5
    out dx,al
   
    shr eax,cl
    and al,0x0f ;lba 第 24～ 27 位
    or al,0xe0 ; 设置 7～ 4 位为 1110,表示 lba 模式
    mov dx,0x1f6
    out dx,al
   
    ;第 3 步:向 0x1f7 端口写入读命令,0x20;
    mov dx,0x1f7
    mov al,0x20
    out dx,al

    ;第 4 步:检测硬盘状态
    .not_ready:
        ;同一端口,写时表示写入命令字,读时表示读入硬盘状态
        nop
        in al,dx
        and al,0x88 ;第 4 位为 1 表示硬盘控制器已准备好数据传输
        ;第 7 位为 1 表示硬盘忙
        cmp al,0x08
        jnz .not_ready ;若未准备好,继续等

        ;第 5 步:从 0x1f0 端口读数据
        mov ax, di
        mov dx, 256
        mul dx
        mov cx, ax
        ; di 为要读取的扇区数,一个扇区有 512 字节,每次读入一个字
        ; 共需 di*512/2 次,所以 di*256
        mov dx, 0x1f0
    .go_on_read:
        in ax,dx
        mov [bx],ax ;将数据写入的内存地址
        add bx,2
        loop .go_on_read
        ret

    times 510-($-$$) db 0
    db 0x55,0xaa

```
将loader.S 写入第二扇区，注意需要将count改为4，loader的数据变大了所以count 也要变大。或者可以不写
```
nasm -I include/ -o loader.bin loader.S  && dd if=./loader.bin of=/Users/isam2016/opt/bochs/hd60M.img bs=512  seek=2  conv=notrunc
```


mbr编译,写入到虚拟硬盘
```
nasm -I include/ -o mbr.bin mbr.S  && dd if=./mbr.bin of=/Users/isam2016/opt/bochs/hd60M.img bs=512 count=1  conv=notrunc
```

`bin/bochs -f bochsrc.disk`