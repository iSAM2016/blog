---
title: 计算机启动第三阶段-硬盘启动
date: 2021-10-13 14:05:50
tags:
categories:
---

# 硬盘组成
<!-- TODO:计算机组成 硬盘 -->
# 硬盘控制
硬盘控制属于IO 接口。 

让硬盘工作，我们需要通过读写硬盘控制的端口，端口的概念在学汇编的时候已经介绍过

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211013142338.jpg)

# 端口的分类
* command block registers 向硬盘驱动器写入命令字或从硬盘控制器获取硬盘状态
* control block registers 用于控制硬盘工作状态

有两个通道
* pimary(ata0)
* secondary(ata1)
  
我们之前在配置 bochs的时候有如下配置
```
ata0: enabled=1,ioaddr1=0x1f0, ioaddr2=0x3f0, irq=14
ata0-master: type=disk, path="hd60M.img", mode=flat
```
所以我们用到的是 pimary 通道

两个通道都连接的是硬盘。每一个通道后边连接的主从两块硬盘。一个要主盘，一个叫从盘

如果我们要操作某个硬盘，需要指定到某个硬盘。是通过*device 寄存器*来实现的。当然这个寄存器还有其他功能。

![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/20211013150635.jpg)

表中还有*data 寄存器*，负责数据的写入和读写

*sector count寄存器（0x1f2）*用来指定待读取或待写入的扇区数(数量)。硬盘每完成一个扇区，就会京次寄存器的值减一

# 物理扇区和逻辑扇区
硬盘中扇区在物理上是用 柱面-磁头-扇区 来定位的简称CHS, 各磁道内的扇区是以1号为起始点的，并且值咸鱼本磁道内有效。所以各个磁道扇区编号都是相同的。

但是这个对逻辑定位是不友好的，LBA定义逻辑扇区从0开始依次递增，不考虑扇区的物理位置（磁道头，磁道号），而是把他们全部组织起来统一管理进行编号，所以每个扇区都有唯一的逻辑扇区编号
* 逻辑0扇区 对应 0面0道1扇区
* 逻辑1扇区 对应 0面0道2扇区
* 逻辑16扇区 对应 0面0道17扇区
* 逻辑17扇区 对应 1面0道1扇区
* 逻辑18扇区 对应 1面0道2扇区

要注意到 扇区在编号是，是以柱面为单位的，即 先是0面0道，接着是1面0到，直到把所有盘面上的0磁道处理完，在接着处理下一个柱面

物理扇区和逻辑扇区换算公式

LBA=C * 磁头总数 * 每道扇区数 + H * 每道扇区数+ （S-1）

一个磁道512字节。知道mrb为啥需要512个字节了吗？

# LBA 寄存器
lba用28位比特来描述一个扇区的地址。最大寻址为2的28次方=268435456个扇区
* LBAlow (0x1f3)来存储28位地址的存储0-7位
* LBA mid(0x1f4) 来存储28位地址的存储8-15位
* LBA high(0x1f5) 来存储28位地址的存储16-23位

其他24-27在device寄存器中

# 其他寄存器

发出写硬盘指令，使用的是command寄存器。指令写入command 硬盘开始干活了
* 0xEC 识别硬盘
* 0x20 读扇区
* 0x30 写扇区

status寄存器是存储断硬盘的工作信息

# 向硬盘写数据流程
1. 先选择通道，往该通道的serctor count寄存器中写入待操作的扇区数
2. 往该通道上的三个LBA 寄存器写入扇区起始地址的24位
3. 往device 寄存器写入LBA地址的24-27位，并设置6位为1，让他为LBA模式。设置第四位，选择操作的硬盘（主盘，从盘）
4. 获取该通道上的status 寄存器，判断盘工作是否完成呢
5. 如果以上步骤是读硬盘，进入下一个步骤，否则，完成
6. 将硬盘数据读出

# 实验 读取1扇区数据

我们的 MBR 受限于 512 字节大小的，在那么小的空间中，没法为内核准备好环境，更没法将内核成功加载到内存并运行。所以我们要在另一个程序中完成初始化环境及加载内核的任务，这个程序我们称之为 loader，即加载器。 

Loader 会在下一节中实现。问题来了， loader 在哪里？如何跳过去执行？

这就是新款 MBR 的使命，简而言之就是，负责从硬盘上把 loader 加载到内存，并将接力棒交给它。

由于 MBR 是占据了硬盘的第 0 扇区，第 2 扇区是空闲的，可以用。
```
;主引导程序 1mbr.S
;------------------------------------------------------------
%include "boot.inc"
SECTION MBR vstart=0x7c00 ;SECTION 伪指令
    mov ax,cs
    mov ds,ax
    mov es,ax
    mov ss,ax
    mov fs,ax
    mov sp,0x7c00
    mov ax,0xb800
    mov gs,ax

    ; 清屏利用0x06号功能，上卷全部行，则可清屏
    ; -----------------------------------------------------------
    ;INT 0x10   功能号:0x06	   功能描述:上卷窗口
    ;------------------------------------------------------
    ;输入
    ;AH 功能号= 0x06
    ;AL = 上卷行数(如果为零,表示全部)
    ;BH = 上卷行属性
    ;(CL,CH) =窗口左上角的(X,Y)位置
    ;(DL,DH) = 窗口右下角(X,Y)位置
    ;无返回值
    mov     ax, 0x600
    mov     bx, 0x700
    mov     cx, 0        ; 左上角: (0, 0)
    mov     dx, 0x184f	; 右下角: (80,25),
                        ; 80个字符 25行
                        ; 从下标0开始,所以0x18=24,0x4f=79
    int     0x10         ; int 0x10


    ;;;;;;;;;   输出背景色为绿色，前景色为红色，并且跳动的字符串字符 “1 MBR”    ;;;;;;;;;
    mov byte [gs:0x00], '1'
    mov byte [gs:0x01], 0xa4

    mov byte [gs:0x02], ' '
    mov byte [gs:0x04], 0xa4

    mov byte [gs:0x04], 'M'
    mov byte [gs:0x05], 0xa4

    mov byte [gs:0x06], 'B'
    mov byte [gs:0x07], 0xa4

    mov byte [gs:0x08], 'R'
    mov byte [gs:0x09], 0xa4

    mov eax,LOADER_START_SECTOR ; 起始扇区 lba 地址 0x2 定义了loader在硬盘上的逻辑扇区地址 LBA
    mov bx,LOADER_BASE_ADDR ; 写入的地址 0x900 定义了loader在内存中的位置，MBR要把loader从硬盘读入后放到此处
    mov cx,1 ; 待读入的扇区数
    call rd_disk_m_16 ; 以下读取程序的起始部分(一个扇区)
   
    jmp LOADER_BASE_ADDR ;mbr程序结束，程序跳到laoder
   
    ;-------------------------------------------------------------------------------
    ;功能:读取硬盘 n 个扇区
    rd_disk_m_16:
    ;-------------------------------------------------------------------------------
    ; eax=LBA 扇区号
    ; bx=将数据写入的内存地址
    ; cx=读入的扇区数
    mov esi,eax ;备份 eax
    mov di,cx ;备份 cx
    ;读写硬盘:
    ;第 1 步:设置要读取的扇区数
    mov dx,0x1f2
    mov al,cl
    out dx,al ;写入的扇区数量 serctor count 
   
    mov eax,esi ;恢复 ax
   
    ;第 2 步:将 LBA 地址存入 0x1f3 ～ 0x1f6 
    
    ;LBA 地址 7～ 0 位写入端口 0x1f3
    mov dx,0x1f3;LBAlow
    out dx,al
   
    ;LBA 地址 15～ 8 位写入端口 0x1f4
    mov cl,8
    shr eax,cl
    mov dx,0x1f4
    out dx,al
   
    ;LBA 地址 23～ 16 位写入端口 0x1f5
    shr eax,cl
    mov dx,0x1f5
    out dx,al
   
    shr eax,cl
    and al,0x0f ;lba 第 24～ 27 位
    or al,0xe0 ; 设置 7～ 4 位为 1110,表示 lba 模式
    mov dx,0x1f6
    out dx,al
   
    ;第 3 步:向 0x1f7 端口写入读命令,0x20;
    mov dx,0x1f7
    mov al,0x20
    out dx,al

    ;第 4 步:检测硬盘状态
    .not_ready:
        ;同一端口,写时表示写入命令字,读时表示读入硬盘状态
        nop
        in al,dx
        and al,0x88 ;第 4 位为 1 表示硬盘控制器已准备好数据传输
        ;第 7 位为 1 表示硬盘忙
        cmp al,0x08
        jnz .not_ready ;若未准备好,继续等

        ;第 5 步:从 0x1f0 端口读数据
        mov ax, di
        mov dx, 256
        mul dx
        mov cx, ax
        ; di 为要读取的扇区数,一个扇区有 512 字节,每次读入一个字
        ; 共需 di*512/2 次,所以 di*256
        mov dx, 0x1f0
    .go_on_read:
        in ax,dx
        mov [bx],ax ;将数据写入的内存地址
        add bx,2
        loop .go_on_read
        ret

    times 510-($-$$) db 0
    db 0x55,0xaa
```
编译,写入到虚拟硬盘
```
nasm -I include/ -o mbr.bin mbr1.S  && dd if=./mbr.bin of=/Users/isam2016/opt/bochs/hd60M.img bs=512 count=1  conv=notrunc
```

实现内核加载器
```
%include "boot.inc"
section loader vstart=LOADER_BASE_ADDR

; 输出背景色绿色,前景色红色,并且跳动的字符串"1 MBR"
 mov byte [gs:0x00],'2'
 mov byte [gs:0x01],0xA4 ; A 表示绿色背景闪烁， 4 表示前景色为红色

 mov byte [gs:0x02],' '
 mov byte [gs:0x03],0xA4

 mov byte [gs:0x04],'L'
 mov byte [gs:0x05],0xA4

 mov byte [gs:0x06],'O'
 mov byte [gs:0x07],0xA4

 mov byte [gs:0x08],'A'
 mov byte [gs:0x09],0xA4

 mov byte [gs:0x0a],'D'
 mov byte [gs:0x0b],0xA4

 mov byte [gs:0x0c],'E'
 mov byte [gs:0x0d],0xA4

 mov byte [gs:0x0e],'R'
 mov byte [gs:0x0f],0xA4

 jmp $ ; 通过死循环使程序悬停在此
```

编译,写入到第2扇区。
```
nasm -I include/ -o loader.bin loader.S  && dd if=./loader.bin of=/Users/isam2016/opt/bochs/hd60M.img bs=512 count=1 seek=2  conv=notrunc
```

开启测试执行`bochs`
![](https://isam2016hexo.oss-cn-hangzhou.aliyuncs.com/img/17.38.15.gif)